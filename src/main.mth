# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

import base/nat
import base/list
import base/str
import base/path
import base/io
import base/panic

import mirth/build
import mirth/mtest

export target
  type +IO
  mirth : List(Str) -- Int +IO
end

mirth-build : List(Str) -- Int +IO
mirth-mtest : List(Str) -- Int +IO
mirth-help  : List(Str) -- Int +IO

# entry point for bootstrap
main : List(Str) -- Int +IO
main = mirth

# entry point
mirth : List(Str) -- Int +IO
mirth =
  cond(
    nil? -> drop "USAGE: mirth COMMAND" trace 1,
    no_panic_12(unsnoc) swap cond(
      dup "build"  streq -> drop mirth-build,
      dup "mtest"  streq -> drop mirth-mtest,
      dup "help"   streq -> drop mirth-help,
      dup "--help" streq -> drop mirth-help,
      dup "-h"     streq -> drop mirth-help,
      nip "mirth: Unknown command " swap <>
      ". Use 'mirth --help' to list available commands." <> trace 1
    )
  )

# mirth build [PACKAGE]
mirth-build : List(Str) -- Int +IO
mirth-build =
  cond(
    nil? -> drop "src" path buildPath,
    len? n2 n>= -> drop "USAGE: mirth build [PKG]" trace 1,
    no_panic_12(unsnoc) drop path buildPath
  )

# mirth mtest
mirth-mtest : List(Str) -- Int +IO
mirth-mtest =
  lmatchR(
    false runTests,
    cond(
      dup "--update" streq -> drop2 true runTests,
      drop2 "USAGE: mirth mtest [--update]" trace 1
    )
  )

# mirth -h, mirth --help, mirth help
mirth-help : List(Str) -- Int +IO
mirth-help = $(
  drop
  "USAGE: mirth COMMAND"
  "OPTIONS\n"                               <>
  "    --help      Display this table.\n"   <>
  "\n"                                      <>
  "COMMANDS\n"                              <>
  "    build       Build Mirth package.\n"  <>
  print 0
)

