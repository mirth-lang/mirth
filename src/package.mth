module(mirth.package)

import(std.prelude)
import(std.path)
import(std.maybe)
import(std.str)
import(std.list)
import(std.posix)
import(mirth.name)
import(mirth.def)

table(Package)
field(Package.~name, Package, Name)
field(Package.~path, Package, Maybe(Path))

def(Package.name, Package -- Name, ~name @)
def(Package.qname, Package -- QName, NAMESPACE_ROOT swap name QNAME0)
def(Package.path, Package -- Maybe(Path), ~path @)

var(PACKAGE_SEARCH_PATH, List(Path))

def(Package.path-or-search, +World Package -- +World Maybe(Path),
    dup path match(
        Some -> nip Some,
        None ->
            PACKAGE_SEARCH_PATH memoize("lib" Path L1)
            map(over name >Str >Path join)
            find(dup is-directory?)
            tuck swap ~path !
    ))

def(Package.path!, Path Package --,
    tuck path match(
        Some ->
            dup2 == if(
                drop3,
                Str(
                    "Tried to set different path for the same package." ;
                    "\n\tPackage: " ; rotl name >Str ;
                    "\n\tPath 1: " ; swap /Path show;
                    "\n\tPath 2: " ; /Path show;
                ) panic!
            ),
        None ->
            Some swap ~path !
    ))

def(Package.new!, Maybe(Path) Name -- Package,
    Package.alloc!
    tuck ~name !
    tuck ~path !
    dup DefPackage register)

def(Package.new-or-path!, Path Name -- Package,
    dup Package.find match(
        Some -> nip tuck path!,
        None -> dip(Some) Package.new!
    ))

def(Package.find, Name -- Maybe(Package),
    NAMESPACE_ROOT swap QNAME0 def? bind(package?))

def(Package.find-or-new!, Name -- Package,
    dup Package.find unwrap-or(dup None swap Package.new!) nip)

def(Package.==, Package Package -- Bool, both(index) ==)
