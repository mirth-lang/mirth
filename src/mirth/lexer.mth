module(mirth.lexer)
import(prelude)
import(data.char)
import(data.stack)
import(mirth.data.name)
import(mirth.data.token)
import(mirth.data.error)
import(mirth.input)

#########
# LEXER #
#########

var(lexer-module, Module)
var(lexer-row, Row)
var(lexer-col, Col)
var(lexer-stack, Stack(Token))

def(run-lexer!, Path -- Module +Lexer,
    module-new! lexer-module !

    lexer-module @ module-source-path
    Path->Str open-file! input-start!

    1 Int->Row lexer-row !
    1 Int->Col lexer-col !

    token-alloc!

    while(lexer-done? not, lexer-next!)

    input-end!

    lexer-stack stack-pop! match(
        NONE -> TOKEN_NONE lexer-emit!,
        SOME -> "Mismatched left parenthesis." emit-fatal-error!
    )

    token-alloc! lexer-module @ module-end !
    token-succ lexer-module @ module-start !

    lexer-module @)


# Is the lexer done?
def(lexer-done?, Bool +Lexer, input-done?)

# Create a token, and add it to the token buffer,
# returning the new token.
def(lexer-make!, TokenValue -- Token,
    token-alloc!
    tuck token-value !
    lexer-module @ over token-module !
    lexer-row @ over token-row !
    lexer-col @ over token-col !)

# Create a token, and add it to the token buffer.
def(lexer-emit!, TokenValue --, lexer-make! drop)

# Lex the next token.
def(lexer-next!, --,
    lexer-peek

    char-valid? not if(
        "invalid character" lexer-emit-fatal-error!,

    is-name-char? if(
        drop
        lexer-emit-name!,

    is-newline? if(
        drop
        lexer-newline!,

    is-whitespace? if(
        drop,

    is-pound? if(
        drop
        lexer-skip-comment!,

    is-comma? if(
        drop
        TOKEN_COMMA lexer-emit!,

    is-lparen? if(
        drop
        lexer-emit-lparen!,

    is-rparen? if(
        drop
        lexer-emit-rparen!,

    is-lsquare? if(
        drop
        lexer-emit-lsquare!,

    is-rsquare? if(
        drop
        lexer-emit-rsquare!,

    is-lcurly? if(
        drop
        lexer-emit-lcurly!,

    is-rcurly? if(
        drop
        lexer-emit-rcurly!,

    is-quote? if(
        drop
        lexer-emit-string!,

        "unrecognized token" lexer-emit-fatal-error!
    )))))))))))

    lexer-move!)))

def(lexer-newline!, +Lexer,
    lexer-row modify(Row->Int 1+ Int->Row)
    0 Int->Col lexer-col !)

def(lexer-emit-lparen!, +Lexer,
    nil TOKEN_LPAREN lexer-make!
    lexer-stack stack-push!)
def(lexer-emit-rparen!, +Lexer,
    lexer-stack stack-pop! match(
        NONE -> "Mismatched right parenthesis." lexer-emit-fatal-error!,
        SOME -> token-is-lparen? if(
            dup TOKEN_RPAREN lexer-make!
            TOKEN_LPAREN swap token-value !,
            "Mismatched right parenthesis." lexer-emit-fatal-error!
        )
    ))

def(lexer-emit-lsquare!, +Lexer,
    nil TOKEN_LSQUARE lexer-make!
    lexer-stack stack-push!)
def(lexer-emit-rsquare!, +Lexer,
    lexer-stack stack-pop! match(
        NONE -> "Mismatched right bracket." lexer-emit-fatal-error!,
        SOME -> token-is-lsquare? if(
            dup TOKEN_RSQUARE lexer-make!
            TOKEN_LSQUARE swap token-value !,
            "Mismatched right bracket." lexer-emit-fatal-error!
        )
    ))

def(lexer-emit-lcurly!, +Lexer,
    nil TOKEN_LCURLY lexer-make!
    lexer-stack stack-push!)
def(lexer-emit-rcurly!, +Lexer,
    lexer-stack stack-pop! match(
        NONE -> "Mismatched right brace." lexer-emit-fatal-error!,
        SOME -> token-is-lcurly? if(
            dup TOKEN_RCURLY lexer-make!
            TOKEN_LCURLY swap token-value !,
            "Mismatched right brace." lexer-emit-fatal-error!
        )
    ))

def(lexer-emit-name!, +Lexer,
    str-buf-clear!

    lexer-module @
    lexer-row @
    lexer-col @

    lexer-peek while(is-name-char?,
        char-valid? if(
            str-buf-push-char!
            lexer-move!
            lexer-peek,

            "invalid character" lexer-emit-fatal-error!
        )
    )
    drop

    str-buf-is-doc-start? if(
        drop3
        lexer-skip-doc!,
        # TODO: save the doc in the token-doc field for the next token

        str-buf-is-int? if(
            str-buf-int? TOKEN_INT,
            str-buf-dup! name-new! TOKEN_NAME
        )

        token-alloc!
        tuck token-value !
        tuck token-col !
        tuck token-row !
        token-module !
    ))

def(str-buf-is-doc-start?, Bool +StrBuf,
    str-buf-length? 3 == and(
        0 str-buf-char@ is-pipe? nip
        and(1 str-buf-char@ is-pipe? nip)
        and(2 str-buf-char@ is-pipe? nip)
    ))

def(str-buf-is-arrow?, Bool +StrBuf,
    str-buf-length? 2 == and(
        0 str-buf-char@ is-dash? nip
        1 str-buf-char@ is-gt? nip &&
    ))

def(str-buf-is-dashes?, Bool +StrBuf,
    str-buf-length? 2 == if(
        0 str-buf-char@ is-dash? nip
        1 str-buf-char@ is-dash? nip &&,
        false
    ))

def(str-buf-is-equal?, Bool +StrBuf,
    str-buf-length? 1 == if(
        0 str-buf-char@ is-eq? nip,
        false
    ))


def(str-buf-is-int?, Bool +StrBuf,
    str-buf-is-dec-int? if(true, str-buf-is-hex-int?))

def(str-buf-is-dec-int?, Bool +StrBuf,
    0 # number of digits
    0 # current index
    dup str-buf-char@ is-sign? nip if(
        1+,
        id
    )
    while(dup str-buf-char@ is-digit? nip,
        dip(1+) 1+)
    swap 1 >= if(
        str-buf-length? ==,
        drop false
    ))

def(is-zero-char, Char -- Bool, Char->Int 48 ==)
def(is-xX-char, Char -- Bool, Char->Int dup 88 == dip(120 ==) ||)

def(str-buf-is-hex-int?, Bool +StrBuf,
    0 # number of digits
    0 # current index
    dup str-buf-char@ is-sign? nip if(
        1+,
        id
    )
    dup str-buf-char@ is-zero-char if(
        1+
        dup str-buf-char@ is-xX-char if(
            1+
            while(dup str-buf-char@ is-hexdigit? nip, dip(1+) 1+)
            swap 1 >= if(
                str-buf-length? ==,
                drop false
            ),

            drop2 false
        ),

        drop2 false
    ))

def(str-buf-int?, Int +StrBuf,
    str-buf-is-dec-int? if(
        str-buf-dec-int?,
        str-buf-hex-int?
    ))

def(str-buf-dec-int?, Int +StrBuf,
    1 # sign multiplier
    0 # accumulated value
    0 # current index
    dup str-buf-char@ is-sign? if(
        is-dash? nip if(
            dip2(drop -1),
            id
        )
        1+,
        drop
    )
    while(dup str-buf-length? <,
        sip(
            str-buf-char@ Char->Int
            dip(10 *) 48 - +
        )
        1+)
    drop *)

def(str-buf-hex-int?, Int +StrBuf,
    1 # sign multiplier
    0 # accumulated value
    0 # current index
    dup str-buf-char@ is-sign? if(
        is-dash? nip if(
            dip2(drop -1),
            id
        )
        1+,
        drop
    )
    2 + # skip 0x prefix
    while(dup str-buf-length? <,
        sip(
            str-buf-char@
            dip(16 *) hexdigit-value +
        )
        1+)
    drop *)

def(hexdigit-value, Char -- Int,
    is-digit? if(
        Char->Int 48 -,
        is-upper-hexdigit? if(
            Char->Int 55 -,
            Char->Int 87 -
        )
    ))

def(lexer-emit-string!, +Lexer,
    str-buf-clear!
    TOKEN_NONE lexer-make!

    lexer-move!
    lexer-peek while(is-string-end? not,
        char-valid? if(
            lexer-push-string-char!
            lexer-move!
            lexer-peek,

            "invalid character in string literal" lexer-emit-fatal-error!
        )
    )
    drop

    str-buf-dup! TOKEN_STR
    swap token-value !)

def(lexer-push-string-char!, Char -- +Lexer,
    is-backslash? if(
        drop lexer-move! lexer-peek

        is-newline? if(
            drop,

        is-n? if(
            drop 10 Int->Char str-buf-push-char!,

        is-r? if(
            drop 13 Int->Char str-buf-push-char!,

        is-t? if(
            drop 9 Int->Char str-buf-push-char!,

        is-quote? if(
            str-buf-push-char!,

        is-backslash? if(
            str-buf-push-char!,

            str-buf-push-char!

            "Unknown character escape sequence."
            lexer-emit-warning!

        )))))),

        str-buf-push-char!
    ))

def(lexer-skip-comment!, --,
    while(lexer-comment-end? not, lexer-move!)
    lexer-peek is-newline? if(lexer-newline!, id) drop)

def(lexer-skip-doc!, +Lexer, while(lexer-comment-end? not, lexer-move!))

def(lexer-comment-end?, Bool +Lexer,
    lexer-done? if(
        true,
        lexer-peek is-newline? nip
    ))

def(lexer-peek, Char +Lexer, input-peek)
def(lexer-move!, +Lexer, input-move!
    lexer-col modify(Col->Int 1+ Int->Col))

def(lexer-location, Location +Lexer,
    lexer-module @ lexer-row @ lexer-col @ location-pack)

def(lexer-emit-warning!, Str -- +IO, dip(lexer-location) emit-warning-at!)
def(lexer-emit-error!, Str -- +IO, dip(lexer-location) emit-error-at!)
def(lexer-emit-fatal-error!, *a Str -- *b +IO, dip(lexer-location) emit-fatal-error-at!)
