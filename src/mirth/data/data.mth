module(mirth/data/data)
import(mirth/data/name)
import(mirth/elab/syntax)

#######
# TAG #
#######

NameValue->Tag : NameValue -- Tag
NameValue->Tag = cast

Tag->NameValue : Tag -- NameValue
Tag->NameValue = cast

Tag->Int : Tag -- Int
Tag->Int = cast

name-tag! : Tag Name --
name-tag! = (
    DEF_TAG over name-sort!
    dip(Tag->NameValue) name-value!
)

name-tag@ : Name -- Tag
name-tag@ =
    name-is-tag? if(
        name-value@ NameValue->Tag,
        "compiler error: attempted to get tag definition for non-tag name" panic!
    )

name-tag? : Name -- Name Tag
name-tag? = dup name-tag@

tag-nil : Tag
tag-nil = 0 cast

tag-is-nil : Tag -- Bool
tag-is-nil = tag-nil ==

tag-is-nil? : Tag -- Tag Bool
tag-is-nil? = dup tag-is-nil

tag-num-inputs? : Tag -- Tag Int
tag-num-inputs? =
    # TODO: this should actually be calculated & stored during elaboration based on the type
    tag-has-sig? if(
        tag-sig? 0 swap
        while(token-run-end? not, token-next dip(1+)) drop,
        0
    )

########
# DATA #
########

data-num-tags : Data -- Int
data-num-tags = (
    0 swap data-tags@
    while(
        tag-is-nil? not,
        dip(1+) tag-next@
    )
    drop
)

data-add-tag! : Tag Data -- +Mirth
data-add-tag! =
    data-tags? tag-is-nil if(
        data-tags!,
        data-tags@
        while(tag-next? tag-is-nil not, tag-next@)
        tag-next!
    )

#########
# MATCH #
#########

match-is-empty? : Match -- Match Bool
match-is-empty? = match-cases? case-is-nil

match-num-cases? : Match -- Match Int
match-num-cases? = (
    match-cases? dip(0)
    while(
        case-is-nil? not,
        case-next@ dip(1+)
    )
    drop
)

match-add-case! : Match Case -- Match
match-add-case! =
    over match-cases? case-is-nil if(
        match-cases!,
        match-cases@ swap case-add-case!
    )

match-has-default-case : Match -- Bool
match-has-default-case = match-cases@ cases-have-default-case

match-has-default-case? : Match -- Match Bool
match-has-default-case? = dup match-has-default-case

||| Get type we're matching over.
match-scrutinee-type? : Match -- Match Type
match-scrutinee-type? = match-dom? type-expand-tensor tensor-type-snd@

||| Is this a pattern match over a "data" type?
match-scrutinee-is-data? : Match -- Match Bool
match-scrutinee-is-data? = match-scrutinee-type? type-head type-is-data

||| Get "data" associated with scrutinee.
match-scrutinee-data? : Match -- Match Data
match-scrutinee-data? = match-scrutinee-type? type-head type-get-data

########
# CASE #
########

case-nil : Case
case-nil = 0 cast

case-is-nil : Case -- Bool
case-is-nil = case-nil ==

case-is-nil? : Case -- Case Bool
case-is-nil? = dup case-is-nil

||| Add the second case at the end of the list of the first case.
||| Raises an error if case is unreachable.
||| O(n^2) time, sadly, where n is the number of cases.
case-add-case! : Case Case --
case-add-case! =
    case-is-covered? if(
        case-token@ "Case is unreachable." emit-error! drop,
        swap case-next? case-is-nil if(
            case-next!,
            case-next@ swap case-add-case!
        )
    )

case-is-covered : Case Case -- Bool
case-is-covered = dip(case-pattern@) case-pattern@ pattern-is-covered

case-is-covered? : Case Case -- Case Case Bool
case-is-covered? = dup2 case-is-covered

cases-have-default-case : Case -- Bool
cases-have-default-case =
    case-is-nil? if(
        drop false,
        case-is-default-case? if(
            drop true,
            case-next@ cases-have-default-case
        )
    )

case-is-default-case? : Case -- Case Bool
case-is-default-case? = dup case-is-default-case

case-is-default-case : Case -- Bool
case-is-default-case = case-pattern@ pattern-is-underscore

###########
# PATTERN #
###########

Tag->Pattern : Tag -- Pattern
Tag->Pattern = Pattern.wrap

Pattern->Tag : Pattern -- Tag
Pattern->Tag =
    pattern-is-underscore? if(
        "compiler error: tried to call Pattern->Tag on underscore pattern" panic!,
        Pattern.unwrap
    )

pattern-underscore : Pattern
pattern-underscore = 0 cast

pattern-is-underscore : Pattern -- Bool
pattern-is-underscore = pattern-underscore ==

pattern-is-underscore? : Pattern -- Pattern Bool
pattern-is-underscore? = dup pattern-is-underscore

pattern-is-covered : Pattern Pattern -- Bool
pattern-is-covered = swap pattern-is-underscore? if(drop2 true, ==)

pattern-is-covered? : Pattern Pattern -- Pattern Pattern Bool
pattern-is-covered? = dup2 pattern-is-covered
