module(mirth.data.module)

import(prelude)
import(data.path)
import(data.str)
import(data.list)
import(data.byte)
import(mirth.data.name)

# mirth/data/module
table(Module)
field(Module~name, Module, Name)
field(Module~path, Module, Path)
field(Module~start, Module, Token)
field(Module~end, Module, Token)
field(Module~imports, Module, List(Module))

def(Module.name, Module -- Name, ~name @)
def(Module.path, Module -- Path, ~path @)
def(Module.start, Module -- Token, ~start @)
def(Module.end, Module -- Token, ~end @)
def(Module.imports, Module -- List(Module), ~imports @)

def(Module=, Module Module -- Bool, both(.id) =)

def(Module.new!, Path -- Module,
    Module.alloc!
    L0 over ~imports !
    tuck ~path !)

||| ( module1 module2 -- ) Add module2 as import to module1.
||| This does not check if the import is strictly necessary.
def(Module.add-import!, Module Module --,
    swap ~imports modify(cons))

||| Get source path associated with module.
def(Module.source-path, Module -- Path,
    path to-source-path)

def(module-path-from-name, Name -- Path,
    build-str!(
        >Str str-bytes-for(
            dup B'.' = if(
                drop B'/',
                dup is-upper if(to-lower, id)
            )
            str-buf-push-byte!
        )
        ".mth" str-buf-push-str!
    ) >Path)

||| ( module1 module2 -- bool ) Check whether module1 is visible in module2.
def(Module.visible, Module Module -- Bool,
    dup2 = if(
        drop2 T, # Module is always visible to itself.
        imports any(dup2 =) nip
    ))
