module(mirth.data.def)

import(prelude)
import(data.maybe)
import(mirth.data.name)
import(mirth.data.alias)
import(mirth.data.type)
import(mirth.data.data)
import(mirth.data.token)
import(mirth.data.word)
import(mirth.data.external)
import(mirth.data.table)
import(mirth.data.variable)
import(mirth.data.constant)
import(mirth.data.buffer)

data(Def,
    DEF_NONE,
    DEF_ALIAS -> Alias,
    DEF_MODULE -> Module,
    DEF_TYPE -> Type,
    DEF_TAG -> Tag,
    DEF_PRIM -> Prim,
    DEF_WORD -> Word,
    DEF_BUFFER -> Buffer,
    DEF_VARIABLE -> Variable,
    DEF_CONSTANT -> Constant,
    DEF_EXTERNAL -> External,
    DEF_FIELD -> Field)

def(Def>Module?, Def -- Maybe(Module),
    DEF_NONE -> NONE,
    DEF_ALIAS -> head .module SOME,
    DEF_MODULE -> drop NONE,
    DEF_TYPE -> drop NONE,
    DEF_TAG -> .data head? map(.module),
    DEF_PRIM -> drop NONE,
    DEF_WORD -> head .module SOME,
    DEF_BUFFER -> head .module SOME,
    DEF_VARIABLE -> head .module SOME,
    DEF_CONSTANT -> head .module SOME,
    DEF_EXTERNAL -> sig .module SOME,
    DEF_FIELD -> head .module SOME)

def(Def.none?, Def -- Bool, DEF_NONE -> T, _ -> drop F)
def(Def.alias?, Def -- Maybe(Alias), DEF_ALIAS -> SOME, _ -> drop NONE)
def(Def.module?, Def -- Maybe(Module), DEF_MODULE -> SOME, _ -> drop NONE)
def(Def.type?, Def -- Maybe(Type), DEF_TYPE -> SOME, _ -> drop NONE)
def(Def.tag?, Def -- Maybe(Tag), DEF_TAG -> SOME, _ -> drop NONE)
def(Def.prim?, Def -- Maybe(Prim), DEF_PRIM -> SOME, _ -> drop NONE)
def(Def.word?, Def -- Maybe(Word), DEF_WORD -> SOME, _ -> drop NONE)
def(Def.buffer?, Def -- Maybe(Buffer), DEF_BUFFER -> SOME, _ -> drop NONE)
def(Def.variable?, Def -- Maybe(Variable), DEF_VARIABLE -> SOME, _ -> drop NONE)
def(Def.constant?, Def -- Maybe(Constant), DEF_CONSTANT -> SOME, _ -> drop NONE)
def(Def.external?, Def -- Maybe(External), DEF_EXTERNAL -> SOME, _ -> drop NONE)
def(Def.field?, Def -- Maybe(Field), DEF_FIELD -> SOME, _ -> drop NONE)

def(Def.typecheck!, Def --,
    DEF_NONE -> id,
    DEF_ALIAS -> target >Def typecheck!, # TODO verify it's not DEF_NONE!!
    DEF_MODULE -> drop,
    DEF_BUFFER -> drop,
    DEF_PRIM -> drop,
    DEF_TYPE -> drop,
    DEF_EXTERNAL -> type drop,
    DEF_WORD -> arrow drop,
    DEF_FIELD -> type drop,
    DEF_TAG -> type drop,
    DEF_VARIABLE -> type drop,
    DEF_CONSTANT -> drop)
