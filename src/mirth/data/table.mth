module(mirth/data/table)
import(mirth/data/common)
import(mirth/data/buffer)
import(mirth/data/type)

#########
# TABLE #
#########

def(TABLE_MAX_SIZE, Int, 0x20000)

def(table-new!, Name -- Table,
    table-alloc!
    dup2 TTable swap name-type!
    tuck table-name!

    TABLE_MAX_SIZE over table-max-count!

    # Table.MAX
    table-name? name-load! ".MAX" str-buf-push-str! name-save!
    word-alloc!
    dup2 swap name-word!
    tuck word-name!

    TYPE_UNIT
    TYPE_UNIT TYPE_INT TTensor
    TMorphism
    over word-type!
    true over word-sig-is-checked!

    TABLE_MAX_SIZE arrow-new-int!
    over word-arrow!
    true swap word-body-is-checked!

    # Table.NUM
    table-name? name-load! ".NUM" str-buf-push-str! name-save!
    8 buffer-alloc!
    over table-num-buffer!

    # Table.alloc!
    table-name? name-load! ".alloc!" str-buf-push-str! name-save!
    word-alloc!
    dup2 swap name-word!
    tuck word-name!

    over dip(T0) TTable T1 T->
    over word-type!
    true over word-sig-is-checked!

    # TODO: perform bounds check in Table.alloc!
    over table-num-buffer@ dup
    dip(
        arrow-new-buffer!
        dup2 swap word-arrow!
        PRIM_INT_GET arrow-push-prim!
        1 arrow-push-int!
        PRIM_INT_ADD arrow-push-prim!
        PRIM_CORE_DUP arrow-push-prim!
    )
    arrow-push-buffer!
    PRIM_INT_SET arrow-push-prim!
    drop true swap word-body-is-checked!)

#########
# FIELD #
#########

def(Field->Int, Field -- Int, cast)
def(field-alloc!, Field,
    Field.alloc!
    dup Field->Int Field.MAX >= if(
        "compiler error: NUM_FIELDS >= MAX_FIELDS" panic!,
        id
    ))

def(field-new!, Name Type Type -- Field,
    field-alloc!
    tuck field-type!
    tuck field-table!
    tuck field-name!

    # define the buffer for the field
    field-name? name-load! "-buffer" str-buf-push-str! name-save!
    over field-table@ type-max-count? match(
        NONE -> "compiler error: attempted to create field for invalid type" panic!,
        SOME -> nip
    ) 16 *
    buffer-alloc! over field-buffer!

    # field&
    field-name? name-load! "&" str-buf-push-str! name-save!
    word-alloc!
    dup2 swap name-word!
    tuck word-name!

    over field-table@
    TYPE_UNIT swap TTensor
    TYPE_UNIT TYPE_PTR TTensor
    TMorphism
    over word-type!
    true over word-sig-is-checked!

    # FIXME: need a better arrow-building inteface that tracks types
    #  i.e. a better elaborator API.
    16 arrow-new-int!
    dup2 swap word-arrow!
    PRIM_INT_MUL arrow-push-prim!
    dip(over field-buffer@) swap arrow-push-buffer!
    PRIM_PTR_ADD arrow-push-prim!
    drop true over word-body-is-checked!
    over field-word-ptr!

    # field!
    field-name? name-load! "!" str-buf-push-str! name-save!
    word-alloc!
    dup2 swap name-word!
    tuck word-name!

    over field-table?
    dip(field-type@ dip(TYPE_UNIT) TTensor) TTensor
    TYPE_UNIT TMorphism
    over word-type!
    true over word-sig-is-checked!

    over field-word-ptr@ arrow-new-word!
    dup2 swap word-arrow!
    PRIM_VALUE_SET arrow-push-prim!
    drop true swap word-body-is-checked!

    # field@
    field-name? name-load! "@" str-buf-push-str! name-save!
    word-alloc!
    dup2 swap name-word!
    tuck word-name!

    over field-table? swap field-type@
    dip(TYPE_UNIT swap TTensor)
    TYPE_UNIT swap TTensor
    TMorphism
    over word-type!
    true over word-sig-is-checked!

    over field-word-ptr@ arrow-new-word!
    dup2 swap word-arrow!
    PRIM_VALUE_GET arrow-push-prim!
    drop true swap word-body-is-checked!

    # field?
    field-name? name-load! "?" str-buf-push-str! name-save!
    word-alloc!
    dup2 swap name-word!
    tuck word-name!

    over field-table? swap field-type@
    dip(TYPE_UNIT swap TTensor)
    dip(dup) TTensor
    TMorphism
    over word-type!
    true over word-sig-is-checked!

    over field-word-ptr@
    dip(
        PRIM_CORE_DUP arrow-new-prim!
        dup2 swap word-arrow!
    )
    arrow-push-word!
    PRIM_VALUE_GET arrow-push-prim!
    drop true swap word-body-is-checked!)

############
### test ###
############
table(TestTable)
def(TestTable.MAX', Size, TestTable.MAX)
def(TestTable.NUM', Ptr, TestTable.NUM)
def(TestTable.alloc!', TestTable, TestTable.alloc!)

field(test-table-int, TestTable, Int)
def(test-table-int-buffer', Ptr, test-table-int-buffer)
def(test-table-int&', TestTable -- Ptr, test-table-int&)
def(test-table-int@', TestTable -- Int, test-table-int@)
def(test-table-int!', Int TestTable --, test-table-int!)
