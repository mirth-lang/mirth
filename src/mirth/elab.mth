module(mirth/elab)
import(mirth/data/name)
import(mirth/data/token)
import(mirth/data/type)
import(mirth/data/table)
import(mirth/data/data)

####################
# Type Elaboration #
####################

nominal(HolesAllowed, Bool)
def(HolesAllowed->Bool, HolesAllowed -- Bool, HolesAllowed.unwrap)
def(allow-holes, HolesAllowed, true HolesAllowed.wrap)
def(forbid-holes, HolesAllowed, false HolesAllowed.wrap)

nominal(TypeElab, [HolesAllowed Ctx])
def(type-elab-default, TypeElab, forbid-holes ctx-empty pack2 TypeElab.wrap)
def(type-elab-stack-assertion, Ctx -- TypeElab, dip(allow-holes) pack2 TypeElab.wrap)
def(type-elab-holes-allowed?, TypeElab -- TypeElab HolesAllowed, dup TypeElab.unwrap .1)
def(type-elab-ctx, TypeElab -- Ctx, TypeElab.unwrap .0)
def(type-elab-ctx?, TypeElab -- TypeElab Ctx, dup type-elab-ctx)
def(type-elab-holes-allowed!, TypeElab HolesAllowed -- TypeElab,
    dip(TypeElab.unwrap) .1! TypeElab.wrap)
def(type-elab-ctx!, TypeElab Ctx -- TypeElab,
    dip(TypeElab.unwrap) .0! TypeElab.wrap)

def(elab-type-sig!, TypeElab Token -- TypeElab Type Token,
    elab-type-stack!
    token-is-dashes? if(
        token-next
        swap dip(elab-type-stack!) swap,
        dip(TYPE_UNIT))
    dip(swap morphism-type-new! TMorphism)
    ) # TODO: elaborate effect type too

def(elab-type-stack!, TypeElab Token -- TypeElab Type Token,
    sig-token-is-stack-var? if(
        elab-stack-var! dip(TVar),
        dip(TYPE_UNIT)
    )
    elab-type-stack-rest!)

def(elab-type-stack-rest!, TypeElab Type Token -- TypeElab Type Token,
    while(sig-is-stack-end2? not,
        swap dip(elab-type-atom!) swap
        dip(swap tensor-type-new! TTensor)
    ))

def(elab-type-arg!, TypeElab Token -- TypeElab Type Token,
    elab-type-atom!
    token-is-arg-end? if(
        id,
        "Unexpected token after type." emit-fatal-error!
    ))

def(elab-type-atom!, TypeElab Token -- TypeElab Type Token,
    sig-token-is-type-var? if(
        elab-type-var! dip(TVar),

    sig-token-is-type-con? if(
        elab-type-con!,

    sig-token-is-type-dont-care? if(
        elab-type-dont-care!,

    sig-token-is-type-hole? if(
        elab-type-hole!,

    token-is-lbracket? if(
        elab-type-quote!,

        dup "Expected type, got unknown token." emit-error!
        dip(TYPE_ERROR) token-next
    ))))))

def(elab-stack-var!, TypeElab Token -- TypeElab Var Token,
    TYPE_STACK elab-implicit-var!)

def(elab-type-var!, TypeElab Token -- TypeElab Var Token,
    TYPE_TYPE elab-implicit-var!)

def(elab-implicit-var!, TypeElab Token Type -- TypeElab Var Token,
    dip(dip(type-elab-ctx?))
    over
    dip(dip(
        token-name@
        dup2 swap ctx-has-name
    ))
    rotl if(
        dip(dip(over ctx-lookup var-type?))
        elab-type-unify! nip,

        dip(
            dip(var-new-implicit!)
            over var-type!
            dup dip(ctx-new!)
        )
    )
    token-next
    dip(dip(type-elab-ctx!)))

def(elab-type-con!, TypeElab Token -- TypeElab Type Token,
    token-name?
    name-is-type? if(
        name-type@
        over token-num-args
        over type-arity == if(
            elab-type-args!,

            drop dup
            "Wrong number of arguments for type." emit-error!
            TYPE_ERROR
        ),

    name-undefined? if(
        drop dup "Unknown type." emit-error! TYPE_ERROR,

        drop dup "Not a type." emit-error! TYPE_ERROR
    ))
    swap token-next)

def(elab-type-args!, TypeElab Token Type -- TypeElab Token Type,
    dip(token-has-args?) swap if(
        dip(tuck) swap token-succ
        while(token-is-right-enclosure? not,
            token-succ swap
            dip(elab-type-arg!) swap
            dip(swap app-type-new! TApp))
        drop dip(swap),

        id
    ))

def(elab-type-hole!, TypeElab Token -- TypeElab Type Token,
    dip(type-elab-holes-allowed?) swap HolesAllowed->Bool if(
        token-has-args? if(
            dup "Types with args not yet supported." emit-error! TYPE_ERROR,
            token-name? THole)
        swap token-next,
        "type holes are not allowed here" emit-fatal-error!
    ))

def(elab-type-dont-care!, TypeElab Token -- TypeElab Type Token,
    dip(type-elab-holes-allowed?) swap HolesAllowed->Bool if(
        token-has-args? if(
            dup "Types with args not yet supported." emit-error! TYPE_ERROR,
            TYPE_DONT_CARE)
        swap token-next,
        "type don't care is not allowed here" emit-fatal-error!
    ))

def(elab-type-quote!, TypeElab Token -- TypeElab Type Token,
    token-bracket-arg
    sig-has-dashes? if(
        elab-type-sig!,
        elab-type-stack!
    )
    token-next)

def(elab-type-unify!, Type Type Token -- Type Token,
    elab-token@ dip(
        dup elab-token! dip(type-unify!)
    ) elab-token!)
    # TODO treat token as an input in type-unify

def(elab-simple-type-arg!, Token -- Type,
    dip(type-elab-default) elab-type-arg! drop nip)

####################
# Word Elaboration #
####################

def(elab-tag-sig!, Tag -- Type +Elab,
    elab-tag-ctx-sig! nip)

def(elab-tag-ctx!, Tag -- Ctx +Elab,
    elab-tag-ctx-sig! drop)

def(elab-tag-ctx-sig!, Tag -- Ctx Type +Elab,
    tag-sig-is-checked? if(
        tag-type? dip(tag-ctx@),

        type-elab-default over

        { Tag TypeElab Tag }
        tag-data@ data-header@ elab-type-atom! drop T1
        { Tag TypeElab Type }
        dip(
            over tag-has-sig?
            if(
                tag-sig@ T0 swap elab-type-stack-rest!
                token-run-end? if(drop, "syntax error" emit-fatal-error!),

                drop T0
            )
        )
        { Tag TypeElab Type Type }
        T-> dip(type-elab-ctx)
        { Tag Ctx Type }
        dup2 dip(dip(
            rotl
            tuck tag-type!
            tuck tag-ctx!
            true swap tag-sig-is-checked!
        ))
    ))


def(elab-external-sig!, External -- Type +Elab,
    elab-external-ctx-sig! nip)

def(elab-external-ctx!, External -- Ctx +Elab,
    elab-external-ctx-sig! drop)

def(elab-external-ctx-sig!, External -- Ctx Type +Elab,
    external-sig-is-checked? if(
        external-type? dip(external-ctx@),

        external-sig?
        dip(type-elab-default)
        elab-type-sig! drop dip(type-elab-ctx)
        dup2 dip(dip(
            rotl
            tuck external-type!
            tuck external-ctx!
            external-sig-is-checked!
        ))
    ))

def(elab-word-sig!, Word -- Type +Elab,
    elab-word-ctx-sig! nip)

def(elab-word-ctx!, Word -- Ctx +Elab,
    elab-word-ctx-sig! drop)

def(elab-word-ctx-sig!, Word -- Ctx Type +Elab,
    word-sig-is-checked? if(
        word-type? dip(word-ctx@),

        word-sig?
        dip(type-elab-default)
        elab-type-sig! drop dip(type-elab-ctx)
        dup2 dip(dip(
            rotl
            tuck word-type!
            tuck word-ctx!
            word-sig-is-checked!
        ))
    ))

def(elab-word-body!, Word -- Arrow +Elab,
    word-body-is-checked? if(
        word-arrow@,

    word-has-body? if(
        dup dip(elab-word-ctx-sig!)
        dup dip(word-body@ elab-arrow!)
        dup2 word-arrow!
        word-body-is-checked!,

        word-sig@ token-prev token-prev # not very good :-/
        "Missing word definition."
        emit-fatal-error!
    )))

def(elab-arrow!, Ctx Type Token -- Arrow,
    dip(type-get-morphism morphism-type-unpack) swap
    dip(elab-arrow-fwd! arrow-dom? swap arrow-token@)
    swap elab-type-unify! drop2)

def(elab-arrow-fwd!, Ctx Type Token -- Arrow Arrow,
    arrow-alloc!
    tuck arrow-token!
    tuck arrow-dom!
    tuck arrow-ctx!
    dup
    while(elab-arrow-done? not,
        elab-arrow-step!))

||| Is this arrow at the end of a run?
def(elab-arrow-done?, Arrow -- Arrow Bool,
    arrow-token? token-run-end? nip)

||| From the {ctx, dom, token} fields, elaborate the
||| arrow's {op, arg, next} fields, returning
||| the next arrow with {ctx, dom, token} set.
def(elab-arrow-step!, Arrow -- Arrow,
    dup elab-arrow-op!
    arrow-alloc! tuck arrow-dom!
    dip(arrow-token? token-next) tuck arrow-token!
    dip(arrow-ctx?) tuck arrow-ctx!
    tuck dip(arrow-next!))

||| From the {ctx, dom, token} fields, elaborate the
||| arrow's {op, arg} fields, returning the
||| codomain of the operation.
def(elab-arrow-op!, Arrow -- Type,
    arrow-token?
    token-is-name? if(
        token-name@
        elab-arrow-op-name!,
    token-is-int? if(
        token-int@
        elab-arrow-op-int!,
    token-is-str? if(
        token-strlit@
        elab-arrow-op-str!,
    token-is-lbrace? if(
        elab-arrow-op-stack-assertion!,
        "compiler error: Unknown token in elab-arrow-step!" emit-fatal-error!
    )))))

def(elab-arrow-op-name!, Arrow Name -- Type,
    name-is-buffer? if(
        name-buffer@ elab-arrow-op-buffer!,
    name-is-external? if(
        name-external@ elab-arrow-op-external!,
    name-is-word? if(
        name-word@ elab-arrow-op-word!,
    name-is-prim? if(
        name-prim@ elab-arrow-op-prim!,
    name-is-tag? if(
        name-tag@ elab-arrow-op-tag!,

        drop
        arrow-token@ "Unknown word." emit-error!
        TYPE_ERROR
    ))))))

def(elab-arrow-op-int!, Arrow Int -- Type,
    over arrow-op-int!
    arrow-dom@ TYPE_INT T*)

def(elab-arrow-op-str!, Arrow StrLit -- Type,
    over arrow-op-str!
    arrow-dom@ TYPE_PTR T*)

def(elab-arrow-op-buffer!, Arrow Buffer -- Type,
    over arrow-op-buffer!
    arrow-dom@ TYPE_PTR T*)

def(elab-arrow-op-stack-assertion!, Arrow Token -- Type,
    dip(arrow-ctx? type-elab-stack-assertion)
    token-succ elab-type-stack!
    drop nip
    over arrow-token@
    dip(dip(arrow-dom@ dup))
    elab-type-unify! drop2)

def(elab-arrow-op-external!, Arrow External -- Type,
    dup2 swap arrow-op-external!
    elab-external-sig! elab-arrow-op-apply-fresh!)

def(elab-arrow-op-word!, Arrow Word -- Type,
    dup2 swap arrow-op-word!
    elab-word-sig! elab-arrow-op-apply-fresh!)

def(elab-arrow-op-tag!, Arrow Tag -- Type,
    dup2 swap arrow-op-tag!
    elab-tag-sig! elab-arrow-op-apply-fresh!)

def(elab-arrow-op-prim!, Arrow Prim -- Type,
    prim-type? TYPE_ERROR == if(
        drop arrow-token@ "compiler error: prim type missing" emit-fatal-error!,

        dup2 swap arrow-op-prim!
        prim-type@ elab-arrow-op-apply-fresh!
    ))

def(elab-arrow-op-dip!, Arrow -- Type,
    arrow-token? token-args-1 dip(arrow-dom? dip(arrow-ctx?))
    dip(type-expand-tensor tensor-type-unpack) swap
    dip(elab-arrow-fwd! arrow-dom@) T*
    dip(Arrow->Arg arrow-arg-add! drop))

||| Apply a type signature after freshening to get a codomain.
def(elab-arrow-op-apply-fresh!, Arrow Type -- Type,
    SUBST_NIL swap type-freshen-sig
    dip(over arrow-subst!)
    elab-arrow-op-apply!)

||| Apply a type signature to get a codomain.
def(elab-arrow-op-apply!, Arrow Type -- Type,
    type-expand

    over arrow-token@ token-num-args
    over type-num-params != if(
        over arrow-token@
        "Wrong number of arguments." emit-fatal-error!
        drop2 TYPE_ERROR, # TODO: improve error message

    type-is-param? if(
        type-get-param param-type-unpack
        type-expand-morphism morphism-type-unpack
        dip(swap dip(elab-arrow-unify-dom!))
        dip(elab-arrow-op-args! drop),

        type-expand-morphism morphism-type-unpack
        dip(elab-arrow-unify-dom! drop)
    )))


||| Unify a type with the dom of an arrow, returning the original arrow.
def(elab-arrow-unify-dom!, Arrow Type -- Arrow,
    over arrow-dom? swap arrow-token@ elab-type-unify! drop2)

||| Elaborate the args of an arrow.
def(elab-arrow-op-args!, Arrow Param -- Arrow,
    dip(arrow-token? token-succ)
    while(param-empty? not,
        dip(token-succ)
        param-unpack
        dip(
            over
            dip(dip(over arrow-ctx@))
            elab-arrow! Arrow->Arg
            swap token-next-arg-end
            dip(arrow-arg-add!)
        )
    )
    drop2)

######################
# Module Elaboration #
######################

||| Elaborate all of a module.
def(elab-module!, Module -- Module +Mirth,
    module-start?
    token-skip-newlines
    elab-module-header!
    elab-module-imports!
    elab-module-decls!
    typecheck-everything!
    drop)

||| Check that the `module(M)` statement exists and save the name.
def(elab-module-header!, Module Token -- Module Token +Mirth,
    PRIM_MODULE token-prim=? if(
        dup2 elab-module-name! token-next,
        dup "Expected module header." emit-error!
    )
    token-skip-newlines)

||| Check & save the module name from the module header `module(M)`.
def(elab-module-name!, Module Token -- +Mirth,
    token-args-1 token-is-name? if(
        token-name?
        name-defined? if(
            drop "Module name already taken." emit-fatal-error!,
            dip(swap)
            dup2 swap module-name!
            dup2 name-module!

            # check module name matches module path
            name-load! ".mth" str-buf-push-str!
            module-path@ Path->Str STR_BUF str-eq if(
                drop,
                "Module name should match path." emit-error!
            )
        ),
        "Expected module name." emit-fatal-error!
    ))


||| Elaborate module imports. Return token after imports.
def(elab-module-imports!, Module Token -- Module Token +Mirth,
    while(PRIM_IMPORT token-prim=?, elab-module-import!))

||| Elaborate `import(M)` statement. Return token after import.
def(elab-module-import!, Module Token -- Module Token +Mirth,
    dup dip(token-next)
    token-args-1 token-is-name? if(
        token-name?

        name-is-module? if(
            drop2, # module is already loaded (TODO: add import to a list)

        name-undefined? if(
            nip name-load! ".mth" str-buf-push-str!
            STR_BUF Str->Path run-lexer!
            elab-module! drop, # elab the imported module (TODO: add import to a list)

            drop "module name already taken" emit-fatal-error!
        )),
        "expected module name" emit-fatal-error!
    )
    token-skip-newlines)

||| Elaborate the rest of the module.
def(elab-module-decls!, Module Token -- Module Token +Mirth,
    while(token-is-module-end? not, elab-module-decl!))

||| Elaborate a declaration. Returns the next token.
def(elab-module-decl!, Module Token -- Module Token +Mirth,
    PRIM_DEF token-prim=? if( elab-def!,
    PRIM_DEF_EXTERNAL token-prim=? if( elab-def-external!,
    PRIM_DEF_TYPE token-prim=? if( elab-def-type!,
    PRIM_NOMINAL token-prim=? if( elab-nominal!,
    PRIM_BUFFER token-prim=? if( elab-buffer!,
    PRIM_TABLE token-prim=? if( elab-table!,
    PRIM_FIELD token-prim=? if( elab-field!,
    PRIM_TARGET_C99 token-prim=? if(elab-target-c99!,
    PRIM_DATA token-prim=? if( elab-data!,
    token-could-be-word-sig? if(elab-decl-word-sig!,
    token-could-be-word-def? if(elab-decl-word-def!,
    "unknown declaration" emit-fatal-error!)))))))))))
    token-skip-newlines)

||| Elaborate a data definition `data T ... end` or `data(T, ...)`
def(elab-data!, Module Token -- Module Token +Mirth,
    Data.alloc! swap
    elab-data-header!
    elab-data-tags!)

||| Get the header, name, arity for a data type. Doesn't move the Token.
def(elab-data-header!, Module Data Token -- Module Data Token +Mirth,
    dup2 token-has-args? if(
        token-succ token-succ,
        token-succ
    )
    dup2 swap data-header!
    sig-token-is-type-con? if(
        token-name?
        name-undefined? if(
            dip(over)
            dup2 swap data-name!
            dip(TData) name-type!,
            drop "Name already defined." emit-fatal-error!
        ),
        drop2 "Expected type name." emit-fatal-error!
    )

    token-has-args? if(
        # TODO check arguments are well-formed / elaborate them properly
        token-num-args swap data-arity!,
        drop2
    ))

||| Get the tags (i.e. constructors) associated with a data type.
||| Moves the Token to after the data declaration.
def(elab-data-tags!, Module Data Token -- Module Token +Mirth,
    token-has-args? if(
        token-succ token-succ token-next
        expect-token-comma token-succ
        elab-data-tags-body!
        expect-token-rparen token-succ,

        token-succ token-next
        expect-token-newline token-skip-newlines
        elab-data-tags-body!
        expect-token-end token-succ
    )
    nip)

expect-token-comma : Token -- Token
expect-token-comma = token-is-comma? if(id, "Expected comma." emit-fatal-error!)

expect-token-rparen : Token -- Token
expect-token-rparen = token-is-rparen? if(id, "Expected right parenthesis." emit-fatal-error!)

expect-token-newline : Token -- Token
expect-token-newline = token-is-newline? if(id, "Expected newline." emit-fatal-error!)

expect-token-end : Token -- Token
expect-token-end = PRIM_END token-prim=? if(id, "Expected 'end'" emit-fatal-error!)

||| Get the tags (i.e. constructors) associated with a data type,
||| from inside the data declaration, one at a time. Moves the Token
||| to just after the last tag.
elab-data-tags-body! : Module Data Token -- Module Data Token
elab-data-tags-body! = while(elab-data-tags-done? not, elab-data-tag!)

elab-data-tags-done? : Token -- Token Bool
elab-data-tags-done? = token-is-rparen? if(true, PRIM_END token-prim=?)

||| Get a tag associated with a data type.
||| This looks like either "TAG" or "TAG -> TYPE1 ... TYPEN".
||| Moves the Token to after the tag declaration.
elab-data-tag! : Module Data Token -- Module Data Token +Mirth
elab-data-tag! = (
    token-is-name? if(
        token-name? name-undefined? if(
            Tag.alloc!
            dup2 swap name-tag!
            tuck tag-name!,
            drop "Name already defined. (Overlapping tags not yet supported.)" emit-fatal-error!
        )
        { Module Data Token Tag }
        dip(over) dup2 tag-data!
        tuck dip(data-add-tag!)
        { Module Data Token Tag }
        swap token-succ
        PRIM_ARROW token-prim=? if(
            dip(true over tag-has-sig!)
            token-succ tuck dip(tag-sig!)
            while(token-run-end? not, token-next),
        token-is-newline? if(
            nip,
        token-is-comma? if(
            nip,
            "Expected arrow, comma, or newline." emit-fatal-error!
        )))
        { Module Data Token },

        "Expected tag name." emit-fatal-error!
    )
    token-is-comma? if(token-succ, id)
    token-skip-newlines
)

||| Elaborate a word definition `def(w, t, b)`.
def(elab-def!, Module Token -- Module Token +Mirth,
    dup dip(token-next) token-args-3
    rotl token-type? TOKEN_NAME == if(
        token-name? name-undefined? if(
            nip
            word-alloc!
            dup2 swap name-word!
            dup dip(word-name!)
            dup dip(word-body!)
            word-sig!,

            drop "word already defined" emit-fatal-error!
        ),
        "expected word name" emit-fatal-error!
    ))

||| Elaborate a word signature "foo : Bar -- Baz"
def(elab-decl-word-sig!, Module Token -- Module Token +Mirth,
    token-name? name-undefined? if(
        word-alloc! tuck
        dup2 word-name!
        swap name-word!
        dip(token-next token-succ dup) word-sig!
        while(token-is-newline? not, token-next) token-skip-newlines,

        drop "word already defined" emit-fatal-error!
    ))

||| Elaborate a word definition "foo = bar"
def(elab-decl-word-def!, Module Token -- Module Token +Mirth,
    token-name?
    name-is-word? if(
        name-word@
        word-has-body? if(
            drop "word already defined" emit-fatal-error!,
            dip(token-next token-succ
                token-is-lparen? if(token-succ, token-skip-newlines)
                dup) word-body!
            while(token-is-newline? not, token-next) token-skip-newlines
        ),

    name-undefined? if(
        drop "missing word signature" emit-fatal-error!,

        drop "name already defined, not a word" emit-fatal-error!
    )))

||| Elaborate an external declaration `def-external(w, t)`
def(elab-def-external!, Module Token -- Module Token +Mirth,
    dup dip(token-next) token-args-2
    swap token-type? TOKEN_NAME == if(
        token-name? name-undefined? if(
            nip
            external-alloc!
            dup2 swap name-external!
            tuck external-name!
            external-sig!,

            drop "word already defined" emit-fatal-error!
        ),
        "expected word name" emit-fatal-error!
    ))

||| Elaborate a type definition `def-type(t1, t2)`.
def(elab-def-type!, Module Token -- Module Token +Mirth,
    dup dip(token-next) token-args-2
    swap sig-token-is-type-con? if(
        token-name? name-undefined? if(
            nip swap
            elab-simple-type-arg!
            swap name-type!,

            drop "type already defined" emit-fatal-error!
        ),
        "expected type constructor" emit-fatal-error!
    ))

||| Elaborate a nominal type definition `nominal(t1, t2)`.
def(elab-nominal!, Module Token -- Module Token +Mirth,
    dup dip(token-next) token-args-2
    swap sig-token-is-type-con? if(
        token-name? name-undefined? if(
            { Module Token Token Token Name }
            dup dip(
                swap token-num-args
                rotl elab-simple-type-arg!
                    # TODO use params to infer a ctx
                    # and actually use that ctx here
                nominal-new! elab-nominal-words! TNominal
            ) name-type!,

            drop "type already defined" emit-fatal-error!
        ),
        "expected type constructor" emit-fatal-error!
    ))

def(elab-nominal-words!, Nominal -- Nominal +Mirth,
    nominal-arity? 0 == if(
        nominal-name? name-load! ".wrap" str-buf-push-str! name-save!
        word-alloc! dup2 swap name-word! tuck word-name!
        over dup nominal-type@ T1 swap TNominal T1 T->
        over word-type!
        arrow-alloc! over word-arrow!
        dup word-sig-is-checked!
        word-body-is-checked!

        nominal-name? name-load! ".unwrap" str-buf-push-str! name-save!
        word-alloc! dup2 swap name-word! tuck word-name!
        over dup nominal-type@ T1 swap TNominal T1 swap T->
        over word-type!
        arrow-alloc! over word-arrow!
        dup word-sig-is-checked!
        word-body-is-checked!,

        id # TODO, possibly.
    ))

||| Elaborate a buffer definition `buffer(B, size)`.
def(elab-buffer!, Module Token -- Module Token +Mirth,
    dup dip(token-next) token-args-2
    swap token-type? TOKEN_NAME == if(
        token-name? name-undefined? if(
            nip swap
            token-type? TOKEN_INT == if(
                token-int@ buffer-alloc! drop,
                "expected buffer size" emit-fatal-error!
            ),
            drop "buffer already defined" emit-fatal-error!
        ),
        "expected buffer name" emit-fatal-error!
    ))

||| Elaborate a table definition `table(T)`.
def(elab-table!, Module Token -- Module Token +Mirth,
    dup dip(token-next) token-args-1
    sig-token-is-type-con? if(
        token-name@ table-new! drop,
        "expected table name" emit-fatal-error!
    ))

||| Elaborate a field definition `field(f, T1, T2)`.
def(elab-field!, Module Token -- Module Token +Mirth,
    dup dip(token-next) token-args-3
    rotl token-type? TOKEN_NAME == if(
        token-name@
        rotl token-type? TOKEN_NAME == if(
            token-name?
            name-is-table? if(
                nip name-type@ type-get-table rotl
                elab-simple-type-arg!
                field-new! drop,

                drop "unknown table name" emit-fatal-error!
            ),
            "expected table name" emit-fatal-error!
        ),
        "expected field name" emit-fatal-error!
    ))

||| Elaborate C99 build target.
def(elab-target-c99!, Module Token -- Module Token +Mirth,
    typecheck-everything!

    dup dip(token-next) token-args-2
    swap token-type? TOKEN_STR == if(
        dip(
            dip(ctx-empty T0 T0 T->)
            elab-arrow!
        )

        token-str@ Str->Path run-output-c99!,
        "Expected relative C file path (a string)." emit-fatal-error!
    ))

||| Ensure that everything so far has been typechecked.
def(typecheck-everything!, --,
    1
    while(dup num-names@ <=,
        dup Int->Name
        typecheck-name!
        1+)
    drop)

||| Typecheck name.
def(typecheck-name!, Name --,
    name-is-external? if(
        name-external@ elab-external-sig! drop,
    name-is-word? if(
        name-word@ elab-word-body! drop,
        drop
    )))
