module(mirth.main)

import(std.prelude)
import(std.path)
import(std.str)
import(std.list)
import(std.byte)
import(std.maybe)

import(args.parse)
import(args.types)
import(args.state)

import(posix.posix)
import(posix.file)

import(mirth.package)
import(mirth.error)
import(mirth.prim)
import(mirth.word)
import(mirth.type)
import(mirth.name)
import(mirth.def)
import(mirth.lexer)
import(mirth.elab)
import(mirth.ctx)
import(mirth.c99)

def(init!, --,
    init-errors!
    init-types!
    init-prims!)

data(Arguments,
    Arguments -> 
        input-file:Path 
	output-file:Maybe(Path) 
	entry-point:Maybe(Str) 
	packages:List([Str Path]) 
	emit-debug-info:Bool)

||| Create a default initialized arguments table
||| Since the input file is always required we don't need it to be Maybe(Path)
def(Arguments.default, -- Arguments,
    F >emit-debug-info
    "mirth.mth" >Path >input-file
    NONE >output-file
    "main" SOME >entry-point
    L0 >packages
    Arguments.Arguments)

||| Pretty print the contents of the arguments struct
def(Arguments.show, Arguments -- Str,
    /Arguments
    "Arguments { "
    "output-file: " cat output-file> if-some(>Str, "(none)") cat
    ", input-file: " cat input-file> >Str cat
    ", entry-point: " cat entry-point> unwrap-or("(none)") cat
    ", emit-debug-info: " cat emit-debug-info> if("yes", "no") cat
    ", packages: " cat "[" packages> List.for(dip(Str.cat) unpack2 dip(Str.cat ":" cat) Path.>Str cat " ") drop "]" cat
    " }" cat)

########
# Main #
########

def(compile!, Arguments +World -- +World,
    /Arguments
    "Compiling " trace!
    input-file> dup >Str trace-ln! >input-file
    packages> List.for(
      unpack2 swap Str.>Name Package.new-or-set-path! drop
    )
    input-file> run-lexer!
    "Building." trace-ln!

    elab-module!
    typecheck-everything!

    entry-point> map(
        dip(dup NAMESPACE_MODULE) >Name QNAME0
        elab-entry-point
    ) nip

    num-errors @ 0> if(
        num-errors @ trace!
        " errors." trace-ln!
        1 posix-exit!,
  	    "No errors." trace-ln!
    )

    match(
        SOME ->
            "Codegen." trace-ln!
            output-file> unwrap >output-path
            C99_Options.make run-output-c99!,
        NONE ->
            "Skipping Codegen." trace-ln!
            emit-debug-info> drop
            output-file> drop
    ))

def(parse-package-def, Str -- [Str Path],
  dip(BCOLON) split-byte match(
    L2 -> >Path pack2,
    _ -> drop "Invalid package path definition" panic!
  )
)

def(compiler-parse-args, +ArgumentParser(Arguments) Arguments Maybe(Str) ArgpOptionType -- +ArgumentParser(Arguments) Arguments,
    SHORT -> match(
        B'o' -> unwrap >Path SOME swap output-file!,
        B'e' -> unwrap SOME swap entry-point!,
        B'c' -> drop NONE swap entry-point!,
        B'p' -> unwrap parse-package-def swap packages(cons),
        _ -> drop drop UNKNOWN_ARG SOME state error! state!
    ),
    POSITIONAL -> state positional-index 0= if(
        unwrap >Path swap input-file!,
        drop TOO_MANY_ARGS SOME state:error! 
    ),
    LONG_ONLY -> dup "debug" == if(
        drop2 T swap emit-debug-info!,
        drop2 UNKNOWN_ARG SOME state:error!
    ),
    END ->
        state positional-index 1 < then(
            TOO_FEW_ARGS SOME state error! state!
        )
        state arguments output-file match(
            SOME -> drop,
            # If we have an entry point we need to have an output file currently
            NONE -> state arguments entry-point match (
                SOME -> drop "output-file" MISSING_ARG SOME state error! state!,
                NONE ->
            )
        )
        drop,
    _ -> drop drop UNKNOWN_ARG SOME state error! state!)

def(main, +World -- +World,
    init!

    Arguments.default
    LIST(
        "output-file" SOME >name
        B'o' SHORT >flag-type
        "OUTPUT_FILE" SOME >arg-doc
        "Test argument" SOME >doc
        NONE >group 
	ArgpOption.ArgpOption ;

        "compile-only" SOME >name
        B'c' SHORT >flag-type
        NONE >arg-doc
        "Compile code without running codegen step" SOME >doc
        NONE >group 
	ArgpOption.ArgpOption ;

        "entry-point" SOME >name
        B'e' SHORT >flag-type
        "ENTRY_POINT" SOME >arg-doc
        "Custom entry point word for compilation" SOME >doc
        NONE >group 
	ArgpOption.ArgpOption ;

	"package" SOME >name
	B'p' SHORT >flag-type
	"(PACKAGE:PATH)*" SOME >arg-doc
	"Package locations" SOME >doc
	NONE >group
	ArgpOption.ArgpOption ;

        "debug" SOME >name
        "debug" LONG_ONLY >flag-type
        NONE >arg-doc
        "Emit debugging information during codegen" SOME >doc
        NONE >group
	ArgpOption.ArgpOption ;
    ) >options
    [ compiler-parse-args ] SOME >parser
    "input-file" SOME >args-doc
    "Mirth Compiler" >doc
    ArgumentParser

    parse-args match(
        RIGHT -> ,
        LEFT -> show panic!
    )

    compile!)
