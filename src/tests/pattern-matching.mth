
module(tests/pattern-matching)
import(prelude)

data Foo
    FOO0
    FOO1 -> Int
    FOO2 -> Int Int
    FOO3 -> Int Int Int
end

foo-sum : Foo -- Int
foo-sum =
    match(
        FOO0 -> 0,
        FOO1 -> id,
        FOO2 -> +,
        FOO3 -> + +,
    )

foo-tag : Foo -- Str
foo-tag =
    match(
        FOO0 -> "FOO0",
        FOO1 -> drop "FOO1",
        FOO2 -> drop2 "FOO2",
        _ -> drop "FOO3"
    )

foo-display : Foo -- +IO
foo-display = (
    dup
    foo-tag str-print! ", sum = " str-print!
    foo-sum int-print-ln!
)

main : +IO
main = (
    FOO0 foo-display
    10 FOO1 foo-display
    20 FOO1 foo-display
    30 12 FOO2 foo-display
    40 60 FOO2 foo-display
    90 90 20 FOO3 foo-display
    9000 0 1 FOO3 foo-display
)

target-c99("pattern-matching.c", main)
# mirth-test # pout # FOO0, sum = 0
# mirth-test # pout # FOO1, sum = 10
# mirth-test # pout # FOO1, sum = 20
# mirth-test # pout # FOO2, sum = 42
# mirth-test # pout # FOO2, sum = 100
# mirth-test # pout # FOO3, sum = 200
# mirth-test # pout # FOO3, sum = 9001
