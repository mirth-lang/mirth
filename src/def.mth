module mirth.def

import std.prelude
import std.maybe
import std.list
import std.str

import mirth.mirth
import mirth.name
import mirth.alias
import mirth.buffer
import mirth.data
import mirth.external
import mirth.module
import mirth.package
import mirth.prim
import mirth.table
import mirth.token
import mirth.tycon
import mirth.type
import mirth.typedef
import mirth.variable
import mirth.word
import mirth.macro

data Def {
    Alias [ Alias ]
    Module [ Module ]
    Package [ Package ]
    Data [ Data ]
    Table [ Table ]
    Type [ TypeDef ]
    Tag [ Tag ]
    Prim [ Prim ]
    Word [ Word ]
    Buffer [ Buffer ]
    Variable [ Variable ]
    External [ External ]
    Field [ Field ]
    Macro [ Macro ]
    --

    def package? { Package -> Some, _ -> drop None }
    def module? { Module -> Some, _ -> drop None }
    def alias? { Alias -> Some, _ -> drop None }
    def data? { Data -> Some, _ -> drop None }
    def table? { Table -> Some, _ -> drop None }
    def typedef? { Type -> Some, _ -> drop None }
    def tag? { Tag -> Some, _ -> drop None }
    def prim? { Prim -> Some, _ -> drop None }
    def word? { Word -> Some, _ -> drop None }
    def buffer? { Buffer -> Some, _ -> drop None }
    def variable? { Variable -> Some, _ -> drop None }
    def external? { External -> Some, _ -> drop None }
    def field? { Field -> Some, _ -> drop None }
    def macro? { Macro -> Some, _ -> drop None }

    def == [ Def Def -- Bool ] {
        { Package -> Some swap package? == }
        { Module -> Some swap module? == }
        { Alias -> Some swap alias? == }
        { Data -> Some swap data? == }
        { Table -> Some swap table? == }
        { Type -> Some swap typedef? == }
        { Tag -> Some swap tag? == }
        { Prim -> Some swap prim? == }
        { Word -> Some swap word? == }
        { Buffer -> Some swap buffer? == }
        { Variable -> Some swap variable? == }
        { External -> Some swap external? == }
        { Field -> Some swap field? == }
        { Macro -> Some swap macro? == }
    }

    def typecheck! [ +Mirth Def -- +Mirth ] {
        { Package -> drop }
        { Module -> drop }
        { Alias -> target typecheck! }
        { Data -> drop }
        { Table -> drop }
        { Type -> target drop }
        { Tag -> type drop }
        { Prim -> drop }
        { Word -> dup type drop arrow drop }
        { Buffer -> drop }
        { Variable -> type drop }
        { External -> type drop }
        { Field -> type drop }
        { Macro -> drop }
    }

    def callable? [ +Mirth Def -- +Mirth Bool ] {
        { Alias -> ~target try-force! has(callable?) }
        { Package -> drop False }
        { Module -> drop False }
        { Buffer -> drop True }
        { Prim -> drop True }
        { Data -> drop False }
        { Table -> drop False }
        { Type -> drop False }
        { External -> drop True }
        { Word -> drop True }
        { Field -> drop True }
        { Tag -> drop True }
        { Variable -> drop True }
        { Macro -> action callable? }
    }

    def defines-a-type? [ +Mirth Def -- +Mirth Bool ] {
        { Alias -> ~target try-force! has(defines-a-type?) }
        { Package -> drop False }
        { Module -> drop False }
        { Buffer -> drop False }
        { Prim -> drop False }
        { Data -> drop True }
        { Table -> drop True }
        { Type -> drop True }
        { External -> drop False }
        { Word -> drop False }
        { Field -> drop False }
        { Tag -> drop False }
        { Variable -> drop False }
        { Macro -> drop False }
    }

    def exposed-tycon? [ +Mirth Def -- +Mirth Maybe(Tycon) ] {
        { Alias -> ~target try-force! bind(exposed-tycon?) }
        { Package -> drop None }
        { Module -> drop None }
        { Data -> Tycon.Data Some }
        { Table -> Tycon.Table Some }
        { Type -> target tycon? }
        { Buffer -> drop None }
        { Prim -> drop None }
        { External -> drop None }
        { Word -> drop None }
        { Field -> drop None }
        { Tag -> drop None }
        { Variable -> drop None }
        { Macro -> drop None }
    }

    def head? [ Def -- Maybe(Token) ] {
        { Alias -> head Some }
        { Package -> drop None }
        { Module -> start Some }
        { Data -> head? }
        { Table -> head Some }
        { Type -> head? }
        { Buffer -> drop None }
        { Prim -> drop None }
        { External -> head Some }
        { Word -> head Some }
        { Field -> head Some }
        { Tag -> .data head? }
        { Variable -> head Some }
        { Macro -> head? }
    }

    def defining-module? [ Def -- Maybe(Module) ] {
        head? map(.module)
    }

    def same-resolved? [ +Mirth Def Def -- +Mirth Bool ] {
        on2(resolve) ==
    }

    def resolve [ +Mirth Def -- +Mirth Def ] {
        while-some(dup alias? bind(~target try-force!), nip)
    }

    def name [ Def -- Name ] {
        { Alias -> name }
        { Package -> name }
        { Module -> name }
        { Buffer -> name }
        { Prim -> name }
        { Data -> name }
        { Table -> name }
        { Type -> name }
        { External -> name }
        { Word -> name }
        { Field -> name }
        { Tag -> name }
        { Variable -> name }
        { Macro -> name }
    }

    def arity [ Def -- Int ] {
        { Alias -> arity }
        { Package -> drop 0 }
        { Module -> drop 0 }
        { Buffer -> drop 0 }
        { Prim -> arity }
        { Data -> arity }
        { Table -> drop 0 }
        { Type -> drop 0 }
        { External -> arity }
        { Word -> arity >Int }
        { Field -> drop 0 }
        { Tag -> drop 0 }
        { Variable -> drop 0 }
        { Macro -> arity }
    }

    def qname-soft [ +Mirth Def -- +Mirth Maybe(QName) ] {
        { Alias -> qname-soft }
        { Package -> qname Some }
        { Module -> qname Some }
        { Buffer -> qname Some }
        { Prim -> qname Some }
        { Data -> qname-soft }
        { Table -> qname-soft }
        { Type -> qname-soft }
        { External -> qname-soft }
        { Word -> qname-soft }
        { Field -> qname-soft }
        { Tag -> qname Some }
        { Variable -> qname Some }
        { Macro -> qname-soft }
    }

    def qname-hard [ +Mirth Def -- +Mirth QName ] {
        { Alias -> qname-hard }
        { Package -> qname }
        { Module -> qname }
        { Buffer -> qname }
        { Prim -> qname }
        { Data -> qname-hard }
        { Table -> qname-hard }
        { Type -> qname-hard }
        { External -> qname-hard }
        { Word -> qname-hard }
        { Field -> qname-hard }
        { Tag -> qname }
        { Variable -> qname }
        { Macro -> qname-hard }
    }

    def as-namespace? [ +Mirth Def -- +Mirth Maybe(Namespace) ] {
        { Alias -> ~target try-force! bind(as-namespace?) }
        { Package -> Namespace.Package Some }
        { Module -> Namespace.Module Some }
        { Data -> Tycon.Data Namespace.Tycon Some }
        { Table -> Tycon.Table Namespace.Tycon Some }
        { Type -> target tycon? map(Namespace.Tycon) }
        { Word -> Namespace.Word Some }
        { _ -> drop None }
    }

    def register [ +Mirth Def -- +Mirth ] {
        dup qname-soft for(
            dup undefined-soft? else(
                >Str "qualified name already has definition: " swap cat fatal-error!
            ) drop
        )
        dup name ~defs modify(cons)
    }
}
