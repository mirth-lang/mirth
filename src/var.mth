module(mirth.var)

import(std.prelude)
import(std.maybe)
import(std.list)
import(std.str)

import(mirth.name)
import(mirth.type)
import(mirth.mirth)

#######
# VAR #
#######

# mirth/data/var
table(Var)
field(Var.~name, Var, Name)
field(Var.~type, Var, Type)
field(Var.~auto-run?, Var, Maybe(ArrowType)) # run variable by default

def(Var.name, Var -- Name, ~name @)
def(Var.type, Var -- Type, ~type @)
def(Var.auto-run?, Var -- Maybe(ArrowType), ~auto-run? @)

inline (
    def Var.== [ Var Var -- Bool ] { both(index) == }
    def Var.<> [ Var Var -- Bool ] { == not }
)
def(Var.type;, +Str Var -- +Str, name >Str ;)
def(Var.is-type?, Var -- Bool, type prim? has(PRIM_TYPE_TYPE ==))
def(Var.is-stack?, Var -- Bool, type prim? has(PRIM_TYPE_STACK ==))
def(Var.is-resource?, Var -- Bool, type prim? has(PRIM_TYPE_RESOURCE ==))
def(Var.is-physical?, Var -- Bool, type is-physical?)

def(Var.new!, Type Name -- Var,
    Var.alloc!
    tuck ~name !
    tuck ~type !
    None over ~auto-run? !)

def(Var.new-auto-run!, ArrowType Name -- Var,
    dip(dup TMorphism) Var.new!
    dip(Some) tuck ~auto-run? !)

def Var.NewInCtx [ Ctx Type Str -- Ctx Var ] {
    >Name Var.new! sip:Ctx.new
}

###########
# Context #
###########

# A context for types and terms.
data(Ctx, Ctx -> List(Var))
def(List.>Ctx, List(Var) -- Ctx, Ctx)
def(List+.>Ctx, List+(Var) -- Ctx, >List Ctx)
def(Ctx.>List, Ctx -- List(Var), /Ctx)

def(Ctx.L0, Ctx, List.L0 Ctx)
def(Ctx.L1, Var -- Ctx, List.L1 Ctx)
def(Ctx.L2, Var Var -- Ctx, List.L2 Ctx)
def(Ctx.L3, Var Var Var -- Ctx, List.L3 Ctx)
def(Ctx.L4, Var Var Var Var -- Ctx, List.L4 Ctx)
def(Ctx.L5, Var Var Var Var Var -- Ctx, List.L5 Ctx)

def(Ctx.new, Ctx Var -- Ctx, dip(>List) List.snoc >Ctx)

def(Ctx.vars, Ctx -- List(Var), >List)
def(Ctx.physical-vars, Ctx -- List(Var), vars filter(is-physical?))
def(Ctx.physically-empty?, Ctx -- Bool, physical-vars empty?)
def(Ctx.lookup, Name Ctx -- Maybe(Var), >List reverse-lookup(name ==))
def(Ctx.len, Ctx -- Nat, >List len)
def(Ctx.fresh-name!, Ctx -- Ctx Name,
    1 "_x1" >Name while-some(
        dup over3 lookup,
        drop2 1+ "_x" over show cat >Name
    ) nip)
def(Ctx.fresh-stack-type-var!, Ctx -- Ctx Var,
    dip(PRIM_TYPE_STACK TPrim) fresh-var!)
def(Ctx.fresh-type-var!, Ctx -- Ctx Var,
    dip(PRIM_TYPE_TYPE TPrim) fresh-var!)
def(Ctx.fresh-var!, Type Ctx -- Ctx Var,
    fresh-name! dip(swap) Var.new! sip(Ctx.new))

def(Ctx.==, Ctx Ctx -- Bool,
    on2:vars ==)

def(Var.unify!, +Mirth +Gamma Var Var -- +Mirth +Gamma Type,
    dup2 == if(drop TVar, both(TVar) unify-failed!))

def(Var.freshen, Subst Var -- Subst Type,
    swap dup2 has-var? if(
        tuck get-var,
        MetaVar.new! TMeta dup
        dip(rotr cons)
    ))
