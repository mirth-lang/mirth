module(mirth.data)

import(std.prelude)
import(std.list)
import(std.str)
import(std.maybe)
import(std.either)

import(mirth.var)
import(mirth.type)
import(mirth.token)
import(mirth.name)
import(mirth.def)
import(mirth.label)
import(mirth.tycon)
import(mirth.word)
import(mirth.mirth)

table(Data)
field(Data.~head?, Data, Maybe(Token))
field(Data.~qname, Data, Prop(QName))
field(Data.~name, Data, Name)
field(Data.~arity, Data, Int)
field(Data.~params, Data, Prop(List(Var)))
field(Data.~tags, Data, List(Tag))
field(Data.~ctype?, Data, Prop(Maybe(CType)))

def(Data.head?, Data -- Maybe(Token), ~head? @)
def(Data.name, Data -- Name, ~name @)
def(Data.qname-soft, Data -- Maybe(QName), ~qname @? bind(ready?))
def(Data.qname-hard, +Mirth Data -- +Mirth QName, ~qname force!)
def(Data.namespace-hard, +Mirth Data -- +Mirth Namespace, qname-hard namespace)
def(Data.arity, Data -- Int, ~arity @)
def(Data.params, +Mirth Data -- +Mirth List(Var), ~params force!)
def(Data.tags, Data -- List(Tag), ~tags @)
def(Data.ctype?, Data +Mirth -- Maybe(CType) +Mirth, ~ctype? force!)

def(TYPE_BOOL, +Mirth -- +Mirth Type, builtin bool TData)
def(TYPE_U64, +Mirth -- +Mirth Type, builtin u64 TData)
def(TYPE_U32, +Mirth -- +Mirth Type, builtin u32 TData)
def(TYPE_U16, +Mirth -- +Mirth Type, builtin u16 TData)
def(TYPE_U8,  +Mirth -- +Mirth Type, builtin u8  TData)
def(TYPE_I64, +Mirth -- +Mirth Type, builtin i64 TData)
def(TYPE_I32, +Mirth -- +Mirth Type, builtin i32 TData)
def(TYPE_I16, +Mirth -- +Mirth Type, builtin i16 TData)
def(TYPE_I8,  +Mirth -- +Mirth Type, builtin i8  TData)

def(make-prim-data!, +Mirth Str List(Var) Data List(Tag) Maybe(CType) -- +Mirth,
    >ctype >tags >data >params >name

    @params len >Int dup @data ~arity !
    @name >Name @data ~name !
    name> swap QName.Prim @data DataQName prop @data ~qname !
    ctype> @data DataCType prop @data ~ctype? !

    @tags for(@data swap ~data !)
    tags> @data ~tags !

    params> @data DataParams prop @data ~params !
    None @data ~head? !

    data> DefData register)

def(make-prim-tag!, +Mirth Str Int List(Type) Tag -- +Mirth,
    >tag >inputs >value >name

    @tag .data name> 0u data-word-qname @tag ~qname !
    value> @tag ~value !

    @inputs len @tag ~num-type-inputs !
    0u @tag ~num-resource-inputs !
    List.L0 @tag ~label-inputs !

    @tag .data params >Ctx
    inputs> TT
    T0 @tag .data full-type T*+ T->
    @tag TagType prop2
    @tag ~ctx-type !

    tag> DefTag register)

def(make-prim-intlike!, Str Data Str Tag Str +Mirth -- +Mirth,
    >underlying >tag >tagname >data >dataname

    dataname> List.L0 data> @tag List.L1 underlying> IntLike Some make-prim-data!
    tagname> 0 TYPE_INT List.L1 tag> make-prim-tag!)

def(init-data!, +Mirth -- +Mirth,
    "Bool" List.L0 builtin bool builtin true builtin false List.L2 "_Bool" IntLike Some make-prim-data!
    "False" 0 List.L0 builtin false make-prim-tag!
    "True" 1 List.L0 builtin true make-prim-tag!

    TYPE_TYPE "a" >Name Var.new! >a
    "CPtr" @a List.L1 builtin cptr builtin mk-cptr List.L1 None make-prim-data!
    "CPtr" 0 TYPE_PTR List.L1 builtin mk-cptr make-prim-tag!
    "CConst" @a List.L1 builtin cconst builtin mk-cconst List.L1 None make-prim-data!
    "CConst" 0 @a TVar List.L1 builtin mk-cconst make-prim-tag!
    "CRestrict" @a List.L1 builtin crestrict builtin mk-crestrict List.L1 None make-prim-data!
    "CRestrict" 0 @a TVar List.L1 builtin mk-crestrict make-prim-tag!
    "CVolatile" @a List.L1 builtin cvolatile builtin mk-cvolatile List.L1 None make-prim-data!
    "CVolatile" 0 @a TVar List.L1 builtin mk-cvolatile make-prim-tag!
    a> drop

    "CVoid" List.L0 builtin cvoid builtin mk-cvoid List.L1 Phantom Some make-prim-data!
    "CVoid" 0 List.L0 builtin mk-cvoid make-prim-tag!

    "U64" builtin u64 "Int>U64-unsafe" builtin mk-u64 "uint64_t" make-prim-intlike!
    "U32" builtin u32 "Int>U32-unsafe" builtin mk-u32 "uint32_t" make-prim-intlike!
    "U16" builtin u16 "Int>U16-unsafe" builtin mk-u16 "uint16_t" make-prim-intlike!
    "U8"  builtin u8  "Int>U8-unsafe"  builtin mk-u8  "uint8_t"  make-prim-intlike!

    "I64" builtin i64 "Int>I64-unsafe" builtin mk-i64 "int64_t" make-prim-intlike!
    "I32" builtin i32 "Int>I32-unsafe" builtin mk-i32 "int32_t" make-prim-intlike!
    "I16" builtin i16 "Int>I16-unsafe" builtin mk-i16 "int16_t" make-prim-intlike!
    "I8"  builtin i8  "Int>I8-unsafe"  builtin mk-i8  "int8_t"  make-prim-intlike!

    "CChar"      builtin cchar      "CChar"      builtin mk-cchar      "char"         make-prim-intlike!
    "CIChar"     builtin cichar     "CIChar"     builtin mk-cichar     "signed char"  make-prim-intlike!
    "CShort"     builtin cshort     "CShort"     builtin mk-cshort     "short"        make-prim-intlike!
    "CInt"       builtin cint       "CInt"       builtin mk-cint       "int"          make-prim-intlike!
    "CLong"      builtin clong      "CLong"      builtin mk-clong      "long"         make-prim-intlike!
    "CLongLong"  builtin clonglong  "CLongLong"  builtin mk-clonglong  "long long"    make-prim-intlike!
    "CISize"     builtin cisize     "CISize"     builtin mk-cisize     "ssize_t"      make-prim-intlike!
    "CIntPtr"    builtin cintptr    "CIntPtr"    builtin mk-cintptr    "intptr_t"     make-prim-intlike!

    "CUChar"     builtin cuchar     "CUChar"     builtin mk-cuchar     "unsigned char"      make-prim-intlike!
    "CUShort"    builtin cushort    "CUShort"    builtin mk-cushort    "unsigned short"     make-prim-intlike!
    "CUInt"      builtin cuint      "CUInt"      builtin mk-cuint      "unsigned int"       make-prim-intlike!
    "CULong"     builtin culong     "CULong"     builtin mk-culong     "unsigned long"      make-prim-intlike!
    "CULongLong" builtin culonglong "CULongLong" builtin mk-culonglong "unsigned long long" make-prim-intlike!
    "CUSize"     builtin cusize     "CUSize"     builtin mk-cusize     "size_t"             make-prim-intlike!
    "CUIntPtr"   builtin cuintptr   "CUIntPtr"   builtin mk-cuintptr   "uintptr_t"          make-prim-intlike!)

def(data-qname, Data Name Int -- QName,
    >arity >name TYCON_DATA NAMESPACE_TYCON >namespace QName)

def(data-word-qname, Data Str Nat -- QName,
    dip(>Name) >Int data-qname)

########
# DATA #
########

def(Data.==, Data Data -- Bool, both(index) ==)
def(Data.num-tags, Data -- Nat, tags len)

field(Data.~last-tag-value, Data, Int)

||| Adds constructor to data type, and gives tag its value.
def Data.add-tag! [ +Mirth Tag Data -- +Mirth ] {
    >data >tag
    @tag ~value memoize(
        @data ~last-tag-value @? if-some(1+, 0)
    )
    @data tags find(value over ==) for(
        Str("Constructors ";
            name rdip:>Str ;
            " and " ;
            @tag name rdip:>Str ;
            " have the same tag value." ;
        )
        @data head? if-some(swap emit-error!, error!)
    )
    @data ~last-tag-value !
    data> ~tags modify(tag> List.Snoc)
}

def Data.is-enum-or-unit? [ Data -- Bool ] {
    dup is-enum? dip(is-unit? >Bool) ||
}

field(Data.~is-unit?, Data, Maybe(Tag))
def(Data.is-unit?, Data -- Maybe(Tag),
    dup ~is-unit? memoize(
        dup tags single? filter(num-total-inputs 0=)
    ) nip)

field(Data.~is-enum?, Data, Bool)
def(Data.is-enum?, Data -- Bool,
    dup ~is-enum? memoize(
        dup is-unit? not and(
            dup tags all(num-total-inputs 0=)
        )
    ) nip)

def Data.single-tag? [ Data -- Maybe(Tag) ] {
    tags single?
}

field(Data.~is-transparent?, Data, Bool)
def(Data.is-transparent?, Data -- Bool,
    dup ~is-transparent? memoize(
        dup is-resource? if(
            dup tags single? match(
                Some -> dup num-resource-inputs 1u == swap num-total-inputs 1u == &&,
                None -> False,
            ),
            dup tags single? match(
                Some -> dup num-type-inputs 1u == swap num-total-inputs 1u == &&,
                None -> False
            )
        )
    ) nip)

struct SemiTransparentData {
    tag: Tag
    input: StackTypePart
}

field(Data.~semi-transparent?, Data, Maybe(SemiTransparentData))
def(Data.semi-transparent?, +Mirth Data -- +Mirth Maybe(SemiTransparentData),
    dup ~semi-transparent? memoize(
        dup tags single? bind(
            dup inputs single? map(
                >input dup >tag SemiTransparentData
            ) nip
        )
    ) nip)

field(Data.~is-resource?, Data, Bool)
def(Data.is-resource?, Data -- Bool,
    dup ~is-resource? memoize(
        dup name could-be-resource-con
    ) nip)

def(Data.is-value-type?, Data -- Bool,
    is-resource? not)

def(Data.full-type, +Mirth Data -- +Mirth Type/Resource,
    dup sip:TData params for(TVar TApp)
    swap is-value-type? if(
        Left,
        Resource Right
    ))

#######
# TAG #
#######

table(Tag)
field(Tag.~data, Tag, Data)
field(Tag.~qname, Tag, QName)
field(Tag.~value, Tag, Int)
field(Tag.~label-inputs, Tag, List(Label))
field(Tag.~num-type-inputs, Tag, Nat)
field(Tag.~num-resource-inputs, Tag, Nat)
field(Tag.~sig?, Tag, Maybe(Token))
field(Tag.~ctx-type, Tag, Prop([Ctx ArrowType]))
field(Tag.~untag, Tag, Maybe(Word))

def(Tag.data, Tag -- Data, ~data @)
def(Tag.qname, Tag -- QName, ~qname @)
def(Tag.name, Tag -- Name, qname name)
def(Tag.value, Tag -- Int, ~value @)
def(Tag.label-inputs, Tag -- List(Label), ~label-inputs @)
def(Tag.num-type-inputs, Tag -- Nat, ~num-type-inputs @)
def(Tag.num-resource-inputs, Tag -- Nat, ~num-resource-inputs @)
def(Tag.sig?, Tag -- Maybe(Token), ~sig? @)
def(Tag.ctx-type, +Mirth Tag -- +Mirth Ctx ArrowType, ~ctx-type force! unpack2)
def(Tag.ctx, +Mirth Tag -- +Mirth Ctx, ctx-type drop)
def(Tag.type, +Mirth Tag -- +Mirth ArrowType, ctx-type nip)
def(Tag.untag, Tag -- Maybe(Word), ~untag @)

def(Tag.only-tag?, Tag -- Bool, .data tags single? >Bool)

def Tag.label-inputs-from-sig [ Tag -- List(Label) ] {
    sig? if-some(run-tokens filter-some(sig-label?), Nil)
}

def(Tag.num-type-inputs-from-sig, Tag -- Nat,
    dup sig? if-some(
        run-length
        over num-resource-inputs-from-sig minus
        swap label-inputs-from-sig len minus,
        drop 0u
    ))

def(Tag.num-resource-inputs-from-sig, Tag -- Nat,
    sig? if-some(
        run-tokens filter(could-be-sig-label? not) filter-some(name?)
        filter(or(could-be-resource-var, could-be-resource-con))
        len,
        0u
    ))

field(Tag.~num-label-inputs, Tag, Nat)
def(Tag.num-label-inputs, Tag -- Nat,
    dup ~num-label-inputs memoize(
        dup label-inputs len
    ) nip)

def(Tag.num-total-inputs, Tag -- Nat,
    >tag
    @tag num-label-inputs
    @tag num-type-inputs +
    tag> num-resource-inputs +)

def(Tag.is-transparent?, Tag -- Bool,
    .data is-transparent?)

field(Tag.~inputs, Tag, List(StackTypePart))
def(Tag.inputs, +Mirth Tag -- +Mirth List(StackTypePart),
    dup ~inputs memoize (
        dup type dom split-parts nip
    ) nip)

def(Tag.semi-transparent?, +Mirth Tag -- +Mirth Maybe(SemiTransparentData),
    .data semi-transparent?)

def(Tag.is-unit?, Tag -- Bool,
    .data is-unit? >Bool)

def(Tag.outputs-resource?, Tag -- Bool,
    .data is-resource?)

def(Tag.==, Tag Tag -- Bool, both(index) ==)

def Tag.prefer-inline? [ +Mirth Tag -- +Mirth Bool ] {
    dup .data semi-transparent?
    or(dup num-total-inputs 0=)
    nip
}

#
# Partial Data
#

||| DataPartial represents a data type with missing parts.
||| In general, it could represent missing fields, missing tags, etc.
||| For now, all we need to represent is a single missing field.
data(DataPartial, DataPartial ->
    data:Data field:Label)

def(DataPartial.==, DataPartial DataPartial -- Bool,
    /DataPartial /DataPartial
    data> data> ==
    field> field> == &&)

struct TagField {
    tag: Tag
    label: Label
    type/resource: Type/Resource
}

field(Tag.~fields, Tag, List(TagField))
def Tag.fields [ +Mirth Tag -- +Mirth List(TagField) ] {
    >tag
    @tag ~fields memoize (
        @tag type dom split-parts nip filter-some(
            match {
                { STPCons -> drop None }
                { STPWith -> drop None }
                { STPConsLabel -> @tag >tag >label Left  >type/resource TagField Some }
                { STPWithLabel -> @tag >tag >label Right >type/resource TagField Some }
            }
        )
    )
    tag> drop
}
