# This Source Code Form is subject to the terms of the Mozilla Public
# License, v. 2.0. If a copy of the MPL was not distributed with this
# file, You can obtain one at https://mozilla.org/MPL/2.0/.

import base/list
import base/str

export base/sexp
  type Sexp
  Atom : Str -- Sexp
  List : List(Sexp) -- Sexp
  sexpPair : Sexp Sexp -- Sexp
  sexp0(f: Str) : Sexp
  sexp1(f: Str, g: a -- Sexp) : a -- Sexp
  sexp2(f: Str, g: a -- Sexp, h: b -- Sexp) : a b -- Sexp
  sexp3(f: Str, g: a -- Sexp, h: b -- Sexp, i: c -- Sexp) : a b c -- Sexp
  sexp4(f: Str, g: a -- Sexp, h: b -- Sexp, i: c -- Sexp, j: d -- Sexp) : a b c d -- Sexp
  list->sexp(f: a -- Sexp) : List(a) -- Sexp
  sexp->str : Sexp -- Str
end

data Sexp
  Atom : Str -- Sexp
  List : List(Sexp) -- Sexp
end

sexp->str =
  match(
    Atom ->
      showStr,

    List ->
      dip("(")
      lmatchL(
        id,
        dip(sexp->str)
        for(dip(" ") sexp->str <> <>)
        <>
      )
      ")" <>
  )


sexpPair = dip2(nil) dip(cons) cons List

sexp0(f) = f Atom
sexp1(f,g) = dip(f Atom lpure) g cons List
sexp2(f,g,h) = dip2(f Atom lpure) dip(g cons) h cons List
sexp3(f,g,h,i) = dip3(f Atom lpure) dip2(g cons) dip(h cons) i cons List
sexp4(f,g,h,i,j) = dip4(f Atom lpure) dip3(g cons) dip2(h cons) dip(i cons) j cons List

list->sexp(f) = map(f) List
