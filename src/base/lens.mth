import base/unit

export base/lens
  type Lens(s,a)

  lens (get: s -- x a, set: x a -- s) : Lens(s,a)
  lensParam (get: s p -- x a, set: x a -- s) : p -- Lens(s,a)

  lensId : Lens(a,a)
  lensCp : Lens(a,b) Lens(b,c) -- Lens(a,c)
  lensCp3 : Lens(a,b) Lens(b,c) Lens(c,d) -- Lens(a,d)
  lensCp4 : Lens(a,b) Lens(b,c) Lens(c,d) Lens(d,e) -- Lens(a,e)

  @ : s Lens(s,a) -- a
  ! : s a Lens(s,a) -- s
  ^ (f: *x a -- *y a) : *x s Lens(s,a) -- *y s

  @@ : s1 Lens(s1,s2) Lens(s2,s3) -- s3
  !! : s1 s3 Lens(s1,s2) Lens(s2,s3) -- s1
  ^^ (f: *x s3 -- *y s3) : *x s1 Lens(s1,s2) Lens(s2,s3) -- *y s1

  @@@ : s1 Lens(s1,s2) Lens(s2,s3) Lens(s3,s4) -- s4
  !!! : s1 s4 Lens(s1,s2) Lens(s2,s3) Lens(s3,s4) -- s1
  ^^^ (f: *x s4 -- *y s4) : *x s1 Lens(s1,s2) Lens(s2,s3) Lens(s3,s4) -- *y s1

  @@@@ : s1 Lens(s1,s2) Lens(s2,s3) Lens(s3,s4) Lens(s4,s5) -- s5
  !!!! : s1 s5 Lens(s1,s2) Lens(s2,s3) Lens(s3,s4) Lens(s4,s5) -- s1
  ^^^^ (f: *x s5 -- *y s5) : *x s1 Lens(s1,s2) Lens(s2,s3) Lens(s3,s4) Lens(s4,s5) -- *y s1

  lens1/1 (c: x1 -- y, d: y -- x1) : Lens(y,x1)

  lens1/2 (c: x1 x2 -- y, d: y -- x1 x2) : Lens(y,x1)
  lens2/2 (c: x1 x2 -- y, d: y -- x1 x2) : Lens(y,x2)

  lens1/3 (c: x1 x2 x3 -- y, d: y -- x1 x2 x3) : Lens(y, x1)
  lens2/3 (c: x1 x2 x3 -- y, d: y -- x1 x2 x3) : Lens(y, x2)
  lens3/3 (c: x1 x2 x3 -- y, d: y -- x1 x2 x3) : Lens(y, x3)

  lens1/4 (c: x1 x2 x3 x4 -- y, d: y -- x1 x2 x3 x4) : Lens(y, x1)
  lens2/4 (c: x1 x2 x3 x4 -- y, d: y -- x1 x2 x3 x4) : Lens(y, x2)
  lens3/4 (c: x1 x2 x3 x4 -- y, d: y -- x1 x2 x3 x4) : Lens(y, x3)
  lens4/4 (c: x1 x2 x3 x4 -- y, d: y -- x1 x2 x3 x4) : Lens(y, x4)

  lens1/5 (c: x1 x2 x3 x4 x5 -- y, d: y -- x1 x2 x3 x4 x5) : Lens(y, x1)
  lens2/5 (c: x1 x2 x3 x4 x5 -- y, d: y -- x1 x2 x3 x4 x5) : Lens(y, x2)
  lens3/5 (c: x1 x2 x3 x4 x5 -- y, d: y -- x1 x2 x3 x4 x5) : Lens(y, x3)
  lens4/5 (c: x1 x2 x3 x4 x5 -- y, d: y -- x1 x2 x3 x4 x5) : Lens(y, x4)
  lens5/5 (c: x1 x2 x3 x4 x5 -- y, d: y -- x1 x2 x3 x4 x5) : Lens(y, x5)

end

data Lens(s,a)
  MkLens(get: s p -- x a, set: x a -- s) : p -- Lens(s,a)
end

lens(get, set) = unit MkLens(drop get, set)
lensParam(get, set) = MkLens(get, set)

lensId = lens1/1(id, id)
lensCp =
  match(MkLens(f2,g2) ->
    swap match(MkLens(f1,g1) ->
      swap pack2
      MkLens(
        unpack2 dip(f1) f2 dip(pack2),
        dip(unpack2) g2 g1
      )
    )
  )

lensCp3 = lensCp lensCp
lensCp4 = lensCp lensCp lensCp

@ = ^(dup) drop
! = dip(swap) ^(drop)
^(f) = match(MkLens(get,set) -> get dip'(f) set)

@@ = lensCp @
!! = lensCp !
^^(f) = lensCp ^(f)

@@@ = lensCp3 @
!!! = lensCp3 !
^^^(f) = lensCp3 ^(f)

@@@@ = lensCp4 @
!!!! = lensCp4 !
^^^^(f) = lensCp4 ^(f)

lens1/1(c,d) = lens(dip(unit) d, nip c)
lens2/2(c,d) = lens(d, c)
lens3/3(c,d) = lens(d dip(pack2), dip(unpack2) c)
lens4/4(c,d) = lens(d dip(pack3), dip(unpack3) c)
lens5/5(c,d) = lens(d dip(pack4), dip(unpack4) c)

lens1/2(c,d) = lens2/2(swap c, d swap)
lens2/3(c,d) = lens3/3(swap c, d swap)
lens3/4(c,d) = lens4/4(swap c, d swap)
lens4/5(c,d) = lens5/5(swap c, d swap)

lens1/3(c,d) = lens3/3(rotr c, d rotl)
lens2/4(c,d) = lens4/4(rotr c, d rotl)
lens3/5(c,d) = lens5/5(rotr c, d rotl)

lens1/4(c,d) = lens4/4(rot4r c, d rot4l)
lens2/5(c,d) = lens5/5(rot4r c, d rot4l)

lens1/5(c,d) = lens5/5(rot5r c, d rot5l)

