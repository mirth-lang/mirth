module(resource.output)

import(prelude)
import(data.str)
import(data.byte)
import(resource.buffer)
import(platform.posix)

def(OUTPUT_BUFFER_SIZE, Size, 4096)
data(+Output, +OUTPUT -> File Offset +Buffer)
def(-OUTPUT, +Output -- File Offset +Buffer, +OUTPUT -> id)

def(output-start!, File -- +Output,
    0 OUTPUT_BUFFER_SIZE
    allocate-buffer +OUTPUT)

def(output-end!, +Output --,
    output-flush
    -OUTPUT free-buffer drop close-file!)

def(output-file@, +Output -- File +Output, -OUTPUT over dip(+OUTPUT))
def(output-file!, File +Output -- +Output, dip(-OUTPUT nip) swap +OUTPUT)

def(output-offset@, +Output -- Offset +Output, -OUTPUT dup dip(+OUTPUT))
def(output-offset!, Offset +Output -- +Output, dip(-OUTPUT drop) +OUTPUT)

def(+Output~Buffer(f), (*a +Buffer -- *b +Buffer) *a +Output -- *b +Output,
    -OUTPUT dip2(f) +OUTPUT)

def(output-flush, +Output -- +Output,
    output-file@ ~Buffer(buffer-base) output-offset@ slice-write!
    0 output-offset!)

def(output-capacity-total, +Output -- Size +Output, ~Buffer(buffer-size))
def(output-capacity-remaining, +Output -- Size +Output,
    output-capacity-total output-offset@ -)
def(output-full?, +Output -- Bool +Output, output-capacity-remaining 0 <=)

def(Str+Output.put, Str +Output -- +Output,
    dup num-bytes output-capacity-remaining > if(
        output-flush
        output-file@ swap write!,

        dup output-offset@ ~Buffer(!Str)
        num-bytes output-offset@ + output-offset!
    ))

def(Byte+Output.put, Byte +Output -- +Output,
    output-full? then(output-flush)
    dip(-OUTPUT) >U8 over !U8 1+ +OUTPUT)

def(Int+Output.put, Int +Output -- +Output, int-to-str put)

def(+Output.line, +Output -- +Output, BLF put)
