module(resource.set)
import(prelude)
import(resource.buffer)

||| Represents a set as a bit string.
data(+Set(t), +SET -> [t -- Int] +Buffer)
def(-SET, +Set(t) -- [t -- Int] +Buffer, +SET -> id)
def(+Set~Buffer(f), (*a +Buffer -- *b +Buffer) *a +Set(t) -- *b +Set(t),
    -SET dip(f) +SET)
def(+Set.index, t +Set(t) -- Int +Set(t),
    -SET dup rdip(dip(run)) +SET
    expect!(dup 0>=, "negative index not allowed"))

def(+Set.new(f), (t -- Int) -- +Set(t), [f] 256 +Buffer.new +SET)
def(+Set.rdrop, +Set(t) --, -SET drop rdrop)

def(+Set.member?, t +Set(t) -- Bool +Set(t),
    index 8 /% over 1+ ~Buffer(expand!)
    1 swap << >U8 ~Buffer(swap @U8) & >Int 0 <>)
def(+Set.insert!, t +Set(t) -- +Set(t),
    index 8 /% over 1+ ~Buffer(expand!)
    1 swap << >U8 ~Buffer(over @U8 | swap !U8))
def(+Set.remove!, t +Set(t) -- +Set(t),
    index 8 /% over 1+ ~Buffer(expand!)
    1 swap << 0xFF ^ >U8 ~Buffer(over @U8 & swap !U8))
