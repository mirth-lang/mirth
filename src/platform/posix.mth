module(platform.posix)
import(prelude)
import(data.str)
import(data.path)

def(panic!, *a Str -- *b,
    "panic: " str-trace! str-trace-ln!
    ptr-nil u64@ # for debug
    1 posix-exit!)

data(File, FILE -> Int)
def(Int->File, Int -- File, FILE)
def(File->Int, File -- Int, FILE -> id)

def(file@, Ptr -- File, int@ Int->File)
def(file!, File Ptr --, dip(File->Int) int!)

def(stdin, File, 0 Int->File)
def(stdout, File, 1 Int->File)
def(stderr, File, 2 Int->File)

def(str-write!, Str File --,
    swap with-str-data(slice-write!))

def(slice-write!, File Ptr Int --,
    dip2(File->Int) dup dip(posix-write!)
    dup 0 < if("error: write failed!" panic!, id)
    > if("error: write output fewer bytes than expected!" panic!, id))

def(str-print!, Str --, stdout str-write!)
def(str-trace!, Str --, stderr str-write!)
def(str-print-ln!, Str --, str-print! print-ln!)
def(str-trace-ln!, Str --, str-trace! trace-ln!)

def(print-ln!, --, "\n" str-print!)
def(trace-ln!, --, "\n" str-trace!)

def(int-write!, Int File --, dip(int-to-str) str-write!)
def(int-print!, Int --, stdout int-write!)
def(int-trace!, Int --, stderr int-write!)
def(int-print-ln!, Int --, int-print! print-ln!)
def(int-trace-ln!, Int --, int-trace! trace-ln!)

def(with-open-file!(f,g), (*a File -- *b, *a -- *b) *a Str -- *b,
    0 0 posix-open! dup 0 < if(drop g, dup dip(Int->File f) posix-close! drop))

def(READ_FILE_BUF_SIZE, 0x1000)
buffer(READ_FILE_BUF, 0x1000)
def(read-file!, File -- Str,
    File->Int \(fp -> ""
    fp READ_FILE_BUF READ_FILE_BUF_SIZE prim-posix-read
    while(dup 0 >,
        READ_FILE_BUF swap prim-str-copy str-cat
        fp READ_FILE_BUF READ_FILE_BUF_SIZE prim-posix-read)
    0 < if("io error while reading file" panic!, id)))

def(open-file!, Str -- File,
    0 0 posix-open!
    dup 0 < if(
        "Failed to open file!" panic!,
        Int->File
    ))

def(create-file!, Str -- File,
    O_WRONLY|O_CREAT|O_TRUNC
    0x1B6
        # this is the default mode for creating a file
        # on unix, 666 in octal, i.e. owner can
        # read+write, everyone can read
        #  ---- NB: this is inaccurate . . .
    posix-open!
    dup 0 < if(
        "Failed to create file!" panic!,
        Int->File
    ))

def(O_WRONLY|O_CREAT|O_TRUNC, Int,
    RUNNING_OS match(
        OS_MACOS -> 0x601, # O_WRONLY = 0x1, O_CREAT = 0x200, O_TRUNC = 0x400
        OS_LINUX -> 0x241, # O_WRONLY = 0x1, O_CREAT = 0x40, O_TRUNC = 0x200
        OS_WINDOWS -> 0x301, # O_WRONLY = 0x1, O_CREAT|O_TRUNC = 0x300
        OS_UNKNOWN -> "O_WRONLY|O_CREAT|O_TRUNC on unknown os" panic!
    ))

def(close-file!, File --,
    File->Int posix-close!
    0 < if(
        "failed to close file." panic!,
        id
    ))

########
# STAT #
########

def-external(stat, RawPtr RawPtr -- Int)
buffer(POSIX_STAT_BUF, 256)

def(with-raw-path(f), (*a RawPtr -- *b) *a Path -- *b Path,
    sip(Path->Str prim-str-base with-raw-ptr(f) drop))

def(is-directory?, Path -- Path Bool,
    with-raw-path(
        POSIX_STAT_BUF with-raw-ptr(stat)
        swap 0 == if(
            st_mode@ S_ISDIR,
            drop false
        )
    ) swap)

#define S_IFMT  00170000
#define S_IFSOCK 0140000
#define S_IFLNK	 0120000
#define S_IFREG  0100000
#define S_IFBLK  0060000
#define S_IFDIR  0040000

def(S_IFMT, Int, 0xF000)
def(S_IFDIR, Int, 0x4000)
def(S_IFREG, Int, 0x8000)

def(S_ISDIR, U16 -- Bool, U16->Int S_IFMT & S_IFDIR ==)

def(st_mode@, Ptr -- U16,
    RUNNING_OS match(
        OS_LINUX -> 24, # ... this is all terrible and brittle
        OS_WINDOWS -> 6,
        _ -> drop 8
    ) swap with-ptr+(u16@))
