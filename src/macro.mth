module mirth.macro

import std.prelude
import std.maybe
import mirth.mirth
import mirth.name
import mirth.token
import mirth.name
import mirth.elab
import mirth.def

table(Macro)
field(Macro.~name, Macro, Name)
field(Macro.~qname, Macro, Prop(QName))
field(Macro.~arity, Macro, Int)
field(Macro.~head, Macro, Token)
field(Macro.~action, Macro, MacroAction)

def Macro.name [ Macro -- Name ] { ~name @ }
def Macro.qname-hard [ +Mirth Macro -- +Mirth QName ] { ~qname force! }
def Macro.qname-soft [ +Mirth Macro -- +Mirth Maybe(QName) ] { ~qname @? bind(ready?) }
def Macro.arity [ Macro -- Int ] { ~arity @ }
def Macro.head? [ Macro -- Maybe(Token) ] { ~head @? }
def Macro.action [ Macro -- MacroAction ] { ~action @ }

def Macro.>Int [ Macro -- Int ] { index }
def Macro.== [ Macro Macro -- Bool ] { on2:>Int == }

data MacroAction {
    MADecl  [ [ +World +Mirth Token -- +World +Mirth Token ] ]
    MAArrow [ [ +Mirth +AB -- +Mirth +AB ] ]
    --
    def decl?  { MADecl -> Some, _ -> drop None }
    def arrow? { MAArrow -> Some, _ -> drop None }
    def callable? { arrow? >Bool }
}

def Macro.Prim [ +Mirth Str MacroAction -- +Mirth Macro ] {
    Macro.alloc! >macro
    @macro ~action !
    dup >Name @macro ~name !
    -1 QName.Prim @macro PropLabel.MacroQName prop @macro ~qname !
    -1 @macro ~arity !
    macro>
}

def +Mirth.init-macros! [ +Mirth -- +Mirth ] {
    "import"        [ elab-module-import! ] MacroAction.MADecl Macro.Prim Def.DefMacro register
    "alias"         [ elab-alias!         ] MacroAction.MADecl Macro.Prim Def.DefMacro register
    "inline"        [ elab-inline!        ] MacroAction.MADecl Macro.Prim Def.DefMacro register
    "def"           [ elab-def!           ] MacroAction.MADecl Macro.Prim Def.DefMacro register
    "def-missing"   [ elab-def-missing!   ] MacroAction.MADecl Macro.Prim Def.DefMacro register
    "def-type"      [ elab-def-type!      ] MacroAction.MADecl Macro.Prim Def.DefMacro register
    "def-external"  [ elab-def-external!  ] MacroAction.MADecl Macro.Prim Def.DefMacro register
    "external"      [ elab-external!      ] MacroAction.MADecl Macro.Prim Def.DefMacro register
    "buffer"        [ elab-buffer!        ] MacroAction.MADecl Macro.Prim Def.DefMacro register
    "var"           [ elab-variable!      ] MacroAction.MADecl Macro.Prim Def.DefMacro register
    "table"         [ elab-table!         ] MacroAction.MADecl Macro.Prim Def.DefMacro register
    "field"         [ elab-field!         ] MacroAction.MADecl Macro.Prim Def.DefMacro register
    "data"          [ elab-data!          ] MacroAction.MADecl Macro.Prim Def.DefMacro register
    "struct"        [ elab-struct!        ] MacroAction.MADecl Macro.Prim Def.DefMacro register
    "embed-str"     [ elab-embed-str!     ] MacroAction.MADecl Macro.Prim Def.DefMacro register

    "match" [ elab-atom-match!    ] MacroAction.MAArrow Macro.Prim Def.DefMacro register
    "\\"    [ elab-atom-lambda!   ] MacroAction.MAArrow Macro.Prim Def.DefMacro register
}
