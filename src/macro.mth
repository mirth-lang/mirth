module mirth.macro

import std.prelude
import std.maybe
import mirth.mirth
import mirth.name
import mirth.token
import mirth.name
import mirth.elab
import mirth.def
import mirth.alias

table(Macro)
field(Macro.~name, Macro, Name)
field(Macro.~qname, Macro, Prop(QName))
field(Macro.~arity, Macro, Int)
field(Macro.~head, Macro, Token)
field(Macro.~action, Macro, MacroAction)

def Macro.name [ +Mirth Macro -- +Mirth Name ] { ~name @ }
def Macro.qname-hard [ +Mirth Macro -- +Mirth QName ] { ~qname force! }
def Macro.qname-soft [ +Mirth Macro -- +Mirth Maybe(QName) ] { ~qname @? bind(ready?) }
def Macro.arity [ +Mirth Macro -- +Mirth Int ] { ~arity @ }
def Macro.head? [ +Mirth Macro -- +Mirth Maybe(Token) ] { ~head @? }
def Macro.action [ +Mirth Macro -- +Mirth MacroAction ] { ~action @ }

def Macro.>Int [ Macro -- Int ] { index >Int }
def Macro.== [ Macro Macro -- Bool ] { on2:>Int == }

data MacroAction {
    Decl  [ [ +World +Mirth Token -- +World +Mirth Token ] ]
    Arrow [ [ +Mirth +AB -- +Mirth +AB ] ]
    --
    def decl?  { Decl -> Some, _ -> drop None }
    def arrow? { Arrow -> Some, _ -> drop None }
    def callable? { arrow? >Bool }
}

def Macro.Prim [ +Mirth Str MacroAction -- +Mirth Macro ] {
    Macro.alloc! >macro
    @macro ~action !
    dup >Name @macro ~name !
    -1 QName.Prim @macro PropLabel.MacroQName prop @macro ~qname !
    -1 @macro ~arity !
    macro>
}

def module-statement-error! [ +Mirth Token -- +Mirth Token ] {
    "'module' statement outside of module header." emit-fatal-error!
}
def import-statement-error! [ +Mirth Token -- +Mirth Token ] {
    "'import' statement outside of module header." emit-fatal-error!
}

def prim-decl-macro! { MacroAction.Decl Macro.Prim Def.Macro register }
def prim-word-macro! { MacroAction.Arrow Macro.Prim Def.Macro register }

def +Mirth.init-macros! [ +Mirth -- +Mirth ] {
    "module"             [ module-statement-error!  ] prim-decl-macro!
    "import"             [ import-statement-error!  ] prim-decl-macro!
    "patch"              [ elab-patch!              ] prim-decl-macro!
    "alias"              [ elab-alias!              ] prim-decl-macro!
    "inline"             [ elab-inline!             ] prim-decl-macro!
    "def"                [ elab-def!                ] prim-decl-macro!
    "def-missing"        [ elab-def-missing!        ] prim-decl-macro!
    "def-type"           [ elab-def-type!           ] prim-decl-macro!
    "external"           [ elab-external!           ] prim-decl-macro!
    "buffer"             [ elab-buffer!             ] prim-decl-macro!
    "table"              [ elab-table!              ] prim-decl-macro!
    "field"              [ elab-field!              ] prim-decl-macro!
    "data"               [ elab-data!               ] prim-decl-macro!
    "struct"             [ elab-struct!             ] prim-decl-macro!
    "embed-str"          [ elab-embed-str!          ] prim-decl-macro!
    "max-mirth-revision" [ elab-max-mirth-revision! ] prim-decl-macro!
    "min-mirth-revision" [ elab-min-mirth-revision! ] prim-decl-macro!

    "match" [ elab-atom-match!    ] MacroAction.Arrow Macro.Prim Def.Macro register
    "\\"    [ elab-atom-lambda!   ] MacroAction.Arrow Macro.Prim Def.Macro register
}
