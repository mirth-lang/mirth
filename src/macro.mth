module mirth.macro

import std.prelude
import std.maybe
import mirth.mirth
import mirth.name
import mirth.token
import mirth.name
import mirth.elab
import mirth.def
import mirth.alias

table(Macro)
field(Macro.~name, Macro, Name)
field(Macro.~qname, Macro, Prop(QName))
field(Macro.~arity, Macro, Int)
field(Macro.~head, Macro, Token)
field(Macro.~action, Macro, MacroAction)

def Macro.name [ +Mirth Macro -- +Mirth Name ] { ~name @ }
def Macro.qname-hard [ +Mirth Macro -- +Mirth QName ] { ~qname force! }
def Macro.qname-soft [ +Mirth Macro -- +Mirth Maybe(QName) ] { ~qname @? bind(ready?) }
def Macro.arity [ +Mirth Macro -- +Mirth Int ] { ~arity @ }
def Macro.head? [ +Mirth Macro -- +Mirth Maybe(Token) ] { ~head @? }
def Macro.action [ +Mirth Macro -- +Mirth MacroAction ] { ~action @ }

def Macro.>Int [ Macro -- Int ] { index }
def Macro.== [ Macro Macro -- Bool ] { on2:>Int == }

data MacroAction {
    Decl  [ [ +World +Mirth Token -- +World +Mirth Token ] ]
    Arrow [ [ +Mirth +AB -- +Mirth +AB ] ]
    --
    def decl?  { Decl -> Some, _ -> drop None }
    def arrow? { Arrow -> Some, _ -> drop None }
    def callable? { arrow? >Bool }
}

def Macro.Prim [ +Mirth Str MacroAction -- +Mirth Macro ] {
    Macro.alloc! >macro
    @macro ~action !
    dup >Name @macro ~name !
    -1 QName.Prim @macro PropLabel.MacroQName prop @macro ~qname !
    -1 @macro ~arity !
    macro>
}

def module-statement-error! [ +Mirth Token -- +Mirth Token ] {
    "'module' statement outside of module header." emit-fatal-error!
}
def import-statement-error! [ +Mirth Token -- +Mirth Token ] {
    "'import' statement outside of module header." emit-fatal-error!
}

def +Mirth.init-macros! [ +Mirth -- +Mirth ] {
    "module" [ module-statement-error! ] MacroAction.Decl Macro.Prim Def.Macro register
    "import" [ import-statement-error! ] MacroAction.Decl Macro.Prim Def.Macro register

    "alias"         [ elab-alias!         ] MacroAction.Decl Macro.Prim Def.Macro register
    "inline"        [ elab-inline!        ] MacroAction.Decl Macro.Prim Def.Macro register
    "def"           [ elab-def!           ] MacroAction.Decl Macro.Prim Def.Macro register
    "def-missing"   [ elab-def-missing!   ] MacroAction.Decl Macro.Prim Def.Macro register
    "def-type"      [ elab-def-type!      ] MacroAction.Decl Macro.Prim Def.Macro register
    "external"      [ elab-external!      ] MacroAction.Decl Macro.Prim Def.Macro register
    "buffer"        [ elab-buffer!        ] MacroAction.Decl Macro.Prim Def.Macro register
    "table"         [ elab-table!         ] MacroAction.Decl Macro.Prim Def.Macro register
    "field"         [ elab-field!         ] MacroAction.Decl Macro.Prim Def.Macro register
    "data"          [ elab-data!          ] MacroAction.Decl Macro.Prim Def.Macro register
    "struct"        [ elab-struct!        ] MacroAction.Decl Macro.Prim Def.Macro register
    "embed-str"     [ elab-embed-str!     ] MacroAction.Decl Macro.Prim Def.Macro register

    "match" [ elab-atom-match!    ] MacroAction.Arrow Macro.Prim Def.Macro register
    "\\"    [ elab-atom-lambda!   ] MacroAction.Arrow Macro.Prim Def.Macro register
}
