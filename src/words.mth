module(words)
import(prelude)
import(names)
import(types)

def(init-words!, --,
    0 NUM_WORDS !)

#########
# WORDS #
#########

nominal(Word, Int)
def(Word->Int, Word -- Int, cast)
def(Int->Word, Int -- Word, cast)

def(MAX_WORDS, Size, 0x4000)
quad def-static-buffer(NUM_WORDS)
def(word-alloc!, Word +Words,
    NUM_WORDS @ dup 1+ NUM_WORDS !
    dup MAX_WORDS >= if(
        "compiler error: NUM_WORDS > MAX_WORDS" panic!,
        id
    )
    0 over word-name!
    0 over word-sig!
    0 over word-body!
    0 over word-status!
    ctx-empty over word-ctx!
    TYPE_ERROR over word-type!)

MAX_WORDS |Name| * def-static-buffer(WORD_NAME)
def(word-name&, Word -- Ptr, Word->Int |Name| * WORD_NAME +)
def(word-name@, Word -- Name, word-name& name@)
def(word-name!, Name Word --, word-name& name!)

MAX_WORDS |Token| * def-static-buffer(WORD_SIG)
def(word-sig&, Word -- Ptr, Word->Int |Token| * WORD_SIG +)
def(word-sig@, Word -- Token, word-sig& token@)
def(word-sig!, Token Word --, word-sig& token!)

MAX_WORDS |Token| * def-static-buffer(WORD_BODY)
def(word-body&, Word -- Ptr, Word->Int |Token| * WORD_BODY +)
def(word-body@, Word -- Token, word-body& token@)
def(word-body!, Token Word --, word-body& token!)

MAX_WORDS |Ctx| * def-static-buffer(WORD_CTX)
def(word-ctx&, Word -- Ptr, Word->Int |Ctx| * WORD_CTX +)
def(word-ctx@, Word -- Ctx, word-ctx& ctx@)
def(word-ctx!, Ctx Word --, word-ctx& ctx!)

MAX_WORDS |Type| * def-static-buffer(WORD_TYPE)
def(word-type&, Word -- Ptr, Word->Int |Type| * WORD_TYPE +)
def(word-type@, Word -- Type, word-type& type@)
def(word-type!, Type Word --, word-type& type!)

def(word-dom@, Word -- Type, word-type@ type-get-morphism morphism-type-dom@)
def(word-cod@, Word -- Type, word-type@ type-get-morphism morphism-type-cod@)

MAX_WORDS def-static-buffer(WORD_STATUS)
nominal(WordStatus, U8)
def(WordStatus->U8, WordStatus -- U8, cast)
def(U8->WordStatus, U8 -- WordStatus, cast)
def(WordStatus->Int, WordStatus -- Int, WordStatus->U8 U8->Int)
def(Int->WordStatus, Int -- WordStatus, Int->U8 U8->WordStatus)
def(word-status&, Word -- Ptr, Word->Int WORD_STATUS +)
def(word-status!, WordStatus Word --, dip(WordStatus->U8) word-status& u8!)
def(word-status@, Word -- WordStatus, word-status& u8@ U8->WordStatus)
def(word-status?, Word -- Word WordStatus, dup word-status@)

nominal(WordStatusFlag, Int)
def(Int->WordStatusFlag, Int -- WordStatusFlag, cast)
def(WordStatusFlag->Int, WordStatusFlag -- Int, cast)
def(word-status-get, WordStatus WordStatusFlag -- Bool,
    dip(WordStatus->Int) WordStatusFlag->Int & 0 !=)
def(word-status-set, WordStatus WordStatusFlag -- WordStatus,
    dip(WordStatus->Int) WordStatusFlag->Int | Int->WordStatus)

def(WORD_SIG_CHECKED, WordStatusFlag, 0x01 Int->WordStatusFlag)
def(word-sig-is-checked?, Word -- Word Bool, word-status? WORD_SIG_CHECKED word-status-get)
def(word-sig-is-checked!, Word --, word-status? WORD_SIG_CHECKED word-status-set swap word-status!)

def(WORD_BODY_CHECKED, WordStatusFlag, 0x02 Int->WordStatusFlag)
def(word-body-is-checked?, Word -- Word Bool, word-status? WORD_BODY_CHECKED word-status-get)
def(word-body-is-checked!, Word --, word-status? WORD_BODY_CHECKED word-status-set swap word-status!)

#############
# MORPHISMS #
#############

# Each Mirth word is represented by a morphism in some category
# (or some class of categories). We provide special support for
# the morphisms of symmetric monoidal category theory, since we
# want to manipulate multiple pieces of data, like a string diagram,
# but we do not assume commutativity (i.e. that dip(f) g == g dip(f)),
# so that we can more easily represent computational effects like IO.
# As such, we actually provide special support for premonoidal
# categories, and we're really working with ordered string diagrams.

# The representation, for now, involves building up morphisms as a
# sequence of atomic morphisms. Atomic morphisms are morphisms that
# can't be broken down into a composition. These include calling
# words, primitives, control flow, and literal values. This is a
# nested intermediate representation, but, to reduce indirection,
# we represent it as a flat sequence.
