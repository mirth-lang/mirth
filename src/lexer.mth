module mirth.lexer

import std.prelude
import std.byte
import std.path
import std.str
import std.maybe
import std.list
import std.ctypes
import std.world
import std.file
import std.input

import mirth.location
import mirth.name
import mirth.label
import mirth.token
import mirth.mirth
import mirth.module


#########
# LEXER #
#########

data(+Lexer, +Lexer ->
    lexer-module:Module
    lexer-row:Row
    lexer-col:Col
    lexer-stack:List(Token)
    lexer-last-token:Token
    +input: +Input)

def(lexer-stack-push!, Token +Lexer -- +Lexer,
    lexer-stack List.Cons lexer-stack!)
def(lexer-stack-pop!, +Lexer -- Maybe(Token) +Lexer,
    lexer-stack uncons lexer-stack!)
def(lexer-stack-drop, +Lexer -- +Lexer,
    lexer-stack-pop! drop)
def(lexer-stack-peek, +Lexer -- Maybe(Token) +Lexer,
    lexer-stack first)

def-missing(+Lexer.lexer-col, +Lexer -- +Lexer Col, lexer-col)
def-missing(+Lexer.lexer-row, +Lexer -- +Lexer Row, lexer-row)
def-missing(+Lexer.lexer-module, +Lexer -- +Lexer Module, lexer-module)
def-missing(+Lexer.lexer-stack, +Lexer -- +Lexer List(Token), lexer-stack)
def-missing(+Lexer.lexer-last-token, +Lexer -- +Lexer Token, lexer-last-token)

def(run-lexer!, Path +World +Mirth -- Module +World +Mirth,
    Module.new! dup >lexer-module
    source-path rswap open-file! unwrap!(rswap fatal-error!) +Input.start! >+input rswap
    1 Row >lexer-row
    1 Col >lexer-col
    List.L0 >lexer-stack
    Token.alloc-none! dup >lexer-last-token
    +Lexer
    while(done? not, lexer-next!)
    TokenValue.TokenNone lexer-emit!
    /+Lexer
    rdip(+input> end! close-file!)
    lexer-stack> uncons drop for("Mismatched left parenthesis." emit-fatal-error!)
    lexer-row> lexer-col> lexer-last-token> drop3
    lexer-module> Token.alloc-none! over ~end !
    swap succ over ~start !)

# Is the lexer done?
def(+Lexer.done?, +Lexer -- Bool +Lexer, +input:done?)

# Create a token, and add it to the token buffer,
# returning the new token.
def(lexer-make!, TokenValue +Lexer -- Token +Lexer,
    Token.alloc!
    tuck ~value !
    lexer-module over ~module !
    lexer-row over ~row !
    lexer-col over ~col !
    dup lexer-last-token!)

# Create a token, and add it to the token buffer.
def(lexer-emit!, TokenValue +Lexer -- +Lexer, lexer-make! drop)

# Lex the next token.
def(lexer-next!, +Mirth +Lexer -- +Mirth +Lexer, lexer-peek match(
    BLF -> lexer-newline! lexer-move!,
    BSPACE -> lexer-move!,
    BHT -> lexer-move!,
    BVT -> lexer-move!,
    BCR -> lexer-move!,
    BHASH -> lexer-skip-comment! lexer-move!,
    BCOMMA -> lexer-close-colons! TokenValue.TokenComma lexer-emit! lexer-move!,
    BRPAREN -> lexer-close-colons! lexer-emit-rparen! lexer-move!,
    BRSQUARE -> lexer-close-colons! lexer-emit-rsquare! lexer-move!,
    BRCURLY -> lexer-close-colons! lexer-emit-rcurly! lexer-move!,
    BCOLON -> lexer-prepare-for-args! lexer-emit-lcolon! lexer-move!,
    BLPAREN -> lexer-prepare-for-args! lexer-emit-lparen! lexer-move!,
    BLSQUARE -> lexer-prepare-for-atom! lexer-emit-lsquare! lexer-move!,
    BLCURLY -> lexer-prepare-for-atom! lexer-emit-lcurly! lexer-move!,
    BQUOTE -> lexer-prepare-for-atom! lexer-emit-string! lexer-move!,
    _ -> is-name-byte if(
        lexer-prepare-for-atom! lexer-emit-name!,
        "Unrecognized byte." lexer-emit-fatal-error!
    )))

def(lexer-newline!, +Lexer -- +Lexer,
    lexer-row:1+
    0 Col lexer-col!)

def(lexer-emit-lcolon!, +Lexer -- +Lexer,
    TokenValue.TokenLColonOpen lexer-make!
    lexer-stack-push!)

def(lexer-close-colons!, +Lexer -- +Lexer,
    while-some(
        lexer-stack-peek filter(lcolon-open?),
        lexer-stack-drop
        dup TokenValue.TokenRColon lexer-make!
        TokenValue.TokenLColon swap ~value !
    ))
def(lexer-prepare-for-atom!, +Lexer -- +Lexer,
    lexer-last-token lcolon-open? else(lexer-close-colons!))
def(lexer-prepare-for-args!, +Lexer -- +Lexer,
    lexer-last-token name/dname? else(lexer-close-colons!))

def(lexer-emit-lparen!, +Lexer -- +Lexer,
    TokenValue.TokenLParenOpen lexer-make!
    lexer-stack-push!)
def(lexer-emit-rparen!, +Mirth +Lexer -- +Mirth +Lexer,
    lexer-stack-pop! match(
        None -> "Mismatched right parenthesis." lexer-emit-fatal-error!,
        Some -> dup lparen-open? if(
            dup TokenValue.TokenRParen lexer-make!
            TokenValue.TokenLParen swap ~value !,
            "Mismatched right parenthesis." lexer-emit-fatal-error!
        )
    ))

def(lexer-emit-lsquare!, +Lexer -- +Lexer,
    TokenValue.TokenLSquareOpen lexer-make!
    lexer-stack-push!)
def(lexer-emit-rsquare!, +Mirth +Lexer -- +Mirth +Lexer,
    lexer-stack-pop! match(
        None -> "Mismatched right square bracket." lexer-emit-fatal-error!,
        Some -> dup lsquare-open? if(
            dup TokenValue.TokenRSquare lexer-make!
            TokenValue.TokenLSquare swap ~value !,
            "Mismatched right square bracket." lexer-emit-fatal-error!
        )
    ))

def(lexer-emit-lcurly!, +Lexer -- +Lexer,
    TokenValue.TokenLCurlyOpen lexer-make!
    lexer-stack-push!)
def(lexer-emit-rcurly!, +Mirth +Lexer -- +Mirth +Lexer,
    lexer-stack-pop! match(
        None -> "Mismatched right curly brace." lexer-emit-fatal-error!,
        Some -> dup lcurly-open? if(
            dup TokenValue.TokenRCurly lexer-make!
            TokenValue.TokenLCurly swap ~value !,
            "Mismatched right curly brace." lexer-emit-fatal-error!
        )
    ))

def(lexer-emit-name!, +Lexer -- +Lexer,
    rdip("" thaw)
    lexer-module
    lexer-row
    lexer-col

    lexer-peek while(dup is-name-byte,
        rdip(push-byte-unsafe!)
        lexer-move!
        lexer-peek)
    drop

    rdip(is-doc-start?) if(
        drop3
        lexer-skip-doc!,
        # TODO: save the doc in the token-doc field for the next token
        #   https://github.com/mirth-lang/mirth/issues/240

        rdip(is-float? if(
            float? TokenValue.TokenF64,
            is-int? if(
                int? TokenValue.TokenInt,
                dname? match(
                    Some -> TokenValue.TokenDName,
                    None ->
                        label-token? match(
                            Some -> id,
                            None -> name? TokenValue.TokenName
                        )
                )
            )
        ))

        Token.alloc!
        tuck ~value !
        tuck ~col !
        tuck ~row !
        tuck ~module !
        lexer-last-token!
    )
    rdip(freeze drop))

def(+Str.name?, +Str -- Name +Str, dup! >Name)

def(+Str.first-byte, +Str -- Byte +Str, 0u >UOffset byte@)
def(+Str.second-byte, +Str -- Byte +Str, 1u >UOffset byte@)
def(+Str.third-byte, +Str -- Byte +Str, 2u >UOffset byte@)

def(+Str.first-two-bytes, +Str +Unsafe -- Str +Str +Unsafe, 2u >USize +Str.take-slice)
def(+Str.last-two-bytes, +Str +Unsafe -- Str +Str +Unsafe,
    rdip:num-bytes? >UOffset pred pred +Str.drop-slice)

def(+Str.drop-first-byte, +Str +Unsafe -- Str +Str +Unsafe, 1u >UOffset +Str.drop-slice)
def(+Str.drop-first-two-bytes, +Str +Unsafe -- Str +Str +Unsafe, 2u >UOffset +Str.drop-slice)
def(+Str.drop-last-byte, +Str +Unsafe -- Str +Str +Unsafe,
    rdip:num-bytes? pred +Str.take-slice)
def(+Str.drop-last-two-bytes, +Str +Unsafe -- Str +Str +Unsafe,
    rdip:num-bytes? pred pred +Str.take-slice)

def(+Str.label-token?, +Str -- Maybe(TokenValue) +Str,
    label-push-token?
    or-some(label-push-r-token?)
    or-some(label-pop-token?)
    or-some(label-pop-r-token?)
    or-some(label-get-token?)
    or-some(label-set-token?))

def(+Str.label-pop-token?, +Str -- Maybe(TokenValue) +Str,
    first-byte is-lower and(last-byte Byte.B'>' ==) if(
        unsafe(+Str.drop-last-byte) >Name Label.new! TokenValue.TokenLabelPop Maybe.Some,
        Maybe.None
    ))

def(+Str.label-pop-r-token?, +Str -- Maybe(TokenValue) +Str,
    first-byte Byte.B'+' == and(second-byte is-lower and(last-byte Byte.B'>' ==)) if(
        unsafe(+Str.drop-last-byte) >Name Label.new! TokenValue.TokenLabelPopR Maybe.Some,
        Maybe.None
    ))

def(+Str.label-push-token?, +Str -- Maybe(TokenValue) +Str,
    first-byte Byte.B'>' == and(second-byte is-lower) if(
        unsafe(+Str.drop-first-byte) >Name Label.new! TokenValue.TokenLabelPush Maybe.Some,
        Maybe.None
    ))

def(+Str.label-push-r-token?, +Str -- Maybe(TokenValue) +Str,
    unsafe(+Str.first-two-bytes) ">+" == and(third-byte is-lower) if(
        unsafe(+Str.drop-first-byte) >Name Label.new! TokenValue.TokenLabelPushR Maybe.Some,
        Maybe.None
    ))

def(+Str.label-get-token?, +Str -- Maybe(TokenValue) +Str,
    first-byte Byte.B'@' == and(second-byte is-lower or(second-byte Byte.B'+' == and(third-byte is-lower))) if(
        unsafe(+Str.drop-first-byte) >Name Label.new! TokenValue.TokenLabelGet Maybe.Some,
        Maybe.None
    ))

def(+Str.label-set-token?, +Str -- Maybe(TokenValue) +Str,
    first-byte Byte.B'!' == and(second-byte is-lower) if(
        unsafe(+Str.drop-first-byte) >Name Label.new! TokenValue.TokenLabelSet Maybe.Some,
        Maybe.None
    ))

def(+Str.dname?, +Str -- Maybe(DName) +Str,
    Byte.BDOT +Str.split-byte
    uncons >List+? match(
        None -> drop Maybe.None,
        Some ->
            dip(dup Str.empty? if(drop Maybe.None, >Name Maybe.Some))
            map(>Name) DName Maybe.Some
    ))

def(+Str.is-doc-start?, +Str -- Bool +Str,
    dup! "|||" ==)

def(+Str.is-arrow?, +Str -- Bool +Str,
    +Str.dup! "->" ==)

def(+Str.is-dashes?, +Str -- Bool +Str,
    dup! "--" ==)

def(+Str.is-float?, +Str -- Bool +Str,
    0u >USize
    0u >UOffset

    dup byte@ is-sign if(1+, id)

    while(dup byte@ is-digit, dip(1+) 1+)

    dup byte@ Byte.BDOT == if(
        dip(1+) 1+ while(dup byte@ is-digit, dip(1+) 1+)
        swap >Int 3 >= if(
            num-bytes? >UOffset ==,
            drop Bool.False
        ),
        drop2 Bool.False
    )
)

||| Process float sign and return initial state for rest float lexing.
||| returns: (sign multiplier) (str-buf index)
def(+Str.float-sign, +Str -- Byte UOffset +Str,
    0u >UOffset byte@ byte-sign-value-index-float)

def byte-sign-value-index-float [ Byte -- Byte UOffset ] {
    { B'-' -> Byte.B'-' 1u >UOffset }
    { B'+' -> Byte.B'+' 1u >UOffset }
    { _ -> drop Byte.B'+' 0u >UOffset }
}

def(+Str.float?, +Str -- F64 +Str,
    freeze dup thaw >F64? unwrap(0.0))

def(+Str.is-int?, +Str -- Bool +Str,
    is-dec-int?
    or(is-hex-int?)
    or(is-oct-int?))

def(+Str.is-dec-int?, +Str -- Bool +Str,
    0u >USize # number of digits
    0u >UOffset # current index
    dup byte@ is-sign if(1+, id)
    while(dup byte@ is-digit, dip(1+) 1+)
    swap 0> if(num-bytes? >UOffset ==, drop Bool.False))

def(+Str.is-hex-int?, +Str -- Bool +Str,
    0u >USize # number of digits
    0u >UOffset # current index
    dup byte@ is-sign if(
        1+,
        id
    )
    dup byte@ Byte.B'0' == if(
        1+
        dup byte@ Byte.B'x' == if(
            1+
            while(dup byte@ is-hexdigit, dip(1+) 1+)
            swap 0> if(
                num-bytes? >UOffset ==,
                drop Bool.False
            ),

            drop2 Bool.False
        ),

        drop2 Bool.False
    ))

def(+Str.is-oct-int?, +Str -- Bool +Str,
    0u >USize # number of digits
    0u >UOffset # current index
    dup byte@ is-sign if(
        1+,
        id
    )
    dup byte@ Byte.B'0' == if(
        1+
        dup byte@ Byte.B'o' == if(
            1+
            while(dup byte@ Byte.B'0' Byte.B'7' in-range, dip(1+) 1+)
            swap 0> if(
                num-bytes? >UOffset ==,
                drop Bool.False
            ),

            drop2 Bool.False
        ),

        drop2 Bool.False
    ))

def(+Str.int?, +Str -- Int +Str,
    is-dec-int? if(
        dec-int?,
	is-hex-int? if(
            hex-int?,
	    oct-int?
	)
    ))

||| process int sign and return initial state for rest of int lexing.
||| returns: (sign multiplier) (accumulated value == 0) (str-buf index)
def(+Str.int-sign, +Str -- Int Int UOffset +Str,
    0u >UOffset byte@ byte-sign-value-index)

def(byte-sign-value-index, Byte -- Int Int UOffset,
    B'-' -> -1 0 1u >UOffset,
    B'+' -> +1 0 1u >UOffset,
    _ -> drop +1 0 0u >UOffset)

def(+Str.dec-int?, +Str -- Int +Str,
    int-sign
    while(dup num-bytes? >UOffset <,
        sip(
            byte@ >Int
            dip(10 *) 48 - +
        )
        1+)
    drop *)

def(+Str.hex-int?, +Str -- Int +Str,
    int-sign 1+ 1+ # skip 0x prefix
    while(dup num-bytes? >UOffset <,
        sip(
            byte@
            dip(16 *) hexdigit-value +
        )
        1+)
    drop *)

def(+Str.oct-int?, +Str -- Int +Str,
    int-sign 1+ 1+ # skip 0x prefix
    while(dup num-bytes? >UOffset <,
        sip(
            byte@ >Int
            dip(8 *) 48 - +
        )
        1+)
    drop *)

def(hexdigit-value, Byte -- Int,
    dup is-digit if(
        >Int 48 -,
        >Int 55 -
    ))

def(lexer-emit-string!, +Mirth +Lexer -- +Mirth +Lexer,
    rdip("" thaw)
    TokenValue.TokenNone lexer-make!

    lexer-move!
    lexer-peek while(dup is-string-end not,
        lexer-push-string-byte!
        lexer-move!
        lexer-peek
    )
    Byte.BQUOTE == else(
        "String literal is missing end quote (\")."
        rdip':lexer-emit-fatal-error!
    )

    rdip(freeze TokenValue.TokenStr)
    swap ~value !)

def(lexer-push-string-byte!, +Mirth +Str +Lexer Byte -- +Mirth +Str +Lexer,
    B'\' -> lexer-move! lexer-peek lexer-push-string-escape-byte!,
    _ -> rdip(push-byte-unsafe!))

def(lexer-push-string-escape-byte!, +Mirth +Str +Lexer Byte -- +Mirth +Str +Lexer,
    BLF -> lexer-newline!,
    B'n' -> Byte.BLF rdip(push-byte-ascii!),
    B'r' -> Byte.BCR rdip(push-byte-ascii!),
    B't' -> Byte.BHT rdip(push-byte-ascii!),
    B'\' -> Byte.B'\' rdip(push-byte-ascii!),
    BQUOTE -> Byte.BQUOTE rdip(push-byte-ascii!),
    _ -> rdip(push-byte-unsafe!)
    "Unknown character escape sequence." rdip':lexer-emit-warning!)

def(lexer-skip-comment!, +Lexer -- +Lexer,
    while(lexer-comment-end? not, lexer-move!)
    lexer-peek Byte.BLF == then(lexer-newline!))

def(lexer-skip-doc!, +Lexer -- +Lexer, while(lexer-comment-end? not, lexer-move!))

def(lexer-comment-end?, +Lexer -- Bool +Lexer,
    done? if(
        Bool.True,
        lexer-peek dup Byte.BLF == nip
    ))

def(lexer-peek, +Lexer -- Byte +Lexer, +input:peek)
def(lexer-move!, +Lexer -- +Lexer,
    +input:move!
    lexer-col:1+ )

def(lexer-location, +Lexer -- Location +Lexer,
    lexer-module >module
    lexer-row >row
    lexer-col >col
    Location)

def(lexer-emit-warning!, Str +Mirth +Lexer -- +Mirth +Lexer, dip(lexer-location) rdip:emit-warning-at!)
def(lexer-emit-error!, Str +Mirth +Lexer -- +Mirth +Lexer, dip(lexer-location) rdip:emit-error-at!)
def(lexer-emit-fatal-error!, *a Str +Mirth +Lexer -- *b, dip(lexer-location) rswap emit-fatal-error-at!)
