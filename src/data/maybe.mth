module(data.maybe)
import(prelude)

data(Maybe(t), NONE, SOME -> t)

def(is-none, Maybe(t) -- Bool, is-nil)
def(is-some, Maybe(t) -- Bool, is-nil not)
def(is-none?, Maybe(t) -- Maybe(t) Bool, is-nil?)
def(is-some?, Maybe(t) -- Maybe(t) Bool, is-nil? not)

def(unwrap, Maybe(t) -- t,
    NONE -> "tried to unwrap NONE" panic!,
    SOME -> id)

def(maybe-map(f), *c Maybe(a) [*c a -- *c b] -- *c Maybe(b),
    NONE -> NONE,
    SOME -> f SOME)

def(maybe-bind(f), *c Maybe(a) [*c a -- *c Maybe(b)] -- *c Maybe(b),
    NONE -> NONE,
    SOME -> f)

def(maybe-for(f), *c Maybe(a) [*c a -- *c] -- *c,
    NONE -> id,
    SOME -> f)

def(maybe-filter(f), *c Maybe(a) [*c a -- *c a Bool] -- *c Maybe(a),
    NONE -> NONE,
    SOME -> f if(SOME, drop NONE))

def(while-some(f,g), *a [*a b -- *a] [*a -- *a Maybe(b)] -- *a,
    f while(is-some?, unwrap g f) drop)
