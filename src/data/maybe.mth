module(data.maybe)

import(prelude)
import(data.list)

data(Maybe(t), NONE, SOME -> t)

def(Maybe>List, Maybe(t) -- List(t),
    NONE -> L0,
    SOME -> L1)

def(Maybe.is-none, Maybe(t) -- Bool,
    NONE -> T,
    SOME -> drop F)
def(Maybe.is-some, Maybe(t) -- Bool, is-none not)

def(Maybe.unwrap, Maybe(t) -- t,
    NONE -> "tried to unwrap NONE" panic!,
    SOME -> id)

def(Maybe.unwrap-or(f), (*a -- *a t) *a Maybe(t) -- *a t,
    NONE -> f,
    SOME -> id)

def(Maybe.map(f), (*c a -- *c b) *c Maybe(a) -- *c Maybe(b),
    NONE -> NONE,
    SOME -> f SOME)

def(Maybe.bind(f), (*c a -- *c Maybe(b)) *c Maybe(a) -- *c Maybe(b),
    NONE -> NONE,
    SOME -> f)

def(Maybe.for(f), (*c a -- *c) *c Maybe(a) -- *c,
    NONE -> id,
    SOME -> f)

def(Maybe.filter(f), (*c a -- *c a Bool) *c Maybe(a) -- *c Maybe(a),
    NONE -> NONE,
    SOME -> f if(SOME, drop NONE))

def(while-some(f,g), (*a -- *a Maybe(b), *a b -- *a) *a -- *a,
    f while(dup is-some, unwrap g f) drop)
