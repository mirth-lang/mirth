module(std.str)

import(std.prelude)
import(std.list)
import(std.maybe)
import(std.byte)
import(std.buffer)
import(std.ctypes)

alias(Str.cat, prim-str-cat)
inline(
    def(Str.num-bytes, Str -- USize, prim-str-num-bytes .u >USize)
    def(Str.empty?, Str -- Bool, num-bytes 0=)
)

||| Copy string bytes to pointer location (without adding 0 at end).
||| Return first unwritten location in second argument.
def(Ptr.!Str+, Str Ptr +Unsafe -- Ptr +Unsafe,
    swap with-data(
        rotl
        dup2 dip(>UOffset) uoffset
        dip(copy)
    ))

inline(
    def(Str.ClonePtrSlice, Ptr USize +Unsafe -- Str +Unsafe, >Int prim-str-copy)
    def(Str.CloneCStr, CStr +Unsafe -- Str +Unsafe, sip:>Ptr num-bytes >USize Str.ClonePtrSlice)
)

data(+Str, +Str -> Str)

inline (
    def(Str.thaw, Str -- +Str, +Str)
    def(+Str.freeze, +Str -- Str, +Str -> id)
    def(+Str.rdrop, +Str --, +Str -> drop)
    def(Str(f), (*a +Str -- *b +Str) *a -- *b Str, "" thaw f freeze)
    def(+Str.;, Str +Str -- +Str, push-str!)
)

def(+Str.dup!, +Str -- Str +Str,
    +Str -> dup dip(+Str))

def(+Str.num-bytes?, +Str -- USize +Str,
    +Str -> dup num-bytes dip(+Str))

def Str.last-byte [ Str -- Byte ] {
    dup num-bytes 1- >UOffset (
        swap Str.byte@,
        drop2 BNUL
    )
}

def(+Str.last-byte, +Str -- Byte +Str,
    dup! Str.last-byte)

def(+Str.byte@, UOffset +Str -- Byte +Str,
    +Str.dup! Str.byte@)

def(offset-in-bounds, UOffset USize -- Bool,
    dip(>Int 0) >Int in-range)

def(Str.byte@, UOffset Str -- Byte,
    with-data(
        dip(over) offset-in-bounds if(
            unsafe(uoffset @Byte),
            drop2 BNUL
        )
    ))

def(+Str.empty?, +Str -- Bool +Str,
    +Str -> dup +Str Str.empty?)

def(+Str.clear!, +Str -- +Str,
    +Str -> drop "" +Str)

def(+Str.push-byte-unsafe!, Byte +Str -- +Str,
    to-str-unsafe push-str!)

def(+Str.push-byte-ascii!, Byte +Str -- +Str,
    to-ascii-str unwrap push-str!)

||| Append the given string into STR_BUF. If the string is
||| too large it gets truncated.
def(+Str.push-str!, Str +Str -- +Str,
    +Str -> swap cat +Str)

inline:def(Str.with-data(f), (*a Ptr USize -- *b) *a Str -- *b,
    dup dip(dup dip(prim-str-base) num-bytes f) drop)

||| Traverse string bytes.
inline:def(Str.bytes-for(f), (*a Byte -- *a) *a Str -- *a,
    with-data(>Nat repeat(
        dup dip(unsafe(@Byte) f 1u >UOffset) unsafe(uoffset)
    ) drop))

def(Str.first-byte, Str -- Maybe(Byte),
    with-data(
        0> if(
            unsafe(@Byte) Some,
            drop None
        )
    ))

||| Drop n bytes from the beginning of a string.
||| This is unsafe because it can break UTF-8.
||| Returns an empty string if the string has fewer than n bytes.
def(Str.drop-bytes, USize Str +Unsafe -- Str +Unsafe,
    with-data(
        dip(over) swap - >USize(
            dip(dip(>UOffset) uoffset)
            Str.ClonePtrSlice,
            drop3 ""
        )
    ))

def(Str.from-bytes-unsafe, List(Byte) -- Str,
    dup len >USize +Buffer.new
    dip(0u >UOffset) for(>U8 over !U8 1+) drop
    base size unsafe(Str.ClonePtrSlice)
    rdrop)

inline:def(Str.with-cstr(f), (*a CStr -- *b) *a Str -- *b,
    with-data(
        >UOffset over unsafe(uoffset @U8) >Int
        0= else("tried to use Str as CStr, but Str is not zero terminated" panic!)
        CPtr CStr f
    ))

def(clamp-slice-offset-size, UOffset USize USize -- UOffset USize,
    swap dip(tuck >UOffset min tuck >USize -)
    swap >USize(min, drop2 0u >USize))

def(Str.slice, UOffset USize Str +Unsafe -- Str +Unsafe,
    with-data(
        swap dip(clamp-slice-offset-size)
        swap dip(uoffset) Str.ClonePtrSlice
    ))

def(Str.drop-slice, UOffset Str +Unsafe -- Str +Unsafe,
    rdip:thaw +Str.drop-slice rdip:rdrop)

def(Str.take-slice, USize Str +Unsafe -- Str +Unsafe,
    rdip:thaw +Str.take-slice rdip:rdrop)

def(+Str.slice, UOffset USize +Str +Unsafe -- Str +Str +Unsafe,
    rdip(+Str.dup!) slice)

def +Str.offset-slice [ UOffset UOffset +Str +Unsafe -- Str +Str +Unsafe ] {
    over - >USize( +Str.slice, drop2 "" )
}

def(+Str.take-slice, USize +Str +Unsafe -- Str +Str +Unsafe,
    0u >UOffset swap +Str.slice)

def(+Str.drop-slice, UOffset +Str +Unsafe -- Str +Str +Unsafe,
    rdip(+Str.num-bytes? ) +Str.slice)

||| Find the first occurence of a string in the str buf, returning its index.
def +Str.find [ Str +Str -- Maybe(UOffset) +Str ] {
    None swap 0u while(
        over num-bytes over >USize +
        +Str.num-bytes? <= if(
            dup2 >UOffset over num-bytes unsafe(+Str.slice) == if(
                rotl drop dup >UOffset Some rotr False,
                True
            ),
            False
        ),
        1+
    ) drop2
}

inline:
def(+Str.split-byte(p), (*a Byte -- *a Bool) *a +Str -- *a List+(Str) +Str,
    LIST+( 0u >UOffset dup while(
        dup rdip(+Str.num-bytes?) >UOffset <,
        tuck dip2(rdip(byte@ rdip(p))) rotl if(
            tuck rdip:unsafe(+Str.offset-slice)
            ; 1+ dup,
            1+
        )
    ) rdip:unsafe(+Str.offset-slice) ;+))

def(+Str.split-byte, Byte +Str -- List+(Str) +Str,
    split-byte(over ==) nip)

inline:
def(Str.split-byte(p), (*a Byte -- *a Bool) *a Str -- *a List+(Str),
    thaw +Str.split-byte(p) rdrop)

def(Str.split-byte, Byte Str -- List+(Str),
    thaw +Str.split-byte rdrop)

inline:def(+Str.find-last-byte(p), (*a Byte -- *a Bool) *a +Str -- *a Maybe(UOffset) +Str,
    num-bytes? >UOffset None while(
        dup none? and(over 0>),
        drop pred sip(byte@ rdip(p)) swap if(dup Some, None)
    ) nip)

inline:def(Str.split-last-byte(p), (*a Byte -- *a Bool) *a Str -- *a Str Maybe(Str),
    thaw find-last-byte(p) freeze swap map(unsafe(
        swap dup2 dip:1+ drop-slice
        dip(dip:>USize take-slice)
    )))

def(+Str.push-show-byte!, +Str escape-hex:Bool Byte -- escape-hex:Bool +Str,
    BQUOTE -> "\\\"" ; False !escape-hex,
    B'\' -> "\\\\" ; False !escape-hex,
    BLF -> "\n" ; False !escape-hex,
    BCR -> "\r" ; False !escape-hex,
    BHT -> "\t" ; False !escape-hex,
    _ ->
        dup BSPACE B'~' in-range and(@escape-hex and(dup is-hexdigit) not) if(
            push-byte-ascii!
            False !escape-hex,
            "\\x" ; to-hexdigits
            at2:push-byte-ascii!
            True !escape-hex
        )
    )

def(Str.show;, Str +Str -- +Str,
    "\"" ; False >escape-hex bytes-for(push-show-byte!) escape-hex> drop "\"" ;)
