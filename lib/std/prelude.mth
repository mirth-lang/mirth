module std.prelude

import std.maybe
import std.str
import std.byte

############################################
# not re-exported because of optimisations #
############################################
# def(dip, (*a -- *b) *a c -- *b c, prim.core.dip)
# def(if, (*a -- *b, *a -- *b) *a Bool -- *b, prim.core.if)
# def(while, (*a -- *a Bool, *a -- *a) *a -- *a, prim.core.while)
############################################

##################################################
# not re-exported because they're special syntax #
##################################################
# match
# lambda
##################################################

# Re-exported primitives.
alias(id, prim-id)
alias(??, prim-debug)
alias(swap, prim-swap)
alias(dup, prim-dup)
alias(drop, prim-drop)
alias(run, prim-run)

alias(rswap, prim-rswap)
alias(rdip(f), prim-rdip)
alias(dip(f), prim-dip)
alias(while(f,g), prim-while)

alias(Int.+, prim-int-add)
alias(Int.-, prim-int-sub)
alias(Int.*, prim-int-mul)
alias(Int./, prim-int-div)
alias(Int.%, prim-int-mod)
alias(Int.&, prim-int-and)
alias(Int.|, prim-int-or)
alias(Int.^, prim-int-xor)
alias(Int.<<, prim-int-shl)
alias(Int.>>, prim-int-shr)
inline:def(Int./%, Int Int -- Int Int, dup2 % dip(/))

alias(F32.+, prim-f32-add)
alias(F32.-, prim-f32-sub)
alias(F32.*, prim-f32-mul)
alias(F32./, prim-f32-div)
alias(F32.>F64, prim-f32-to-f64)
alias(F32.>Int, prim-f32-to-int)
alias(F32.>Str, prim-f32-to-str)
inline def F32.show [ F32 -- Str ] { Str(show;) }
inline def F32.show; [ +Str F32 -- +Str ] { >Str ; " >F32"; }

alias(F64.+, prim-f64-add)
alias(F64.-, prim-f64-sub)
alias(F64.*, prim-f64-mul)
alias(F64./, prim-f64-div)
alias(F64.>F32, prim-f64-to-f32)
alias(F64.>Int, prim-f64-to-int)
alias(F64.>Str, prim-f64-to-str)
inline def F64.show [ F64 -- Str ] { >Str }
inline def F64.show; [ +Str F64 -- +Str ] { >Str ; }

inline(
    alias(Bool.if(f,g), prim-if)
    alias(Bool.&&, prim-bool-and)
    alias(Bool.||, prim-bool-or)
    alias(Bool.not, prim-bool-not)
    def(Bool.or(p),  (*a -- *a Bool) *a Bool -- *a Bool, if(True, p))
    def(Bool.and(p), (*a -- *a Bool) *a Bool -- *a Bool, if(p, False))
    def(Bool.==, Bool Bool -- Bool, if(id, not))
    def(Bool.then(f), (*a -- *a) *a Bool -- *a, if(f, id))
    def(Bool.else(f), (*a -- *a) *a Bool -- *a, if(id, f))

    def or(f,g) [ (*x a -- *x Bool, *x a -- *x Bool) *x a -- *x Bool ] {
        dup dip(f) swap if(drop True, g)
    }
    def and(f,g) [ (*x a -- *x Bool, *x a -- *x Bool) *x a -- *x Bool ] {
        dup dip(f) swap if(g, drop False)
    }

    def while(f) [ (*x -- *x Bool) *x -- *x ] {
        True while(dup, drop f) drop
    }
)
def(Bool.show, Bool -- Str, if("True", "False"))
def(Bool.show;, Bool +Str -- +Str, show ;)

# Re-export Bool unqualified.
alias(True,  Bool.True)
alias(False, Bool.False)

# Cute booleans.
alias(Yes, True )
alias(No , False)

# Aligned booleans.
alias(Yea, True )
alias(Nay, False)

data OS {
    0 Unknown
    1 Windows
    2 Linux
    3 Mac
    --
    inline {
        def Min [ OS ] { 0 OS.from-enum-value-unsafe }
        def Max [ OS ] { 3 OS.from-enum-value-unsafe }
        def +World.running-os [ +World -- +World OS ] { prim-sys-os >OS }
        def >Int [ OS -- Int ] { enum-value }
        def == [ OS OS -- Bool ] { on2:>Int == }
    }
    def Int.>OS [ Int -- OS ] {
        dup OS.Min OS.Max on2:>Int in-range if(
            OS.from-enum-value-unsafe,
            drop OS.Unknown
        )
    }
    def is-windows? { OS.Windows == }
    def is-linux? { OS.Linux == }
    def is-macos? { OS.Mac == }
    def path-separator {
        is-windows? if("\\", "/")
    }
    def byte-is-path-separator? [ Byte OS -- Bool ] {
        { Windows -> dup Byte.B'/' == swap Byte.B'\' == || }
        { Linux -> Byte.B'/' == }
        { Mac -> Byte.B'/' == }
        { Unknown -> Byte.B'/' == }
    }
}

data Arch {
    0 Unknown
    1 I386
    2 AMD64
    3 ARM64
    --
    inline {
        def Min [ Arch ] { 0 Arch.from-enum-value-unsafe }
        def Max [ Arch ] { 3 Arch.from-enum-value-unsafe }
        def +World.running-arch [ +World -- +World Arch ] { prim-sys-arch >Arch }
        def >Int [ Arch -- Int ] { enum-value }
        def == [ Arch Arch -- Bool ] { on2:>Int == }
    }
    def Int.>Arch [ Int -- Arch ] {
        dup Arch.Min Arch.Max on2:>Int in-range if(
            Arch.from-enum-value-unsafe,
            drop Arch.Unknown
        )
    }
}

alias(argc, prim-sys-argc)
alias(argv, prim-sys-argv)

inline {
    def rotr [ a b c -- c a b ] { swap dip(swap) }
    def rotl [ a b c -- b c a ] { dip(swap) swap }

    def rot4r [ a b c d -- d a b c ] { swap dip(rotr) }
    def rot4l [ a b c d -- b c d a ] { dip(rotl) swap }

    def rrotr [ +a +b +c -- +c +a +b ] { rswap rdip(rswap) }
    def rrotl [ +a +b +c -- +b +c +a ] { rdip(rswap) rswap }

    def rrot4r [ +a +b +c +d -- +d +a +b +c ] { rswap rdip(rrotr) }
    def rrot4l [ +a +b +c +d -- +b +c +d +a ] { rdip(rrotl) rswap }

    def swap2 [ a b c d -- c d a b ] { dip(rotr) rotr }

    def over  [ a b           -- a b           a ] { dip(dup)   swap }
    def over2 [ a b1 b2       -- a b1 b2       a ] { dip(over)  swap }
    def over3 [ a b1 b2 b3    -- a b1 b2 b3    a ] { dip(over2) swap }
    def over4 [ a b1 b2 b3 b4 -- a b1 b2 b3 b4 a ] { dip(over3) swap }

    def tuck [ a b -- b a b ] { dup dip(swap) }

    def nip [ a b -- b ] { dip(drop) }

    def dup2 [ a b -- a b a b ] { over over }
    def dup3 [ a b c -- a b c a b c ] { dup dip(dip(dup2) rotr) }

    def dip'(f) [ (*a x -- *b y) *a c x -- *b c y ] { swap dip(f) swap }
    def dip2(f) [ (*a -- *b) *a c1 c2 -- *b c1 c2 ] { dip(dip(f)) }
    def dip3(f) [ (*a -- *b) *a c1 c2 c3 -- *b c1 c2 c3 ] { dip(dip2(f)) }
    def dip4(f) [ (*a -- *b) *a c1 c2 c3 c4 -- *b c1 c2 c3 c4 ] { dip(dip3(f)) }
    def dip5(f) [ (*a -- *b) *a c1 c2 c3 c4 c5 -- *b c1 c2 c3 c4 c5 ] { dip(dip4(f)) }

    def rdip'(f) [ (*a +r1 -- *b +r2) *a +c +r1 -- *b +c +r2 ] { rswap rdip(f) rswap }
    def rdip2(f) [ (*a -- *b) *a +c1 +c2 -- *b +c1 +c2 ] { rdip(rdip(f)) }
    def rdip3(f) [ (*a -- *b) *a +c1 +c2 +c3 -- *b +c1 +c2 +c3 ] { rdip(rdip2(f)) }

    def sip(f)  [ (*a x     -- *b) *a x     -- *b x     ] { dup  dip(f)  }
    def sip2(f) [ (*a x1 x2 -- *b) *a x1 x2 -- *b x1 x2 ] { dup2 dip2(f) }

    def both(f) [ (a -- b) a a -- b b ] { dip(f) f }

    def on2(f,g) [
        ( *a x1 -- *b x2
        , *b y1 -- *c y2
        ) *a x1 y1 -- *c x2 y2 ]
        { dip(f) dip'(g) }
    def on3(f,g,h) [
        ( *a x1 -- *b x2
        , *b y1 -- *c y2
        , *c z1 -- *d z2
        ) *a x1 y1 z1 -- *d x2 y2 z2 ]
        { dip:on2(f,g) dip':dip'(h) }
    def on4(f,g,h,i) [
        ( *a x1 -- *b x2
        , *b y1 -- *c y2
        , *c z1 -- *d z2
        , *d w1 -- *e w2
        ) *a x1 y1 z1 w1 -- *e x2 y2 z2 w2 ]
        { dip:on3(f,g,h) dip':dip':dip'(i) }

    def on2(f) [ (*a x -- *a y) *a x x     -- *a y y     ] { on2(f,f)     }
    def on3(f) [ (*a x -- *a y) *a x x x   -- *a y y y   ] { on3(f,f,f)   }
    def on4(f) [ (*a x -- *a y) *a x x x x -- *a y y y y ] { on4(f,f,f,f) }

    def drop2 [ a b         -- ] { drop drop                     }
    def drop3 [ a b c       -- ] { drop drop drop                }
    def drop4 [ a b c d     -- ] { drop drop drop drop           }
    def drop5 [ a b c d e   -- ] { drop drop drop drop drop      }
    def drop6 [ a b c d e f -- ] { drop drop drop drop drop drop }

    def rdrop2 (rd1 {rdrop}, rd2 {rdrop}) [ ( +a --, +b -- ) +a +b -- ] { rd2 rd1 }
    def rdrop3 (rd1 {rdrop}, rd2 {rdrop}, rd3 {rdrop}) [ ( +a --, +b --, +c -- ) +a +b +c -- ] { rd3 rd2 rd1 }

    def flip3 [ a b c   --   c b a ] { >x1 >x2 >x3     x1> x2> x3>     }
    def flip4 [ a b c d -- d c b a ] { >x1 >x2 >x3 >x4 x1> x2> x3> x4> }

    def repeat(f)    [ (*a -- *a)     *a Nat -- *a ] { while(dup 0>, dip(f) pred) drop }
    def count(f)     [ (*a Nat -- *a) *a Nat -- *a ] { 0u  swap repeat(dup dip(f) 1+)   drop }
    def countdown(f) [ (*a Nat -- *a) *a Nat -- *a ] { dup swap repeat(pred dup dip(f)) drop }
}

data Comparison {
    -1 LT
    0 EQ
    +1 GT
    --
    inline {
        def eq? [ Comparison -- Bool ] { enum-value 0 == }
        def lt? [ Comparison -- Bool ] { enum-value 0 <  }
        def le? [ Comparison -- Bool ] { enum-value 0 <= }
        def ge? [ Comparison -- Bool ] { enum-value 0 >  }
        def gt? [ Comparison -- Bool ] { enum-value 0 >= }
        def ne? [ Comparison -- Bool ] { enum-value 0 <> }
        def == [ Comparison Comparison -- Bool ] { on2:enum-value == }
        def <> [ Comparison Comparison -- Bool ] { on2:enum-value <> }
        def <  [ Comparison Comparison -- Bool ] { on2:enum-value <  }
        def <= [ Comparison Comparison -- Bool ] { on2:enum-value <= }
        def >  [ Comparison Comparison -- Bool ] { on2:enum-value >  }
        def >= [ Comparison Comparison -- Bool ] { on2:enum-value >= }
        def compare [ Comparison Comparison -- Comparison ] { on2:enum-value compare }
        def show; [ +Str Comparison -- +Str ] { show ; }
    }
    def show [ Comparison -- Str ] { LT -> "LT", EQ -> "EQ", GT -> "GT" }
}

def ==2(f{==}, g{==}) [ (*x a a -- *x Bool, *x b b -- *x Bool) *x a b a b -- *x Bool ] {
    dip(swap) dip2(f) rotl if(g, drop2 False)
}
def <>2(f{<>}, g{<>}) [ (*x a a -- *x Bool, *x b b -- *x Bool) *x a b a b -- *x Bool ] {
    dip(swap) dip2(f) rotl if(drop2 True, g)
}
def compare2(f{compare}, g{compare}) [
    (*x a a -- *x Comparison, *x b b -- *x Comparison )
    *x a b a b -- *x Comparison
] {
    dip(swap) dip2(f) rotl match {
        { EQ -> g }
        { _ -> dip(drop2) }
    }
}

alias(Int.==, prim-int-eq)
alias(Int.< , prim-int-lt)
alias(Int.<=, prim-int-le)
alias(Int.> , prim-int-gt)
alias(Int.>=, prim-int-ge)
alias(Int.<>, prim-int-ne)

inline def Int.in-range [ Int Int Int -- Bool ] { dip(over dip(>=)) <= && }
def Int.compare [ Int Int -- Comparison ] {
    dup2 == if(drop2 Comparison.EQ,
        < if(Comparison.LT, Comparison.GT)
    )
}
def Int.max     [ Int Int     -- Int ] { dup2 < if(nip, drop) }
def Int.min     [ Int Int     -- Int ] { dup2 < if(drop, nip) }
def Int.clamp   [ Int Int Int -- Int ] { dip(max) min }

inline {
    alias(F64.==, prim-f64-eq)
    alias(F64.<, prim-f64-lt)
    def(F64.>, F64 F64 -- Bool, swap <)
    def(F64.<=, F64 F64 -- Bool, swap < not)
    def(F64.>=, F64 F64 -- Bool, < not)
    def(F64.<>, F64 F64 -- Bool, == not)

    alias(F32.==, prim-f32-eq)
    alias(F32.<, prim-f32-lt)
    def(F32.>, F32 F32 -- Bool, swap <)
    def(F32.<=, F32 F32 -- Bool, swap < not)
    def(F32.>=, F32 F32 -- Bool, < not)
    def(F32.<>, F32 F32 -- Bool, == not)
}

inline {
    def(Int.show, Int -- Str, prim-int-to-str)
    def(Int.show;, Int +Str -- +Str, show ;)
    def(Int.>F64, Int -- F64, prim-int-to-f64)

    def Str.compare [ Str Str -- Comparison ] { prim-str-cmp Comparison.from-enum-value-unsafe }
    alias(Str.==, prim-str-eq)
    alias(Str.<>, prim-str-ne)
    alias(Str.<,  prim-str-lt)
    alias(Str.<=, prim-str-le)
    alias(Str.>,  prim-str-gt)
    alias(Str.>=, prim-str-ge)
}
def(Str.max, Str Str -- Str, dup2 < if(nip, drop))
def(Str.min, Str Str -- Str, dup2 < if(drop, nip))

inline {
    def Int.negate [ Int -- Int ] { -1 * }
    def Int.1-  [ Int -- Int  ] { 1 - }
    def Int.1+  [ Int -- Int  ] { 1 + }
    def Int.2*  [ Int -- Int  ] { 2 * }
    def Int.2/  [ Int -- Int  ] { 2 / }
    def Int.0=  [ Int -- Bool ] { 0 == }
    def Int.0<  [ Int -- Bool ] { 0 < }
    def Int.0>  [ Int -- Bool ] { 0 > }
    def Int.0<= [ Int -- Bool ] { 0 <= }
    def Int.0>= [ Int -- Bool ] { 0 >= }
    def Int.0<> [ Int -- Bool ] { 0 <> }

    def Int.succ [ Int -- Int ] { 1+ }
    def Int.pred [ Int -- Int ] { 1- }

    ||| Nullable pointer.
    data Ptr? { Ptr?-unsafe [ Ptr ] }
    def Ptr?.Null [ -- Ptr? ] { prim-ptr-nil Ptr?.Ptr?-unsafe }
    def Ptr?.null? [ Ptr? -- Bool ] { Ptr?-unsafe -> prim-ptr-nil == }
    def Ptr?.== [ Ptr? Ptr? -- Bool ] { both(/Ptr?-unsafe) == }
    def Ptr?.if?(f,g) [ (*a Ptr -- *b, *a -- *b) *a Ptr? -- *b ] {
        dup null? if(drop g, /Ptr?-unsafe f)
    }
    def Ptr?.for(f) [ (*a Ptr -- *a) *a Ptr? -- *a ] { if?(f, id) }
    def Ptr?.unwrap(f) [ (*a -- *a Ptr) *a Ptr? -- *a Ptr ] { if?(id, f) }
    def Ptr?.>Maybe [ Ptr? -- Maybe(Ptr) ] { if?(Some, None) }

    def Ptr.== [ Ptr Ptr -- Bool ] { prim-ptr-eq }
    def Ptr.>Ptr? [ Ptr -- Ptr? ] { Ptr?.Ptr?-unsafe }

    data(+Unsafe, +Unsafe)
    def unsafe(f) [ (*a +Unsafe -- *b +Unsafe) *a -- *b ] {
        +Unsafe f /+Unsafe
    }

    ||| Add byte offset to Ptr.
    def Ptr.ioffset [ IOffset Ptr +Unsafe -- Ptr +Unsafe ] { dip(>Int) prim-ptr-add }
    def Ptr.uoffset [ UOffset Ptr +Unsafe -- Ptr +Unsafe ] { dip(>Int) prim-ptr-add }

    def Ptr.@Ptr [ Ptr +Unsafe -- Ptr +Unsafe ] { prim-ptr-get }
    def Ptr.!Ptr [ Ptr Ptr +Unsafe -- +Unsafe ] { prim-ptr-set }

    def Ptr.@U8  [ Ptr +Unsafe -- U8  +Unsafe ] { prim-u8-get  }
    def Ptr.@U16 [ Ptr +Unsafe -- U16 +Unsafe ] { prim-u16-get }
    def Ptr.@U32 [ Ptr +Unsafe -- U32 +Unsafe ] { prim-u32-get }
    def Ptr.@U64 [ Ptr +Unsafe -- U64 +Unsafe ] { prim-u64-get }
    def Ptr.@I8  [ Ptr +Unsafe -- I8  +Unsafe ] { prim-i8-get  }
    def Ptr.@I16 [ Ptr +Unsafe -- I16 +Unsafe ] { prim-i16-get }
    def Ptr.@I32 [ Ptr +Unsafe -- I32 +Unsafe ] { prim-i32-get }
    def Ptr.@I64 [ Ptr +Unsafe -- I64 +Unsafe ] { prim-i64-get }
    def Ptr.@F32 [ Ptr +Unsafe -- F32 +Unsafe ] { prim-f32-get }
    def Ptr.@F64 [ Ptr +Unsafe -- F64 +Unsafe ] { prim-f64-get }

    def Ptr.!U8  [ U8  Ptr +Unsafe -- +Unsafe ] { prim-u8-set  }
    def Ptr.!U16 [ U16 Ptr +Unsafe -- +Unsafe ] { prim-u16-set }
    def Ptr.!U32 [ U32 Ptr +Unsafe -- +Unsafe ] { prim-u32-set }
    def Ptr.!U64 [ U64 Ptr +Unsafe -- +Unsafe ] { prim-u64-set }
    def Ptr.!I8  [ I8  Ptr +Unsafe -- +Unsafe ] { prim-i8-set  }
    def Ptr.!I16 [ I16 Ptr +Unsafe -- +Unsafe ] { prim-i16-set }
    def Ptr.!I32 [ I32 Ptr +Unsafe -- +Unsafe ] { prim-i32-set }
    def Ptr.!I64 [ I64 Ptr +Unsafe -- +Unsafe ] { prim-i64-set }
    def Ptr.!F32 [ F32 Ptr +Unsafe -- +Unsafe ] { prim-f32-set }
    def Ptr.!F64 [ F64 Ptr +Unsafe -- +Unsafe ] { prim-f64-set }

    def Ptr.alloc       [ USize +Unsafe -- Ptr +Unsafe ] { >Int prim-ptr-alloc   }
    def Ptr.realloc [ Ptr USize +Unsafe -- Ptr +Unsafe ] { >Int prim-ptr-realloc }
    def Ptr.free    [ Ptr +Unsafe -- +Unsafe ] { prim-ptr-free }
    def Ptr.copy    [ Ptr USize Ptr +Unsafe -- +Unsafe ] { dip(>Int) prim-ptr-copy }
    def Ptr.fill    [ U8 USize Ptr +Unsafe -- +Unsafe ] { dip(>Int dip(>Int)) prim-ptr-fill }
    def Ptr.erase   [ USize Ptr +Unsafe -- +Unsafe ] { dip2(0u8) fill }
}

inline {
    def I8.>Int [ I8  -- Int ] { Int>I8-unsafe -> id }

    def Int.>I8-if(f,g) [ (*a I8 -- *b, *a Int -- *b) *a Int -- *b ] {
        dup I8.MIN I8.MAX on2:>Int in-range if(>I8-unsafe f, g)
    }
    def Int.>I8-else(f) [ (*a Int -- *a I8 ) *a Int -- *a I8 ] { >I8-if(id,f) }
    def Int.>I8? [ Int -- Maybe(I8) ] { >I8-if(Some, drop None) }
    def Int.>I8-clamp [ Int -- I8 ] { I8.MIN I8.MAX on2:>Int clamp >I8-unsafe }
    def Int.>I8-unsafe [ Int -- I8 ] { I8.Int>I8-unsafe }

    def  0i8 [ I8 ] {  0 >I8-unsafe }
    def  1i8 [ I8 ] {  1 >I8-unsafe }
    def  2i8 [ I8 ] {  2 >I8-unsafe }
    def -1i8 [ I8 ] { -1 >I8-unsafe }
    def -2i8 [ I8 ] { -2 >I8-unsafe }

    def I8.compare [ I8 I8 -- Comparison ] { on2:>Int compare }
    def I8.== [ I8 I8 -- Bool ] { on2:>Int == }
    def I8.<  [ I8 I8 -- Bool ] { on2:>Int <  }
    def I8.<= [ I8 I8 -- Bool ] { on2:>Int <= }
    def I8.>  [ I8 I8 -- Bool ] { on2:>Int >  }
    def I8.>= [ I8 I8 -- Bool ] { on2:>Int >= }
    def I8.<> [ I8 I8 -- Bool ] { on2:>Int <> }

    def I8.MIN [ I8 ] { -0x80 >I8-unsafe }
    def I8.MAX [ I8 ] {  0x7F >I8-unsafe }
    def I8.succ [ I8 -- I8 ] { dup I8.MAX < then(>Int 1+ >I8-unsafe) }
    def I8.pred [ I8 -- I8 ] { dup I8.MIN > then(>Int 1- >I8-unsafe) }
}

inline {
    def I16.>Int [ I16  -- Int ] { Int>I16-unsafe -> id }

    def Int.>I16-if(f,g) [ (*a I16 -- *b, *a Int -- *b) *a Int -- *b ] {
        dup I16.MIN I16.MAX on2:>Int in-range if(>I16-unsafe f, g)
    }
    def Int.>I16-else(f) [ (*a Int -- *a I16 ) *a Int -- *a I16 ] { >I16-if(id,f) }
    def Int.>I16? [ Int -- Maybe(I16) ] { >I16-if(Some, drop None) }
    def Int.>I16-clamp [ Int -- I16 ] { I16.MIN I16.MAX on2:>Int clamp >I16-unsafe }
    def Int.>I16-unsafe [ Int -- I16 ] { I16.Int>I16-unsafe }

    def  0i16 [ I16 ] {  0 >I16-unsafe }
    def  1i16 [ I16 ] {  1 >I16-unsafe }
    def  2i16 [ I16 ] {  2 >I16-unsafe }
    def -1i16 [ I16 ] { -1 >I16-unsafe }
    def -2i16 [ I16 ] { -2 >I16-unsafe }

    def I16.compare [ I16 I16 -- Comparison ] { on2:>Int compare }
    def I16.== [ I16 I16 -- Bool ] { on2:>Int == }
    def I16.<  [ I16 I16 -- Bool ] { on2:>Int <  }
    def I16.<= [ I16 I16 -- Bool ] { on2:>Int <= }
    def I16.>  [ I16 I16 -- Bool ] { on2:>Int >  }
    def I16.>= [ I16 I16 -- Bool ] { on2:>Int >= }
    def I16.<> [ I16 I16 -- Bool ] { on2:>Int <> }

    def I16.MIN [ I16 ] { -0x8000 >I16-unsafe }
    def I16.MAX [ I16 ] {  0x7FFF >I16-unsafe }
    def I16.succ [ I16 -- I16 ] { dup I16.MAX < then(>Int 1+ >I16-unsafe) }
    def I16.pred [ I16 -- I16 ] { dup I16.MIN > then(>Int 1- >I16-unsafe) }
}

inline (
    def I32.>Int [ I32  -- Int ] { Int>I32-unsafe -> id }

    def Int.>I32-if(f,g) [ (*a I32 -- *b, *a Int -- *b) *a Int -- *b ] {
        dup I32.MIN I32.MAX on2:>Int in-range if(>I32-unsafe f, g)
    }
    def Int.>I32-else(f) [ (*a Int -- *a I32 ) *a Int -- *a I32 ] { >I32-if(id,f) }
    def Int.>I32? [ Int -- Maybe(I32) ] { >I32-if(Some, drop None) }
    def Int.>I32-clamp [ Int -- I32 ] { I32.MIN I32.MAX on2:>Int clamp >I32-unsafe }
    def Int.>I32-unsafe [ Int -- I32 ] { I32.Int>I32-unsafe }

    def  0i32 [ I32 ] {  0 >I32-unsafe }
    def  1i32 [ I32 ] {  1 >I32-unsafe }
    def  2i32 [ I32 ] {  2 >I32-unsafe }
    def -1i32 [ I32 ] { -1 >I32-unsafe }
    def -2i32 [ I32 ] { -2 >I32-unsafe }

    def I32.compare [ I32 I32 -- Comparison ] { on2:>Int compare }
    def I32.== [ I32 I32 -- Bool ] { on2:>Int == }
    def I32.<  [ I32 I32 -- Bool ] { on2:>Int <  }
    def I32.<= [ I32 I32 -- Bool ] { on2:>Int <= }
    def I32.>  [ I32 I32 -- Bool ] { on2:>Int >  }
    def I32.>= [ I32 I32 -- Bool ] { on2:>Int >= }
    def I32.<> [ I32 I32 -- Bool ] { on2:>Int <> }

    def I32.MIN [ I32 ] { -0x80000000 >I32-unsafe }
    def I32.MAX [ I32 ] {  0x7FFFFFFF >I32-unsafe }
    def I32.succ [ I32 -- I32 ] { dup I32.MAX < then(>Int 1+ >I32-unsafe) }
    def I32.pred [ I32 -- I32 ] { dup I32.MIN > then(>Int 1- >I32-unsafe) }
)

inline (
    def I64.>Int [ I64 -- Int ] { Int>I64-unsafe -> id }

    def Int.>I64-clamp  [ Int -- I64 ] { >I64-unsafe }
    def Int.>I64-unsafe [ Int -- I64 ] { I64.Int>I64-unsafe }

    def  0i64 [ I64 ] {  0 >I64-unsafe }
    def  1i64 [ I64 ] {  1 >I64-unsafe }
    def  2i64 [ I64 ] {  2 >I64-unsafe }
    def -1i64 [ I64 ] { -1 >I64-unsafe }
    def -2i64 [ I64 ] { -2 >I64-unsafe }

    def I64.compare [ I64 I64 -- Comparison ] { on2:>Int compare }
    def I64.== [ I64 I64 -- Bool ] { on2:>Int == }
    def I64.<  [ I64 I64 -- Bool ] { on2:>Int <  }
    def I64.<= [ I64 I64 -- Bool ] { on2:>Int <= }
    def I64.>  [ I64 I64 -- Bool ] { on2:>Int >  }
    def I64.>= [ I64 I64 -- Bool ] { on2:>Int >= }
    def I64.<> [ I64 I64 -- Bool ] { on2:>Int <> }

    def I64.MIN [ I64 ] { -0x7FFFFFFFFFFFFFFF 1- >I64-unsafe }
    def I64.MAX [ I64 ] {  0x7FFFFFFFFFFFFFFF    >I64-unsafe }
    def I64.succ [ I64 -- I64 ] { dup I64.MAX < then(>Int 1+ >I64-unsafe) }
    def I64.pred [ I64 -- I64 ] { dup I64.MIN > then(>Int 1- >I64-unsafe) }
)

inline (
    def U8.>Int  [ U8  -- Int ] { Int>U8-unsafe -> id }

    def Int.>U8-if(f,g) [ (*a U8 -- *b, *a Int -- *b ) *a Int -- *b ] {
        dup 0 255 in-range if(>U8-unsafe f, g)
    }
    def Int.>U8-else(f) [ (*a Int -- *a U8 ) *a Int -- *a U8 ] { >U8-if(id, f) }
    def Int.>U8? [ Int -- Maybe(U8) ] { >U8-if(Some, drop None) }
    def Int.>U8-wrap   [ Int -- U8 ] { 255 & >U8-unsafe }
    def Int.>U8-clamp  [ Int -- U8 ] { 0 255 clamp >U8-unsafe }
    def Int.>U8-unsafe [ Int -- U8 ] { U8.Int>U8-unsafe }

    def 0u8 [ U8 ] { 0 >U8-unsafe }
    def 1u8 [ U8 ] { 1 >U8-unsafe }
    def 2u8 [ U8 ] { 2 >U8-unsafe }
    def 3u8 [ U8 ] { 3 >U8-unsafe }

    def U8.& [ U8 U8 -- U8 ] { on2:>Int & >U8-unsafe }
    def U8.| [ U8 U8 -- U8 ] { on2:>Int | >U8-unsafe }
    def U8.^ [ U8 U8 -- U8 ] { on2:>Int ^ >U8-unsafe }

    def U8.compare [ U8 U8 -- Comparison ] { on2:>Int compare }
    def U8.== [ U8 U8 -- Bool ] { on2:>Int == }
    def U8.<  [ U8 U8 -- Bool ] { on2:>Int <  }
    def U8.<= [ U8 U8 -- Bool ] { on2:>Int <= }
    def U8.>  [ U8 U8 -- Bool ] { on2:>Int >  }
    def U8.>= [ U8 U8 -- Bool ] { on2:>Int >= }
    def U8.<> [ U8 U8 -- Bool ] { on2:>Int <> }
    def U8.0= [ U8 -- Bool ] { >Int 0= }
    def U8.0> [ U8 -- Bool ] { >Int 0> }

    def U8.MAX  [ U8 ] { 255 >U8-unsafe }
    def U8.MIN  [ U8 ] { 0 >U8-unsafe }
    def U8.succ [ U8 -- U8 ] { dup U8.MAX < then(>Int 1+ >U8-unsafe) }
    def U8.pred [ U8 -- U8 ] { dup 0> then(>Int 1- >U8-unsafe) }
)

inline (
    def U16.>Int  [ U16  -- Int ] { Int>U16-unsafe -> id }

    def Int.>U16-if(f,g) [ (*a U16 -- *b, *a Int -- *b ) *a Int -- *b ] {
        dup 0 0xFFFF in-range if(>U16-unsafe f, g)
    }
    def Int.>U16-else(f) [ (*a Int -- *a U16 ) *a Int -- *a U16 ] { >U16-if(id, f) }
    def Int.>U16? [ Int -- Maybe(U16) ] { >U16-if(Some, drop None) }
    def Int.>U16-wrap   [ Int -- U16 ] { 0xFFFF & >U16-unsafe }
    def Int.>U16-clamp  [ Int -- U16 ] { 0 0xFFFF clamp >U16-unsafe }
    def Int.>U16-unsafe [ Int -- U16 ] { U16.Int>U16-unsafe }

    def 0u16 [ U16 ] { 0 >U16-unsafe }
    def 1u16 [ U16 ] { 1 >U16-unsafe }
    def 2u16 [ U16 ] { 2 >U16-unsafe }
    def 3u16 [ U16 ] { 3 >U16-unsafe }

    def U16.& [ U16 U16 -- U16 ] { on2:>Int & >U16-unsafe }
    def U16.| [ U16 U16 -- U16 ] { on2:>Int | >U16-unsafe }
    def U16.^ [ U16 U16 -- U16 ] { on2:>Int ^ >U16-unsafe }

    def U16.compare [ U16 U16 -- Comparison ] { on2:>Int compare }
    def U16.== [ U16 U16 -- Bool ] { on2:>Int == }
    def U16.<  [ U16 U16 -- Bool ] { on2:>Int <  }
    def U16.<= [ U16 U16 -- Bool ] { on2:>Int <= }
    def U16.>  [ U16 U16 -- Bool ] { on2:>Int >  }
    def U16.>= [ U16 U16 -- Bool ] { on2:>Int >= }
    def U16.<> [ U16 U16 -- Bool ] { on2:>Int <> }
    def U16.0= [ U16 -- Bool ] { >Int 0= }
    def U16.0> [ U16 -- Bool ] { >Int 0> }

    def U16.MAX  [ U16 ] { 0xFFFF >U16-unsafe }
    def U16.MIN  [ U16 ] { 0 >U16-unsafe }
    def U16.succ [ U16 -- U16 ] { dup U16.MAX < then(>Int 1+ >U16-unsafe) }
    def U16.pred [ U16 -- U16 ] { dup 0> then(>Int 1- >U16-unsafe) }
)

inline (
    def U32.>Int  [ U32  -- Int ] { Int>U32-unsafe -> id }

    def Int.>U32-if(f,g) [ (*a U32 -- *b, *a Int -- *b ) *a Int -- *b ] {
        dup 0 U32.MAX >Int in-range if(>U32-unsafe f, g)
    }
    def Int.>U32-else(f) [ (*a Int -- *a U32 ) *a Int -- *a U32 ] { >U32-if(id, f) }
    def Int.>U32? [ Int -- Maybe(U32) ] { >U32-if(Some, drop None) }
    def Int.>U32-wrap   [ Int -- U32 ] { U32.MAX >Int & >U32-unsafe }
    def Int.>U32-clamp  [ Int -- U32 ] { 0 U32.MAX >Int clamp >U32-unsafe }
    def Int.>U32-unsafe [ Int -- U32 ] { U32.Int>U32-unsafe }

    def 0u32 [ U32 ] { 0 >U32-unsafe }
    def 1u32 [ U32 ] { 1 >U32-unsafe }
    def 2u32 [ U32 ] { 2 >U32-unsafe }
    def 3u32 [ U32 ] { 3 >U32-unsafe }

    def U32.& [ U32 U32 -- U32 ] { on2:>Int & >U32-unsafe }
    def U32.| [ U32 U32 -- U32 ] { on2:>Int | >U32-unsafe }
    def U32.^ [ U32 U32 -- U32 ] { on2:>Int ^ >U32-unsafe }

    def U32.compare [ U32 U32 -- Comparison ] { on2:>Int compare }
    def U32.== [ U32 U32 -- Bool ] { on2:>Int == }
    def U32.<  [ U32 U32 -- Bool ] { on2:>Int <  }
    def U32.<= [ U32 U32 -- Bool ] { on2:>Int <= }
    def U32.>  [ U32 U32 -- Bool ] { on2:>Int >  }
    def U32.>= [ U32 U32 -- Bool ] { on2:>Int >= }
    def U32.<> [ U32 U32 -- Bool ] { on2:>Int <> }
    def U32.0= [ U32 -- Bool ] { >Int 0= }
    def U32.0> [ U32 -- Bool ] { >Int 0> }

    def U32.MAX  [ U32 ] { 0xFFFFFFFF >U32-unsafe }
    def U32.MIN  [ U32 ] { 0 >U32-unsafe }
    def U32.succ [ U32 -- U32 ] { dup U32.MAX < then(>Int 1+ >U32-unsafe) }
    def U32.pred [ U32 -- U32 ] { dup 0> then(>Int 1- >U32-unsafe) }
    def U32.show; [ U32 +Str -- +Str ] { >Nat show; }
)

inline (
    def Int.>U64-unsafe [ Int -- U64 ] { U64.Int>U64-unsafe }
    def Int.>U64& [ Int -- U64 ] { >U64-unsafe }
    def Int.>U64? [ Int -- Maybe(U64) ] { >U64(Some, drop None) }
    def Int.>U64(f) [ (*a Int -- *a U64) *a Int -- *a U64 ] { >U64(id, f) }
    def Int.>U64(f,g) [ (*a U64 -- *b, *a Int -- *b) *a Int -- *b ] {
        dup 0< if(g, >U64-unsafe f)
    }
    def Int.u64 [ Int -- U64 ] { 0 max >U64-unsafe }

    def U64.>Int-unsafe [ U64 -- Int ] { Int>U64-unsafe -> id }
    def U64.>Int? [ U64 -- Maybe(Int) ] { >Int(Some, drop None) }
    def U64.>Int(f) [ ( *a U64 -- *a Int ) *a U64 -- *a Int ] { >Int(id, f) }
    def U64.>Int(f,g) [ ( *a Int -- *b, *a U64 -- *b ) *a U64 -- *b ] {
        dup >Int-unsafe 0< if(g, >Int-unsafe f)
    }

    def U64.MAX [ U64 ] { -1 >U64-unsafe }
    def U64.MIN [ U64 ] {  0 >U64-unsafe }

    def U64.| [ U64 U64 -- U64 ] { on2:>Int-unsafe | >U64-unsafe }
    def U64.& [ U64 U64 -- U64 ] { on2:>Int-unsafe & >U64-unsafe }
    def U64.^ [ U64 U64 -- U64 ] { on2:>Int-unsafe ^ >U64-unsafe }

    def U64.== [ U64 U64 -- Bool ] { on2:>Int-unsafe == }
    def U64.<> [ U64 U64 -- Bool ] { on2:>Int-unsafe <> }
)

#######
# NAT #
#######

data Nat {
    NatUnsafe [ Int ]
    --
    inline {
        def Int.>Nat-if(f,g) [ ( *a Nat -- *b, *a Int -- *b ) *a Int -- *b ] { dup 0< if(g, Nat.NatUnsafe f) }
        def Int.>Nat-else(f) [ ( *a Int -- *a Nat ) *a Int -- *a Nat ] { dup 0< if(f, Nat.NatUnsafe) }
        def Int.>Nat-clamp [ Int -- Nat ] { >Nat-else(drop 0u) }

        def >Int [ Nat -- Int ] { /NatUnsafe }
        def >U8?  [ Nat -- Maybe(U8)  ] { >Int >U8?  }
        def >U16? [ Nat -- Maybe(U16) ] { >Int >U16? }
        def >U32? [ Nat -- Maybe(U32) ] { >Int >U32? }
        def >U64  [ Nat -- U64 ] { >Int >U64-unsafe }
        def >UIndex [ Nat -- UIndex ] { UIndex }
        def >ISize [ Nat -- ISize ] { >Int >ISize }
        def >USize [ Nat -- USize ] { USize }
        def >IOffset [ Nat -- IOffset ] { >Int >IOffset }
        def >UOffset [ Nat -- UOffset ] { UOffset }

        def >U8-wrap  [ Nat -- U8  ] { >Int >U8-wrap  }
        def >U16-wrap [ Nat -- U16 ] { >Int >U16-wrap }
        def >U32-wrap [ Nat -- U32 ] { >Int >U32-wrap }
        def >U64-wrap [ Nat -- U64 ] { >Int >U64-unsafe }

        def +  [ Nat Nat -- Nat ] { both(>Int) + Nat.NatUnsafe }
        def -  [ Nat Nat -- Int ] { both(>Int) - }
        def *  [ Nat Nat -- Nat ] { both(>Int) * Nat.NatUnsafe }
        def /  [ Nat Nat -- Nat ] { both(>Int) / Nat.NatUnsafe }
        def %  [ Nat Nat -- Nat ] { both(>Int) % Nat.NatUnsafe }
        def /% [ Nat Nat -- Nat Nat ] { both(>Int) /% both(Nat.NatUnsafe) }

        def &  [ Nat Nat -- Nat ] { dip(>Int) >Int prim-int-and Nat.NatUnsafe }
        def |  [ Nat Nat -- Nat ] { dip(>Int) >Int prim-int-or  Nat.NatUnsafe }
        def ^  [ Nat Nat -- Nat ] { dip(>Int) >Int prim-int-xor Nat.NatUnsafe }
        def << [ Nat Nat -- Nat ] { dip(>Int) >Int prim-int-shl Nat.NatUnsafe }
        def >> [ Nat Nat -- Nat ] { dip(>Int) >Int prim-int-shr Nat.NatUnsafe }

        def 1+ [ Nat -- Nat ] { >Int 1+ Nat.NatUnsafe }
        def 1- [ Nat -- Int ] { >Int 1- }
        def 2* [ Nat -- Nat ] { >Int 2* Nat.NatUnsafe }
        def 2/ [ Nat -- Nat ] { >Int 2/ Nat.NatUnsafe }

        def succ [ Nat -- Nat ] { 1+ }
        def pred  [ Nat -- Nat ] { 1- >Nat-clamp }
        def minus [ Nat Nat -- Nat ] { - >Nat-clamp }

        def compare [ Nat Nat -- Comparison ] { dip(>Int) >Int compare }
        def == [ Nat Nat -- Bool ] { dip(>Int) >Int == }
        def <  [ Nat Nat -- Bool ] { dip(>Int) >Int <  }
        def >  [ Nat Nat -- Bool ] { dip(>Int) >Int >  }
        def <= [ Nat Nat -- Bool ] { dip(>Int) >Int <= }
        def >= [ Nat Nat -- Bool ] { dip(>Int) >Int >= }
        def <> [ Nat Nat -- Bool ] { dip(>Int) >Int <> }
        def 0> [ Nat -- Bool ] { >Int 0> }
        def 0= [ Nat -- Bool ] { >Int 0= }
        def max [ Nat Nat -- Nat ] { on2:>Int max Nat.NatUnsafe }
        def min [ Nat Nat -- Nat ] { on2:>Int min Nat.NatUnsafe }
        def clamp [ Nat Nat Nat -- Nat ] { on3:>Int clamp Nat.NatUnsafe }
        def show [ Nat -- Str ] { >Int show }
        def show; [ Nat +Str -- +Str ] { >Int show; }
    }
}

inline {
    def 0u [ Nat ] { 0 Nat.NatUnsafe }
    def 1u [ Nat ] { 1 Nat.NatUnsafe }
    def 2u [ Nat ] { 2 Nat.NatUnsafe }
    def 3u [ Nat ] { 3 Nat.NatUnsafe }
    def 4u [ Nat ] { 4 Nat.NatUnsafe }
    def 5u [ Nat ] { 5 Nat.NatUnsafe }
    def 6u [ Nat ] { 6 Nat.NatUnsafe }
    def 7u [ Nat ] { 7 Nat.NatUnsafe }
    def 8u [ Nat ] { 8 Nat.NatUnsafe }
    def 9u [ Nat ] { 9 Nat.NatUnsafe }
    def 10u [ Nat ] { 10 Nat.NatUnsafe }
    def 11u [ Nat ] { 11 Nat.NatUnsafe }
    def 12u [ Nat ] { 12 Nat.NatUnsafe }
    def 13u [ Nat ] { 13 Nat.NatUnsafe }
    def 14u [ Nat ] { 14 Nat.NatUnsafe }
    def 15u [ Nat ] { 15 Nat.NatUnsafe }
    def 16u [ Nat ] { 16 Nat.NatUnsafe }
    def 24u [ Nat ] { 24 Nat.NatUnsafe }
    def 32u [ Nat ] { 32 Nat.NatUnsafe }
    def 64u [ Nat ] { 64 Nat.NatUnsafe }
    def 128u [ Nat ] { 128 Nat.NatUnsafe }
    def 256u [ Nat ] { 256 Nat.NatUnsafe }

    def U8.>Nat  [ U8  -- Nat ] { >Int Nat.NatUnsafe }
    def U16.>Nat [ U16 -- Nat ] { >Int Nat.NatUnsafe }
    def U32.>Nat [ U32 -- Nat ] { >Int Nat.NatUnsafe }
    # def U64.>Nat [ U64 -- Nat ] { >Int-unsafe Nat.NatUnsafe }
}

||| Index into an array-like structure. Non-negative.
struct UIndex {
    Nat
    --
    inline {
        def Int.>UIndex-if(f,g) [ ( *a UIndex -- *b, *a Int -- *b ) *a Int -- *b ] { >Nat-if(UIndex f, g) }
        def Int.>UIndex-else(f) [ ( *a Int -- *a UIndex ) *a Int -- *a UIndex ] { >UIndex-if(id, f) }
        def Int.>UIndex-clamp [ Int -- UIndex ] { >Nat-clamp >UIndex }

        def >Nat   [ UIndex -- Nat   ] { /UIndex }
        def >Int   [ UIndex -- Int   ] { /UIndex >Int }
        def >USize [ UIndex -- USize ] { >Nat >USize }

        def +  [ UIndex UIndex -- UIndex ] { dip:>Nat >Nat + >UIndex }
        def 1+ [ UIndex -- UIndex ] { >Nat 1+ >UIndex }
        def 2* [ UIndex -- UIndex ] { >Nat 2* >UIndex }
        def 2/ [ UIndex -- UIndex ] { >Nat 2/ >UIndex }
        def == [ UIndex UIndex -- Bool ] { dip(>Nat) >Nat == }
        def <  [ UIndex UIndex -- Bool ] { dip(>Nat) >Nat <  }
        def >  [ UIndex UIndex -- Bool ] { dip(>Nat) >Nat >  }
        def <= [ UIndex UIndex -- Bool ] { dip(>Nat) >Nat <= }
        def >= [ UIndex UIndex -- Bool ] { dip(>Nat) >Nat >= }
        def <> [ UIndex UIndex -- Bool ] { dip(>Nat) >Nat <> }
        def 0= [ UIndex -- Bool ] { >Nat 0= }
        def 0> [ UIndex -- Bool ] { >Nat 0> }
        def show [ UIndex -- Str ] { >Int show }
        def show; [ UIndex +Str -- +Str ] { >Int show; }
        def max [ UIndex UIndex -- UIndex ] { on2:>Nat max >UIndex }
        def min [ UIndex UIndex -- UIndex ] { on2:>Nat min >UIndex }
        def clamp [ UIndex UIndex UIndex -- UIndex ] { on3:>Nat clamp >UIndex }
    }
}

||| Size in bytes. Can be negative.
struct ISize {
    Int
    --
    inline {
        def Int.>ISize [ Int -- ISize ] { ISize }

        def >Int [ ISize -- Int ] { /ISize }
        def >Nat-if     (f,g) [ ( *a Nat -- *b,     *a ISize -- *b ) *a ISize -- *b ] { >Int >Nat-if(f, >ISize g) }
        def >UIndex-if  (f,g) [ ( *a UIndex  -- *b, *a ISize -- *b ) *a ISize -- *b ] { >Nat-if(>UIndex  f, g) }
        def >USize-if   (f,g) [ ( *a USize   -- *b, *a ISize -- *b ) *a ISize -- *b ] { >Nat-if(>USize   f, g) }
        def >UOffset-if (f,g) [ ( *a UOffset -- *b, *a ISize -- *b ) *a ISize -- *b ] { >Nat-if(>UOffset f, g) }

        def +  [ ISize ISize -- ISize ] { both(>Int) + >ISize }
        def -  [ ISize ISize -- ISize ] { both(>Int) - >ISize }
        def 1+ [ ISize -- ISize ] { >Int 1+ >ISize }
        def 1- [ ISize -- ISize ] { >Int 1- >ISize }
        def == [ ISize ISize -- Bool ] { dip:>Int >Int == }
        def >  [ ISize ISize -- Bool ] { dip:>Int >Int >  }
        def <  [ ISize ISize -- Bool ] { dip:>Int >Int <  }
        def >= [ ISize ISize -- Bool ] { dip:>Int >Int >= }
        def <= [ ISize ISize -- Bool ] { dip:>Int >Int <= }
        def <> [ ISize ISize -- Bool ] { dip:>Int >Int <> }
        def 0= [ ISize -- Bool ] { >Int 0= }
        def 0< [ ISize -- Bool ] { >Int 0< }
        def 0> [ ISize -- Bool ] { >Int 0> }
        def show [ ISize -- Str ] { >Int show }

        def max [ ISize ISize -- ISize ] { on2:>Int max >ISize }
        def min [ ISize ISize -- ISize ] { on2:>Int min >ISize }
        def clamp [ ISize ISize ISize -- ISize ] { on3:>Int clamp >ISize }
    }
}

||| Size in bytes. Non-negative.
struct USize {
    Nat
    --
    inline {
        def Int.>USize-if (f,g) [ (*a USize -- *b, *a Int -- *b) *a Int -- *b ] { >Nat-if(USize f, g) }
        def Int.>USize-else (f) [ (*a Int -- *a USize) *a Int -- *a USize ] { >USize-if(id, f) }
        def Int.>USize-clamp [ Int -- USize ] { >Nat-clamp >USize }

        def >Nat [ USize -- Nat ] { /USize }
        def >Int [ USize -- Int ] { >Nat >Int }
        def >ISize [ USize -- ISize ] { >Int >ISize }
        def >UOffset [ USize -- UOffset ] { >Nat >UOffset }
        def >UIndex  [ USize -- UIndex  ] { >Nat >UIndex }

        def +  [ USize USize -- USize ] { dip(>Nat) >Nat + >USize }
        def -  [ USize USize -- ISize ] { dip(>Nat) >Nat - >ISize }
        def *  [ UIndex USize -- UOffset ] { dip(>Nat) >Nat * >UOffset }
        def 1+ [ USize -- USize ] { >Nat 1+ >USize }
        def 1- [ USize -- ISize ] { >Nat 1- >ISize }
        def 2* [ USize -- USize ] { >Nat 2* >USize }
        def 2/ [ USize -- USize ] { >Nat 2/ >USize }
        def == [ USize USize -- Bool ] { dip(>Nat) >Nat == }
        def <  [ USize USize -- Bool ] { dip(>Nat) >Nat <  }
        def >  [ USize USize -- Bool ] { dip(>Nat) >Nat >  }
        def <= [ USize USize -- Bool ] { dip(>Nat) >Nat <= }
        def >= [ USize USize -- Bool ] { dip(>Nat) >Nat >= }
        def <> [ USize USize -- Bool ] { dip(>Nat) >Nat <> }
        def 0= [ USize -- Bool ] { >Nat 0= }
        def 0> [ USize -- Bool ] { >Nat 0> }
        def show [ USize -- Str ] { >Int show }
        def show; [ USize +Str -- +Str ] { >Nat show; }


        def succ [ USize -- USize ] { >Nat succ >USize }
        def pred [ USize -- USize ] { >Nat pred >USize }
        def minus [ USize USize -- USize ] { on2:>Nat minus >USize }

        def max [ USize USize -- USize ] { dip(>Nat) >Nat max >USize }
        def min [ USize USize -- USize ] { dip(>Nat) >Nat min >USize }
    }
}

inline {
    def |U8|  [ USize ] { 1u >USize }
    def |U16| [ USize ] { 2u >USize }
    def |U32| [ USize ] { 4u >USize }
    def |U64| [ USize ] { 8u >USize }
    def |I8|  [ USize ] { 1u >USize }
    def |I16| [ USize ] { 2u >USize }
    def |I32| [ USize ] { 4u >USize }
    def |I64| [ USize ] { 8u >USize }
    def |F32| [ USize ] { 4u >USize }
    def |F64| [ USize ] { 8u >USize }
    def |Ptr| [ USize ] { prim-ptr-size >Nat-clamp >USize }
}

||| Offset in bytes. Can be negative.
struct IOffset { Int }
inline(
    def Int.>IOffset [ Int -- IOffset ] { IOffset }

    def IOffset.>Int   [ IOffset -- Int   ] { /IOffset }
    def IOffset.>Nat-if     (f,g) [ ( *a Nat     -- *b, *a IOffset -- *b ) *a IOffset -- *b ] { /IOffset >Nat-if(f, IOffset g) }
    def IOffset.>UIndex-if  (f,g) [ ( *a UIndex  -- *b, *a IOffset -- *b ) *a IOffset -- *b ] { >Nat-if(>UIndex  f, g) }
    def IOffset.>USize-if   (f,g) [ ( *a USize   -- *b, *a IOffset -- *b ) *a IOffset -- *b ] { >Nat-if(>USize   f, g) }
    def IOffset.>UOffset-if (f,g) [ ( *a UOffset -- *b, *a IOffset -- *b ) *a IOffset -- *b ] { >Nat-if(>UOffset f, g) }

    def IOffset.+  [ IOffset IOffset -- IOffset ] { both(>Int) + >IOffset }
    def IOffset.-  [ IOffset IOffset -- IOffset ] { both(>Int) - >IOffset }
    def IOffset.1+ [ IOffset -- IOffset ] { >Int 1+ >IOffset }
    def IOffset.1- [ IOffset -- IOffset ] { >Int 1- >IOffset }
    def IOffset.== [ IOffset IOffset -- Bool ] { dip:>Int >Int == }
    def IOffset.>  [ IOffset IOffset -- Bool ] { dip:>Int >Int >  }
    def IOffset.<  [ IOffset IOffset -- Bool ] { dip:>Int >Int <  }
    def IOffset.>= [ IOffset IOffset -- Bool ] { dip:>Int >Int >= }
    def IOffset.<= [ IOffset IOffset -- Bool ] { dip:>Int >Int <= }
    def IOffset.<> [ IOffset IOffset -- Bool ] { dip:>Int >Int <> }
    def IOffset.0= [ IOffset -- Bool ] { >Int 0= }
    def IOffset.0< [ IOffset -- Bool ] { >Int 0< }
    def IOffset.0> [ IOffset -- Bool ] { >Int 0> }
    def IOffset.show [ IOffset -- Str ] { >Int show }

    def IOffset.max [ IOffset IOffset -- IOffset ] { dip(>Int) >Int max >IOffset }
    def IOffset.min [ IOffset IOffset -- IOffset ] { dip(>Int) >Int min >IOffset }
)

||| Offset in bytes. Non-negative.
struct UOffset {
    Nat
    --
    inline {
        def Int.>UOffset-if (f,g) [ (*a UOffset -- *b, *a Int -- *b) *a Int -- *b ] { >Nat-if(UOffset f, g) }
        def Int.>UOffset-else (f) [ (*a Int -- *a UOffset) *a Int -- *a UOffset ] { >UOffset-if(id, f) }
        def Int.>UOffset-clamp [ Int -- UOffset ] { >Nat-clamp UOffset }

        def >Nat [ UOffset -- Nat ] { /UOffset }
        def >Int [ UOffset -- Int ] { >Nat >Int }
        def >ISize [ UOffset -- ISize ] { >Int >ISize }
        def >USize [ UOffset -- USize ] { >Nat >USize }
        def >IOffset [ UOffset -- IOffset ] { >Nat >IOffset }

        def +  [ UOffset UOffset -- UOffset ] { dip(>Nat) >Nat + >UOffset }
        def -  [ UOffset UOffset -- ISize   ] { dip(>Nat) >Nat - >ISize   }
        def 1+ [ UOffset -- UOffset ] { >Nat 1+ >UOffset }
        def 1- [ UOffset -- IOffset ] { >Nat 1- >IOffset }
        def 2* [ UOffset -- UOffset ] { >Nat 2* >UOffset }
        def 2/ [ UOffset -- UOffset ] { >Nat 2/ >UOffset }
        def == [ UOffset UOffset -- Bool ] { dip(>Nat) >Nat == }
        def <  [ UOffset UOffset -- Bool ] { dip(>Nat) >Nat <  }
        def >  [ UOffset UOffset -- Bool ] { dip(>Nat) >Nat >  }
        def <= [ UOffset UOffset -- Bool ] { dip(>Nat) >Nat <= }
        def >= [ UOffset UOffset -- Bool ] { dip(>Nat) >Nat >= }
        def <> [ UOffset UOffset -- Bool ] { dip(>Nat) >Nat <> }
        def 0= [ UOffset -- Bool ] { >Nat 0= }
        def 0> [ UOffset -- Bool ] { >Nat 0> }
        def show [ UOffset -- Str ] { >Int show }

        def succ [ UOffset -- UOffset ] { >Nat succ >UOffset }
        def pred [ UOffset -- UOffset ] { >Nat pred >UOffset }
        def minus [ UOffset UOffset -- UOffset ] { on2:>Nat minus >UOffset }

        def max [ UOffset UOffset -- UOffset ] { on2:>Nat max >UOffset }
        def min [ UOffset UOffset -- UOffset ] { on2:>Nat min >UOffset }
        def clamp [ UOffset UOffset UOffset -- UOffset ] { on3:>Nat clamp >UOffset }
    }
}

##########
# TUPLES #
##########

inline(
    def(pack0,           -- [         ], prim-tup-pack0)
    def(pack1, a         -- [a        ], prim-tup-pack1)
    def(pack2, a b       -- [a b      ], prim-tup-pack2)
    def(pack3, a b c     -- [a b c    ], prim-tup-pack3)
    def(pack4, a b c d   -- [a b c d  ], prim-tup-pack4)
    def(pack5, a b c d e -- [a b c d e], prim-tup-pack5)

    def(unpack0, [         ] --          , prim-tup-unpack0)
    def(unpack1, [a        ] -- a        , prim-tup-unpack1)
    def(unpack2, [a b      ] -- a b      , prim-tup-unpack2)
    def(unpack3, [a b c    ] -- a b c    , prim-tup-unpack3)
    def(unpack4, [a b c d  ] -- a b c d  , prim-tup-unpack4)
    def(unpack5, [a b c d e] -- a b c d e, prim-tup-unpack5)

    def(pack0==, [] [] -- Bool, drop2 False)
    def(pack1==(aeq{==}), (a a -- Bool) [a] [a] -- Bool,
        on2:unpack1 aeq)
    def(pack2==(aeq{==},beq{==}), (a a -- Bool, b b -- Bool) [a b] [a b] -- Bool,
        dip:unpack2 unpack2 dip:swap
        beq if(aeq, drop2 False))
    def(pack3==(aeq{==},beq{==},ceq{==}), (a a -- Bool, b b -- Bool, c c -- Bool)
            [a b c] [a b c] -- Bool,
        dip:unpack3 unpack3 dip2:rotr dip:swap
        ceq if(beq if(aeq, drop2 False), drop4 False))
    def(pack4==(aeq{==},beq{==},ceq{==},deq{==}),
            (a a -- Bool, b b -- Bool, c c -- Bool, d d -- Bool)
            [a b c d] [a b c d] -- Bool,
        dip:unpack4 unpack4 dip3:rot4r dip2:rotr dip:swap
        deq if(ceq if(beq if(aeq, drop2 False), drop4 False), drop2 drop4 False))

    def(pack2-show;-contents(f{show;},g{show;}), (a +Str -- +Str, b +Str -- +Str) [a b] +Str -- +Str,
        unpack2 dip(f) " " ; g)
    def(pack3-show;-contents(f{show;},g{show;},h{show;}), (a +Str -- +Str, b +Str -- +Str, c +Str -- +Str) [a b c] +Str -- +Str,
        unpack3 dip2(f) " " ; dip(g) " " ; h)
    def(pack4-show;-contents(f{show;},g{show;},h{show;},i{show;}),
        (a +Str -- +Str, b +Str -- +Str, c +Str -- +Str, d +Str -- +Str) [a b c d] +Str -- +Str,
        unpack4 dip3(f) " " ; dip2(g) " " ; dip(h) " " ; i)

    def(pack2-show;(f{show;},g{show;}), (a +Str -- +Str, b +Str -- +Str) [a b] +Str -- +Str,
        pack2-show;-contents(f,g) " pack2" ;)
    def(pack3-show;(f{show;},g{show;},h{show;}), (a +Str -- +Str, b +Str -- +Str, c +Str -- +Str) [a b c] +Str -- +Str,
        pack3-show;-contents(f,g,h) " pack3" ;)
    def(pack4-show;(f{show;},g{show;},h{show;},i{show;}), (a +Str -- +Str, b +Str -- +Str, c +Str -- +Str, d +Str -- +Str) [a b c d] +Str -- +Str,
        pack4-show;-contents(f,g,h,i) " pack4" ;)
)

inline(
    alias(@, prim-mut-get)
    alias(!, prim-mut-set)
    alias(mut-is-set, prim-mut-is-set)
    def(modify(f), (*a t -- *b t) *a Mut(t) -- *b, dup dip(@ f) !)
    def(memoize(f), (*a -- *a t) *a Mut(t) -- *a t,
        dup mut-is-set if(
            @,
            dip(f dup) !
        ))
)
def(@?, Mut(t) -- Maybe(t),
    dup mut-is-set if(@ Some, drop None))

inline:def(panic!, *a Str -- *b, prim-panic)

def(impossible!, *a -- *b, "Impossible! The impossible has occured!" panic!)
def(expect!(f,g), (*a -- *a Bool, *a -- *b Str) *a -- *a,
    f if(id, g panic!))
def(assert!(f,g), (*a -- *a Bool, *a -- *b Str) *a -- *a,
    expect!(f,g "assertion failed: " swap prim-str-cat))
