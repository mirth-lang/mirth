module std.maybe

import std.prelude
import std.list
import std.str

data Maybe(t) {
    0 None
    1 Some [ t ]
    --
    inline (
        def len [ Maybe(t) -- Nat ] { enum-value NatUnsafe }
        def none? [ Maybe(t) -- Bool ] { enum-value 0 == }
        def some? [ Maybe(t) -- Bool ] { enum-value 1 == }
        def >Bool [ Maybe(t) -- Bool ] { some? }
        def not [ Maybe(t) -- Bool ] { none? }
    )
    def if(f,g) { some? if(f,g) }
    def then(f) { some? then(f) }
    def else(f) { none? then(f) }
    def and(f) { some? and(f) }
    def or(f) { some? or(f) }

    def if-some(f,g)  { { Some -> f    } { None -> g } }
    def or-some(f)    { { Some -> Some } { None -> f } }
    def unwrap(f)     { { Some -> id   } { None -> f } }
    def >List {
        { None -> List.L0 }
        { Some -> List.L1 }
    }

    def == (eq {==}) [ (*c a a -- *c Bool) *c Maybe(a) Maybe(a) -- *c Bool ] {
        { None -> none? }
        { Some -> swap if-some(swap eq, drop False) }
    }

    def compare (cmp {compare}) [ (*c a a -- *c Comparison) *c Maybe(a) Maybe(a) -- *c Comparison ] {
        { None -> match {
            { None -> EQ }
            { Some -> drop GT }
        } }
        { Some -> swap match {
            { None -> drop LT }
            { Some -> swap cmp }
        }}
    }

    def map(f)  { { Some -> f Some } { None -> None  } }
    def bind(f) { { Some -> f      } { None -> None  } }
    def for(f)  { { Some -> f      } { None -> id    } }
    def has(f)  { { Some -> f      } { None -> False } }
    def all(f)  { { Some -> f      } { None -> True  } }

    def filter(f) [ (*a b -- *a Bool) *a Maybe(b) -- *a Maybe(b) ] {
        { Some -> dup dip(f) swap if(Some, drop None) }
        { None -> None }
    }

    def map2(f) [ (*c x y -- *c z) *c Maybe(x) Maybe(y) -- *c Maybe(z) ] {
        { None -> drop None }
        { Some ->
            swap match {
                { None -> drop None }
                { Some -> swap f Some }
            }
        }
    }

    def zip [ Maybe(x) Maybe(y) -- Maybe([x y]) ] { map2(pack2) }

    def show;(f {show;}) [ (t +Str -- +Str) Maybe(t) +Str -- +Str ] {
        { None -> "None" ; }
        { Some -> f " Some" ; }
    }

    def show(f {show;}) [ (t +Str -- +Str) Maybe(t) -- Str ] {
        Str( show;(f) )
    }
}

def while-some(f,g) [ (*a -- *a Maybe(b), *a b -- *a) *a -- *a ] {
    while(f if-some(g True, False))
}

def while-none(f,g) [ (*a -- *a Maybe(b), *a -- *a) *a -- *a b ] {
    None while(drop f dup if(False, dip:g True))
    unwrap(impossible!)
}
