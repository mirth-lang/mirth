module(std.output)

import(std.prelude)
import(std.str)
import(std.byte)
import(std.buffer)
import(std.file)

def(OUTPUT_BUFFER_SIZE, Size, 4096 >Size)
data(+Output, +Output ->
    size: Size
    +file: +File
    +buffer: +Buffer)

def(+Output.start!, +File -- +Output,
    >+file
    0 >Size >size
    OUTPUT_BUFFER_SIZE +Buffer.new >+buffer
    +Output)

def(+Output.end!, +Output -- +File,
    flush! /+Output
    size> drop
    +buffer> rdrop
    +file>)

def(+Output.offset, +Output -- Offset +Output, size >Offset)

def(+Output.flush!, +Output -- +Output,
    +buffer:base size +file:unsafe-write!
    0 >Size size!)

def(+Output.capacity-total, +Output -- Size +Output, +buffer:size)
def(+Output.capacity-remaining, +Output -- Size +Output, capacity-total size -)
def(+Output.full?, +Output -- Bool +Output, capacity-remaining 0 >Size <=)

def(+Output.put, Str +Output -- +Output,
    dup num-bytes capacity-remaining > if(
        flush! dup num-bytes capacity-total >= if(
            with-data(+file:unsafe-write!),
            put
        ),

        dup offset +buffer:!Str
        num-bytes size:+
    ))

def(+Output.put-byte, Byte +Output -- +Output,
    full? then(flush!)
    offset +buffer:!Byte
    size:1+)

def(+Output.line, +Output -- +Output, BLF put-byte)
