module std.output

import std.prelude
import std.str
import std.byte
import std.buffer
import std.file

def OUTPUT_BUFFER_SIZE [ Size ] { 4096 bytes }

struct +Output {
    size: Size
    +file: +File
    +buffer: +Buffer
    --

    def start! [ +File -- +Output ] {
        >+file
        0 bytes >size
        OUTPUT_BUFFER_SIZE +Buffer.New >+buffer
        +Output
    }

    def end! [ +Output -- +File ] {
        flush! /+Output +file> ldrop
    }

    def flush! [ +Output -- +Output ] {
        +buffer:base >ptr size >size +file:unsafe:write-bytes! drop
        0 bytes size!
    }

    def offset [ +Output -- +Output Offset ] {
        size >Offset
    }

    def capacity-total [ +Output -- Size +Output ] { +buffer:size }
    def capacity-remaining [ +Output -- Size +Output ] { capacity-total size - }
    def full? [ +Output -- Bool +Output ] { capacity-remaining 0B= }

    def put [ Str +Output -- +Output ] {
        dup num-bytes capacity-remaining > if(
            flush! dup num-bytes capacity-total >= if(
                with-data(>size >ptr +file:unsafe:write-bytes! drop),
                put
            ),

            dup offset +buffer:str!
            num-bytes size:+
        )
    }

    def put-byte [ Byte +Output -- +Output ] {
        full? then(flush!)
        offset +buffer:byte!
        size:1B+
    }

    def line [ +Output -- +Output ] {
        Byte.BLF put-byte
    }

    def put-line [ Str +Output -- +Output ] {
        put line
    }
}
