module(mirth-tests.data-labels)
import(std.prelude)
import(std.posix)

data(Person, PERSON -> age:Int name:Str)
data(Mixed, MIXED -> msg:Str Str)
data(+MyWorld, ENTER -> age:Int msg:Int name:Str +World)

def(Person.greet!, Person +World -- Person +World,
    "Hello, " trace!
    dup name trace!
    " at " trace!
    dup age trace!
    "!" trace-ln!)

def(main, +World -- +World,
    "John" >name
    30 >age
    PERSON
    dup age trace-ln!
    dup name trace-ln!
    drop

    "Hello" >msg "Goodbye"
    MIXED msg trace-ln!

    99 >age 101 >msg "me" >name
    ENTER
    "<<<" trace-ln!
    age trace-ln!
    msg trace-ln!
    name trace-ln!
    ">>>" trace-ln!
    /ENTER
    age> drop
    msg> drop
    name> drop

    "J." >name 30 >age PERSON greet!
    "Jane" swap name! greet!
    31 swap age! greet!
    drop)
# mirth-test # perr # 30
# mirth-test # perr # John
# mirth-test # perr # Hello
# mirth-test # perr # <<<
# mirth-test # perr # 99
# mirth-test # perr # 101
# mirth-test # perr # me
# mirth-test # perr # >>>
# mirth-test # perr # Hello, J. at 30!
# mirth-test # perr # Hello, Jane at 30!
# mirth-test # perr # Hello, Jane at 31!
