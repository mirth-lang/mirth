module(mirth-tests.data-labels)
import(std.prelude)
import(std.posix)
import(std.str)

data(Person, PERSON -> age:Int name:Str)
data(Mixed, MIXED -> msg:Str Str)
data(+MyWorld, +MyWorld -> age:Int msg:Int name:Str +world:+World)
data(+Deep, +Deep -> +myworld:+MyWorld)

def(Person.greet!, Person +World -- Person +World,
    print("Hello, "; dup name ; " at "; dup age show; "!";))

def(main, +World -- +World,
    "John" >name
    30 >age
    PERSON
    dup age show print
    dup name print
    drop

    "Hello" >msg "Goodbye"
    MIXED msg print

    99 >age 101 >msg "me" >name
    >+world
    +MyWorld
    "<<<" +world:print
    age show +world:print
    >+myworld +Deep
    "<<<<<" +myworld:+world:print
    +myworld:msg +myworld:+world:print:show;
    +myworld rdip(name +world rdip:print +world!) +myworld!
    +myworld:+world:print(">>>>>" ;)
    /+Deep +myworld>
    name +world:print
    ">>>" +world:print
    /+MyWorld
    +world>
    age> drop
    msg> drop
    name> drop

    "J." >name 30 >age PERSON greet!
    "Jane" swap name! greet!
    31 swap age! greet!
    drop)
# mirth-test # pout # 30
# mirth-test # pout # John
# mirth-test # pout # Hello
# mirth-test # pout # <<<
# mirth-test # pout # 99
# mirth-test # pout # <<<<<
# mirth-test # pout # 101
# mirth-test # pout # me
# mirth-test # pout # >>>>>
# mirth-test # pout # me
# mirth-test # pout # >>>
# mirth-test # pout # Hello, J. at 30!
# mirth-test # pout # Hello, Jane at 30!
# mirth-test # pout # Hello, Jane at 31!
