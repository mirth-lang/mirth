module(mirth-tests.test-prelude)
import(std.prelude)
import(std.world)

# Assertions

def(!!True, Bool --, if(id, "Assertion failed" panic!))
def(!!False, Bool --, not !!True)
def(!!=, Int Int --, = !!True)
def(!!0, Int --, 0 !!=)
def(!!1, Int --, 1 !!=)
def(!!001, Int Int Int --, 1 !!= 0 !!= 0 !!=)
def(!!010, Int Int Int --, 0 !!= 1 !!= 0 !!=)
def(!!011, Int Int Int --, 1 !!= 1 !!= 0 !!=)
def(!!100, Int Int Int --, 0 !!= 0 !!= 1 !!=)
def(!!111, Int Int Int --, 1 !!= 1 !!= 1 !!=)

# Tests

def(test-prelude!, --,
    test-if test-drop test-dup test-swap test-dip
    test-rotr test-rotl test= test< test<= test-gt test>=
    test+ test- test* test-while)
def(test-if, --,
    0 !!0 1 !!1 0 1 !!1 !!0
    True Bool.if(10,20) 10 !!=
    False Bool.if(10,20) 20 !!=)

def(test-drop, --, 0 1 drop !!0)
def(test-dup, --, 0 1 dup !!011)
def(test-swap, --, 0 1 0 swap !!001)
def(test-dip, --,
    0 0 dip(1) !!010)
def(test-rotr, --,
    1 0 0 rotr !!010
    0 1 0 rotr !!001
    0 0 1 rotr !!100)
def(test-rotl, --,
    1 0 0 rotl !!001
    0 1 0 rotl !!100
    0 0 1 rotl !!010)

def(test=, --,
    0 0 = !!True
    0 1 = !!False
    1 0 = !!False
    1 1 = !!True
    1 2 = !!False
    2 1 = !!False
    2 2 = !!True)
def(test<, --,
    0 0 < !!False
    0 1 < !!True
    1 0 < !!False
    1 1 < !!False
    1 2 < !!True
    2 1 < !!False
    2 2 < !!False)
def(test<=, --,
    0 0 <= !!True
    0 1 <= !!True
    1 0 <= !!False
    1 1 <= !!True
    1 2 <= !!True
    2 1 <= !!False
    2 2 <= !!True)
def(test-gt, --,
    0 0 > !!False
    0 1 > !!False
    1 0 > !!True
    1 1 > !!False
    1 2 > !!False
    2 1 > !!True
    2 2 > !!False)
def(test>=, --,
    0 0 >= !!True
    0 1 >= !!False
    1 0 >= !!True
    1 1 >= !!True
    1 2 >= !!False
    2 1 >= !!True
    2 2 >= !!True
    -1 0 >= !!False
    -1 -1 >= !!True
    0 -1 >= !!True
    -1 -2 >= !!True
    -2 -2 >= !!True
    -2 -1 >= !!False)

def(test+, --,
    0 0 + !!0
    0 1 + !!1
    1 0 + !!1
    1 1 + 2 = !!True
    2 2 + 4 = !!True
    2 -2 + !!0)

def(test-, --,
    0 0 - !!0
    1 0 - !!1
    1 1 - !!0
    0 1 - -1 !!=
    2 2 - !!0
    2 4 - -2 !!=
    4 2 - 2 !!=)

def(test*, --,
    0 0 * !!0
    2 1 * 2 !!=
    2 2 * 4 !!=
    2 4 * 8 !!=)

def(test-while, --,
    999 10 20 while(dup 0>, 1- dip(1+)) drop 30 !!= 999 !!=)

# Morphism quotes
def(test-morphism-quote-id, [ Int -- Int ] -- [ Int -- Int ], id)
def(test-morphism-quote-drop, [ Int -- Int ] -- , drop)
def(test-morphism-quote-dup, [ Int -- Int ] -- [ Int -- Int ] [ Int -- Int ], dup)

# Tensor quotes
def(test-tensor-quote-id, [ Int ] -- [ Int ], id)
def(test-tensor-quote-drop, [ Int ] -- , drop)
def(test-tensor-quote-dup, [ Int ] -- [ Int ] [ Int ], dup)

# Stack assertions
def(test-stack-assertions, --,
    1 2 { Int Int }
    3 4 { Int Int Int Int }
    + + + { Int }
    10 =
    { Bool }
    drop
)

def(test-stack-assertions-var, a b -- a b,
    id
    { a b }
    swap
    { b a }
    swap
    { a b }
)

def(test-stack-assertions-var-2, a b -- a b,
    swap
    { b a }
    swap
    { a b }
)

def(test-stack-assertions-dont-care, a b -- a b,
    id
    { a b }
    { a _ }
    { _ b }
    swap
    { b a }
    { b _ }
    { _ a }
    swap
    { a b }
    { a _ }
    { _ b }
)

def(test-dup2, Int Str Int Str,
    10 "hello" dup2)

def(main, +World -- +World, test-prelude!)
